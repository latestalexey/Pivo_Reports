// Сокращенные ссылки на поля табличных документов.
Перем ПТД_Титульный;
Перем ПТД_Лицензии;
Перем ПТД_Декларация;

// Идентификаторы многострочных разделов для внутренних структур.
Перем мИдГруппы1;
Перем мИдГруппы2;

Перем мСтруктураМногостраничныхРазделов Экспорт;

Перем мСтруктураМногострочныхРазделов Экспорт;
Перем мСтруктураИсхКолвоСтрокРазделов;

Перем мДеревоВыбранныхСтраниц Экспорт;

Перем мСтрокаФормата Экспорт; // Форматная строка представления денежных показателей отчета

Перем мОписаниеТиповЧислоЕ;

Перем мСтараяВерсияФормы Экспорт;

Перем мСтруктураЦветовРаскраски Экспорт;

Перем мВПрограммеИзмененаОрганизация;

Перем мПечатныеФормы Экспорт;
Перем мПечатаемыеРазделы Экспорт;

Перем мСвойстваРазделовДекларации Экспорт;

Перем мСоставПоказателей Экспорт;

Перем мДокументВосстановлен;

Перем мРежимПечати;

Перем мОписаниеТиповБулево;

Перем мБезОткрытияФормы Экспорт;

Перем мПустойОбразЛистаФЛ;

Перем ТаблицаСообщений Экспорт;

Перем мДанныеОтчета Экспорт;

Перем СвойстваПоказателей;

Перем ДополнительныеДанные; // Данные, не отображаемых на одностраничных разделах

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	РегламентированнаяОтчетность.ПередОткрытиемФормыРегламентированногоОтчета(ЭтаФорма, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Инициализация();
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	РегламентированнаяОтчетность.ВключитьОбработчикАвтосохранения(ЭтаФорма);
	
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	РегламентированнаяОтчетность.ПередЗакрытиемРегламентированногоОтчета(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ПриЗакрытии()
	
	СохранитьДеревоСтраницОтчета();
	РегламентированнаяОтчетность.ПриЗакрытииРегламентированногоОтчета(ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

Процедура ФлажокОтклАвтоРасчетПриИзменении(Элемент)
	
	Если ФлажокОтклАвтоРасчет = Ложь Тогда
		Ответ = Вопрос("Будет произведен перерасчет всех рассчитываемых показателей. Продолжить?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
		Если Ответ <> КодВозвратаДиалога.Да Тогда
			ФлажокОтклАвтоРасчет = Истина;
			Возврат;
		КонецЕсли;
		Расчет();
	КонецЕсли;
	
	РегламентированнаяОтчетность.роИзменитьТаблицуВариантовЗаполнения(ЭтаФорма, НЕ ФлажокОтклАвтоРасчет);
	
	УстановитьДоступностьПолейДекларация();
	
КонецПроцедуры

Процедура ПолеВыбораВидДокументаПриИзменении(Элемент)
	
	ВыбранКорректирующийВидДок = (ВидДокумента = 1);
	
	ЭлементыФормы.НомерКорректировки.Доступность = ВыбранКорректирующийВидДок;
	
	Если ВыбранКорректирующийВидДок Тогда
		ПТД_Титульный.Области.ВидДокументаПервичный.Значение = "";
		ПТД_Титульный.Области.ВидДокументаКорректирующий.Значение = "V";
		
	Иначе
		ПТД_Титульный.Области.ВидДокументаПервичный.Значение = "V";
		ПТД_Титульный.Области.ВидДокументаКорректирующий.Значение = "";
		
	КонецЕсли;
	
	ЗаполнитьПоляНомераКорректировки();
	
КонецПроцедуры

Процедура ПолеВыбораВидДокументаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

Процедура НомерКорректировкиПриИзменении(Элемент)
	
	ЗаполнитьПоляНомераКорректировки();
	
КонецПроцедуры

Процедура ОсновнаяПанельПриСменеСтраницы(Элемент, ТекущаяСтраница) Экспорт
	Перем ТекущаяОбласть;
	
	РегламентированнаяОтчетность.СформироватьСоставКнопкиОчиститьДляРегОтчетов(ЭтаФорма);
	
	ИмяТекущейСтраницыПанели = ЭлементыФормы.ОсновнаяПанель.Страницы[ТекущаяСтраница].Имя;
	
	Если РегламентированнаяОтчетность.роПолучитьСвойствоРаздела(ЭтаФорма, ИмяТекущейСтраницыПанели, "МногострочностьВРазделе") > 0 Тогда
		ИдГруппы = ОпределитьИдМногострочногоРазделаПоАктивнойСтранице();
		Если ИдГруппы <> Неопределено Тогда
			ТекТабличноеПоле = ОпределитьТабличноеПолеПоИдМногострочногоРаздела(ИдГруппы);
			ТекущаяОбласть = ТекТабличноеПоле.ТекущаяОбласть;
		КонецЕсли;
		// управляем доступностью кнопок "Добавить" и "Удалить"
		РегламентированнаяОтчетность.роДоступностьКнопокДобавитьУдалить(ЭтаФорма, ТекущаяОбласть, ИмяТекущейСтраницыПанели);
	КонецЕсли;
	
	Если РегламентированнаяОтчетность.роПолучитьСвойствоРаздела(ЭтаФорма, ИмяТекущейСтраницыПанели, "МногостраничностьВРазделе") Тогда
		РегламентированнаяОтчетность.роПоказатьСтатикСтраницРаздела(ЭтаФорма, ИмяТекущейСтраницыПанели);
		// Принудительная раскраска после восстановления исходной раскраски ячеек.
		УстановитьДоступностьПолейДекларация();
	КонецЕсли;
	
	Если РегламентированнаяОтчетность.роПолучитьСвойствоРаздела(ЭтаФорма, ИмяТекущейСтраницыПанели, "РазделЯвляетсяАвтозаполняемым") Тогда
		ТекущаяОбласть = ЭлементыФормы["ПолеТабличногоДокумента" + ИмяТекущейСтраницыПанели].ТекущаяОбласть;
		РегламентированнаяОтчетность.роВывестиЭлементыВариантаЗаполнения(ЭтаФорма, ТекущаяОбласть);
	КонецЕсли;
	
КонецПроцедуры // ОсновнаяПанельПриСменеСтраницы()

Процедура ПолеВыбораПризнакФормыОтчетностиОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

Процедура СтатусНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	РегламентированнаяОтчетность.НачалоВыбораСтатусаВРегламентированномОтчете(ЭтаФорма, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура СтатусПриИзменении(Элемент)
	
	РегламентированнаяОтчетность.ПриИзмененииСтатусаВРегламентированномОтчете(ЭтаФорма, Элемент);
	
КонецПроцедуры

Процедура СтатусОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

Процедура КоманднаяПанельФормыВыбратьВариантОчисткиОтчета(Кнопка)
	
	РегламентированнаяОтчетность.МеханизмыОчисткиРегламентированныхОтчетов(ЭтаФорма, Кнопка);
	
	УстановитьДоступностьПолейДекларация();
	ПронумероватьТаблицыДекларация();
	
КонецПроцедуры

Процедура КоманднаяПанельФормыВыгрузить(Кнопка)
	
	Выгрузить();
	
КонецПроцедуры

Процедура КоманднаяПанельФормыПроверить(Кнопка)
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Сообщить("Организация не выбрана! Выгрузка невозможна.",СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	Если Модифицированность И Вопрос("Перед проверкой отчета требуется сохранить введенную информацию." + Символы.ПС + "Продолжить?",РежимДиалогаВопрос.ДаНет)<>КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность И НЕ Сохранить() Тогда
		Возврат;
	КонецЕсли;
	
	НовыйДок = Документы.ВыгрузкаРегламентированныхОтчетов.СоздатьДокумент();
	СпДокОсн = Новый СписокЗначений;
	СпДокОсн.Добавить(мСохраненныйДок);
	НовыйДок.ПолучитьФорму(, ЭтаФорма).Проверить(СпДокОсн);
	
КонецПроцедуры

Процедура КоманднаяПанельФормыКнопкаНастройкаСтраниц(Кнопка)
	
	ОткрытьФормуВыбораСтраниц("ВыбратьДляНастройки");
	РегламентированнаяОтчетность.роПоказатьСтраницыОтчета(ЭтаФорма);
	
КонецПроцедуры

Процедура КоманднаяПанельФормыПоиск(Кнопка)
	
	РегламентированнаяОтчетность.роПоискВРегламентированномОтчете(ЭтаФорма);
	
КонецПроцедуры

Процедура КнопкаЗаполнитьСведенияОбОрганизацииНажатие(Элемент)
	
	мВПрограммеИзмененаОрганизация = Истина;
	ЗаполнитьСведенияОбОрганизации();
	
	мВПрограммеИзмененаОрганизация = Ложь;
	Модифицированность = Истина;
	
КонецПроцедуры

Процедура КоманднаяПанельФормыРасширитьПолеБланка(Кнопка)
	
	РегламентированнаяОтчетность.ПриНажатииНаКнопкуПереходаВРежимВводаДанных(ЭтаФорма);
	
КонецПроцедуры

Процедура КоманднаяПанельДействийСохранитьИЗакрыть(Кнопка)
	
	Если Сохранить() Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

Процедура КоманднаяПанельДействийЗаписать(Кнопка)
	
	Сохранить();
	
КонецПроцедуры

// Титульный лист.
Процедура ПолеТабличногоДокументаТитульныйВыбор(Элемент, Область, СтандартнаяОбработка)
	
	Если РегламентированнаяОтчетность.ВыбраннаяОбластьРегламентированногоОтчетаНеВерна(ЭтаФорма, Область, СтандартнаяОбработка) Тогда
		Возврат;
	КонецЕсли;
	
	ОбработкаВыбораВПолеТабличногоДокумента(Элемент, Область, СтандартнаяОбработка)
	
КонецПроцедуры

// Лицензии.
Процедура ПолеТабличногоДокументаЛицензииВыбор(Элемент, Область, СтандартнаяОбработка)
	
	ОбработкаВыбораВПолеТабличногоДокумента(Элемент, Область, СтандартнаяОбработка)
	
КонецПроцедуры

Процедура ПолеТабличногоДокументаЛицензииПриИзмененииСодержимогоОбласти(Элемент, Область)
	
	ПолеТабличногоДокументаПриИзмененииСодержимогоОбласти(Элемент, Область)
	
КонецПроцедуры

Процедура КнопкаПредыдущаяСтраницаЛицензииНажатие(Элемент)
	
	ПоказатьСтраницуМногостраничногоРаздела(-1, "Лицензии");
	
КонецПроцедуры

Процедура КнопкаСледующаяСтраницаЛицензииНажатие(Элемент)
	
	ПоказатьСтраницуМногостраничногоРаздела(1, "Лицензии");
	
КонецПроцедуры

Процедура КнопкаДобавитьДопСтраницуЛицензииНажатие(Элемент)
	
	РегламентированнаяОтчетность.роВставитьДополнительнуюСтраницу(ЭтаФорма, "Лицензии");
	
	Если мДатаКонцаПериодаОтчета >= '20131101' И Не ЛицензируемаяДеятельность() Тогда
		НомерПоследнейСтраницы = мСтруктураМногостраничныхРазделов.Лицензии.Количество() - 1;
		ПоследняяСтраница = мСтруктураМногостраничныхРазделов.Лицензии[НомерПоследнейСтраницы];
		ПерваяСтраница = мСтруктураМногостраничныхРазделов.Лицензии[0];
		ПоследняяСтраница.ДополнительныеДанные = Новый Структура;
		ПоследняяСтраница.ДополнительныеДанные.Вставить("П000000000105", ПерваяСтраница.ДополнительныеДанные.П000000000105);
		ПТД_Лицензии.Области.П000000000105.Значение = ПерваяСтраница.Данные.П000000000105;
	КонецЕсли;
	
	РасставитьНомераСтраниц();
	
КонецПроцедуры

Процедура КнопкаУдалитьДопСтраницуЛицензииНажатие(Элемент)
	
	УдалитьДополнительнуюСтраницу("Лицензии");
	РасставитьНомераСтраниц();
	
КонецПроцедуры

Процедура КнопкаПерейтиНаСтраницуЛицензииНажатие(Элемент)
	
	РегламентированнаяОтчетность.роОткрытьФормуСпискаДопСтраниц(ЭтаФорма, "Лицензии");
	
КонецПроцедуры

// Декларация.
Процедура ПолеТабличногоДокументаДекларацияВыбор(Элемент, Область, СтандартнаяОбработка)
	
	ОбработкаВыбораВПолеТабличногоДокумента(Элемент, Область, СтандартнаяОбработка)
	
КонецПроцедуры

Процедура ПолеТабличногоДокументаДекларацияПриАктивизацииОбласти(Элемент)
	
	РегламентированнаяОтчетность.роПриАктивизацииОбластиРегламентированногоОтчета(ЭтаФорма, Элемент);
	
	УстановитьДоступностьКнопокДопСтрокДекларация(Элемент);
	
КонецПроцедуры

Процедура ПолеТабличногоДокументаДекларацияПриИзмененииСодержимогоОбласти(Элемент, Область)
	
	ПолеТабличногоДокументаПриИзмененииСодержимогоОбласти(Элемент, Область)
	
КонецПроцедуры

Процедура КнопкаПредыдущаяСтраницаДекларацияНажатие(Элемент)
	
	ПоказатьСтраницуМногостраничногоРаздела(-1, "Декларация");
	
	УстановитьДоступностьПолейДекларация();
	
КонецПроцедуры

Процедура КнопкаСледующаяСтраницаДекларацияНажатие(Элемент)
	
	ПоказатьСтраницуМногостраничногоРаздела(1, "Декларация");
	
	УстановитьДоступностьПолейДекларация();
	
КонецПроцедуры

Процедура КнопкаДобавитьДопСтраницуДекларацияНажатие(Элемент)
	
	ИзменитьТаблицуПараметровАвтозаполнения(1, "Декларация");
	РегламентированнаяОтчетность.роВставитьДополнительнуюСтраницу(ЭтаФорма, "Декларация");
	
	УстановитьДоступностьПолейДекларация();
	РасчетДекларация();
	ПронумероватьТаблицыДекларация();
	РасставитьНомераСтраниц();
	
КонецПроцедуры

Процедура КнопкаУдалитьДопСтраницуДекларацияНажатие(Элемент)
	
	ИзменитьТаблицуПараметровАвтозаполнения(-1, "Декларация");
	РегламентированнаяОтчетность.роУдалитьДополнительнуюСтраницу(ЭтаФорма, "Декларация");
	
	УстановитьДоступностьПолейДекларация();
	РасставитьНомераСтраниц();
	
КонецПроцедуры

Процедура КнопкаПерейтиНаСтраницуДекларацияНажатие(Элемент)
	
	РегламентированнаяОтчетность.роОткрытьФормуСпискаДопСтраниц(ЭтаФорма, "Декларация");
	
	УстановитьДоступностьПолейДекларация();
	
КонецПроцедуры

Процедура КнопкаДобавитьДопСтрокиДекларацияНажатие(Элемент)
	
	ВставитьДополнительнуюСтроку();
	ПронумероватьТаблицыДекларация();
	
КонецПроцедуры

Процедура КнопкаУдалитьДопСтрокиДекларацияНажатие(Элемент)
	
	УдалитьДополнительнуюСтроку();
	ПронумероватьТаблицыДекларация();
	РасчетДекларация();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция ПолучитьОбособленноеПодразделение(Подразделение)
	
	Если Подразделение.ОбособленноеПодразделение Тогда
		Возврат Подразделение;
	ИначеЕсли ЗначениеЗаполнено(Подразделение.Родитель) Тогда
		Возврат ПолучитьОбособленноеПодразделение(Подразделение.Родитель);
	ИначеЕсли ЗначениеЗаполнено(Подразделение.Владелец) Тогда
		Возврат Подразделение.Владелец;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Процедура РасставитьНомераСтраниц()
	
	ПорядковыйНомер = 1;
	
	ОтображаемыйНомер = Формат(ПорядковыйНомер, "ЧЦ=3; ЧВН=");
	
	РегламентированнаяОтчетность.роПроставитьВКвадратыЗначения(ЭтаФорма, "Титульный", "НомСтр", 3, ОтображаемыйНомер);
	ПорядковыйНомер = ПорядковыйНомер + 1;
	
	РасставитьНомераСтраницВМногостраничномРазделе("Лицензии", ПорядковыйНомер);
	РасставитьНомераСтраницВМногостраничномРазделе("Декларация", ПорядковыйНомер);
	
КонецПроцедуры

Процедура РасставитьНомераСтраницВМногостраничномРазделе(ИмяРаздела, ПорядковыйНомер)
	
	Перем ТаблицаСтраниц;
	
	Если НЕ мСтруктураМногостраничныхРазделов.Свойство(ИмяРаздела, ТаблицаСтраниц) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Страница Из ТаблицаСтраниц Цикл
		ДанныеСтраницы = Страница.Данные;
		ЭтоАктивнаяСтраница = Страница.АктивнаяСтраница;
		
		ОтображаемыйНомер = Формат(ПорядковыйНомер, "ЧЦ=3; ЧВН=");
		
		Если ЭтоАктивнаяСтраница Тогда
			РегламентированнаяОтчетность.роПроставитьВКвадратыЗначения(ЭтаФорма, ИмяРаздела, "НомСтр", 3, ОтображаемыйНомер);
		Иначе
			ДанныеСтраницы.НомСтр1 = Сред(ОтображаемыйНомер, 1, 1);
			ДанныеСтраницы.НомСтр2 = Сред(ОтображаемыйНомер, 2, 1);
			ДанныеСтраницы.НомСтр3 = Сред(ОтображаемыйНомер, 3, 1);
		КонецЕсли;
		
		ПорядковыйНомер = ПорядковыйНомер + 1;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаВыбораВПолеТабличногоДокумента(Элемент, Область, СтандартнаяОбработка)
	
	ИмяОбласти = Область.Имя;
	
	Если РегламентированнаяОтчетность.ВыбраннаяОбластьРегламентированногоОтчетаНеВерна(ЭтаФорма, Область, СтандартнаяОбработка) Тогда
		Возврат;
	КонецЕсли;
	
	Если (ИмяОбласти = "ОбособленноеПодразделение") Тогда 
		ВвестиСведенияОбОП(Элемент, Область, СтандартнаяОбработка);
	КонецЕсли;
	
	ЭтоКППМестаДеятельности = (Найти(ИмяОбласти, "П000000000201") = 1);
	Если ЭтоКППМестаДеятельности Тогда
		ВвестиКППМестаДеятельности(Элемент, Область, СтандартнаяОбработка);
	КонецЕсли;
	
	ЭтоАдресМестаДеятельности = Найти(ИмяОбласти, "П000000000202") = 1
	                        ИЛИ Найти(ИмяОбласти, "П000000000203") = 1
	                        ИЛИ Найти(ИмяОбласти, "П000000000204") = 1
	                        ИЛИ Найти(ИмяОбласти, "П000000000205") = 1
	                        ИЛИ Найти(ИмяОбласти, "П000000000206") = 1
	                        ИЛИ Найти(ИмяОбласти, "П000000000207") = 1;
	Если ЭтоАдресМестаДеятельности Тогда
		ВвестиАдресМестаДеятельности(Элемент, Область, СтандартнаяОбработка);
	КонецЕсли;
	
	ЭтоАдресВМногострочнойЧасти = Найти(ИмяОбласти, "П000010000107") = 1
	                          ИЛИ Найти(ИмяОбласти, "П000010000307") = 1;
	Если ЭтоАдресВМногострочнойЧасти Тогда
		ВвестиАдресПеревозчика(Элемент, Область, СтандартнаяОбработка);
	КонецЕсли;
	
	ПараметрВводаПоля = СвойстваПоказателей.Найти(Область.Имя, "Показатель");
	Если ПараметрВводаПоля = Неопределено Тогда
		Для Каждого Стр из СвойстваПоказателей Цикл
			Если Лев(Область.Имя, СтрДлина(Стр.Показатель)) = Стр.Показатель Тогда
				ПараметрВводаПоля = Стр;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ПараметрВводаПоля <> Неопределено И ПараметрВводаПоля.ТаблицаЗначений <> Неопределено Тогда
		ИмяПоказателя = ?(РегламентированнаяОтчетность.роПоказательВходитВМногострочнуюЧасть(ЭтаФорма, ИмяОбласти) > 0, ИмяОбласти, ПараметрВводаПоля.Показатель);
		ВвестиПоказатель(Область, ИмяПоказателя, Ложь, ПараметрВводаПоля.ТекстПриВыборе, ПараметрВводаПоля.ТаблицаЗначений, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВвестиСведенияОбОП(Элемент, Область, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИмяРаздела = Сред(Элемент.Имя, 24);
	СтраницыРаздела = мСтруктураМногостраничныхРазделов[ИмяРаздела];
	
	АктивнаяСтраница = СтраницыРаздела.Найти(Истина, "АктивнаяСтраница");
	
	Если ТипЗнч(АктивнаяСтраница.ДополнительныеДанные) <> Тип("Структура") Тогда
		АктивнаяСтраница.ДополнительныеДанные = Новый Структура;
	КонецЕсли;
	
	СведенияОбОП = Новый Структура;
	СведенияОбОП.Вставить("Наименование", "");
	СведенияОбОП.Вставить("ИНН",          "");
	СведенияОбОП.Вставить("КПП",          "");
	
	СведенияОбОП.Вставить("Индекс",          "");
	СведенияОбОП.Вставить("КодРегиона",      "");
	СведенияОбОП.Вставить("Район",           "");
	СведенияОбОП.Вставить("Город",           "");
	СведенияОбОП.Вставить("НаселенныйПункт", "");
	СведенияОбОП.Вставить("Улица",           "");
	СведенияОбОП.Вставить("Дом",             "");
	СведенияОбОП.Вставить("Корпус",          "");
	СведенияОбОП.Вставить("Квартира",        "");
	СведенияОбОП.Вставить("ТипДома",         "");
	СведенияОбОП.Вставить("ТипКорпуса",      "");
	СведенияОбОП.Вставить("ТипКвартиры",     "");
	
	СведенияОбОП.Вставить("ПредставлениеАдреса", "");
	
	СохраненныеСведения = Неопределено;
	ИмяЭлементаСтруктуры = "СведенияОбОП";
	
	Если АктивнаяСтраница.ДополнительныеДанные.Свойство(ИмяЭлементаСтруктуры, СохраненныеСведения) Тогда
		ЗаполнитьЗначенияСвойств(СведенияОбОП, СохраненныеСведения);
	КонецЕсли;
	
	Если ПустаяСтрока(СведенияОбОП.ИНН) Тогда
		ИНН = РегламентированнаяОтчетность.роВернутьЗначениеПараметраСЛиста(ЭтаФорма, "Титульный", "ИНН1_");
		Если Лев(ИНН, 2) = "00" Тогда
			ИНН = Сред (ИНН, 3); // Удаление нулей, добавленных в соответствии с порядком заполнения
		КонецЕсли;
		СведенияОбОП.ИНН = ИНН;
	КонецЕсли;
	
	ФормаВводаРеквизитов = ПолучитьФорму("ВводРеквизитовОП");
	ФормаВводаРеквизитов.ПараметрыФормы = СведенияОбОП;
	
	РезультатВвода = ФормаВводаРеквизитов.ОткрытьМодально();
	
	Если РезультатВвода <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(СведенияОбОП, РезультатВвода);
		АктивнаяСтраница.ДополнительныеДанные.Вставить(ИмяЭлементаСтруктуры, СведенияОбОП);
		
		ВыводитьРеквизиты = (НЕ ПустаяСтрока(РезультатВвода.Наименование) 
		                ИЛИ НЕ ПустаяСтрока(РезультатВвода.ИНН) 
		                ИЛИ НЕ ПустаяСтрока(РезультатВвода.КПП)
		                ИЛИ НЕ ПустаяСтрока(РезультатВвода.ПредставлениеАдреса));
		
		Если ВыводитьРеквизиты Тогда
			НаименованиеОП = "";
			Если НЕ ПустаяСтрока(РезультатВвода.Наименование) Тогда
				НаименованиеОП = РезультатВвода.Наименование;
			КонецЕсли;
			Если НЕ ПустаяСтрока(РезультатВвода.ИНН) Тогда
				НаименованиеОП = НаименованиеОП + ?(ПустаяСтрока(НаименованиеОП), "", ", ") + "ИНН " + РезультатВвода.ИНН;
			КонецЕсли;
			Если НЕ ПустаяСтрока(РезультатВвода.КПП) Тогда
				НаименованиеОП = НаименованиеОП + ?(ПустаяСтрока(НаименованиеОП), "", ", ") + "КПП " + РезультатВвода.КПП;
			КонецЕсли;
			Если НЕ ПустаяСтрока(РезультатВвода.ПредставлениеАдреса) Тогда
				НаименованиеОП = НаименованиеОП + ?(ПустаяСтрока(НаименованиеОП), "", Символы.ПС) + РезультатВвода.ПредставлениеАдреса;
			КонецЕсли;
		Иначе
			НаименованиеОП = "";
		КонецЕсли;
		
		Элемент.Области.ОбособленноеПодразделение.Значение = НаименованиеОП;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ВвестиКППМестаДеятельности(Элемент, Область, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИмяОбласти = Область.Имя;
	
	МассивКомпонентИмени = ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок(ИмяОбласти, "_");
	Если МассивКомпонентИмени.Количество() <= 1 Тогда
		// У имени нет постфикса порядкового номера.
		Возврат;
	КонецЕсли;
	
	ПорядковыйНомер = МассивКомпонентИмени[МассивКомпонентИмени.Количество()-1];
	
	КППМестаДеятельности = "";
	Для Инд = 1 По 9 Цикл
		ИмяОбластиСчитывания = "П000000000201_" + Инд + "_" + ПорядковыйНомер;
		КППМестаДеятельности = КППМестаДеятельности + СокрЛП(ПТД_Лицензии.Области[ИмяОбластиСчитывания].Значение);
	КонецЦикла;
	
	ВводЗавершен = Ложь;
	Пока НЕ ВводЗавершен Цикл
		Если НЕ ВвестиСтроку(КППМестаДеятельности, "Введите КПП", 9) Тогда
			Возврат;
		КонецЕсли;
		
		Если КППМестаДеятельности = ""
		 ИЛИ (ОбщегоНазначения.ТолькоЦифрыВСтроке(КППМестаДеятельности) И СтрДлина(КППМестаДеятельности) = 9) Тогда
			
			Для Инд = 1 По 9 Цикл
				ИмяОбластиДляВывода = "П000000000201_" + Инд + "_" + ПорядковыйНомер;
				ЗначениеДляВывода = Сред(КППМестаДеятельности, Инд, 1);
				ПТД_Лицензии.Области[ИмяОбластиДляВывода].Значение = ЗначениеДляВывода;
			КонецЦикла;
			
			Модифицированность = Истина;
			ВводЗавершен = Истина;
		Иначе
			Сообщить("КПП должен состоять из 9 цифр.");
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВвестиАдресМестаДеятельности(Элемент, Область, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИмяОбласти = Область.Имя;
	
	МассивКомпонентИмени = ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок(ИмяОбласти, "_");
	Если МассивКомпонентИмени.Количество() <= 1 Тогда
		// У имени нет постфикса порядкового номера.
		Возврат;
	КонецЕсли;
	
	ПорядковыйНомер = МассивКомпонентИмени[МассивКомпонентИмени.Количество()-1];
	
	СтраницыРазделаЛицензии = мСтруктураМногостраничныхРазделов.Лицензии;
	
	АктивнаяСтраница = СтраницыРазделаЛицензии.Найти(Истина, "АктивнаяСтраница");
	
	Если ТипЗнч(АктивнаяСтраница.ДополнительныеДанные) <> Тип("Структура") Тогда
		АктивнаяСтраница.ДополнительныеДанные = Новый Структура;
	КонецЕсли;
	
	АдресМестаДеятельности = Новый Структура;
	АдресМестаДеятельности.Вставить("КодСтраны",       "");
	АдресМестаДеятельности.Вставить("Индекс",          "");
	АдресМестаДеятельности.Вставить("КодРегиона",      "");
	АдресМестаДеятельности.Вставить("Район",           "");
	АдресМестаДеятельности.Вставить("Город",           "");
	АдресМестаДеятельности.Вставить("НаселенныйПункт", "");
	АдресМестаДеятельности.Вставить("Улица",           "");
	АдресМестаДеятельности.Вставить("Дом",             "");
	АдресМестаДеятельности.Вставить("Корпус",          "");
	АдресМестаДеятельности.Вставить("Квартира",        "");
	АдресМестаДеятельности.Вставить("ТипДома",         "");
	АдресМестаДеятельности.Вставить("ТипКорпуса",      "");
	АдресМестаДеятельности.Вставить("ТипКвартиры",     "");
	
	ИмяЭлементаСтруктуры = "АдресМестаДеятельности" + ПорядковыйНомер;
	СохраненныйАдресМестаДеятельности = Неопределено;
	Если АктивнаяСтраница.ДополнительныеДанные.Свойство(ИмяЭлементаСтруктуры, СохраненныйАдресМестаДеятельности) Тогда
		ЗаполнитьЗначенияСвойств(АдресМестаДеятельности, СохраненныйАдресМестаДеятельности);
	КонецЕсли;
	
	ФормаВводаАдреса = РегламентированнаяОтчетность.роПолучитьОбщуюФорму("ВводРоссийскогоАдреса");
	
	СтруктураАдресныхДанных = Новый Структура;
	СтруктураАдресныхДанных.Вставить("Индекс",          АдресМестаДеятельности.Индекс);
	СтруктураАдресныхДанных.Вставить("Регион",          АдресМестаДеятельности.КодРегиона);
	СтруктураАдресныхДанных.Вставить("Район",           АдресМестаДеятельности.Район);
	СтруктураАдресныхДанных.Вставить("Город",           АдресМестаДеятельности.Город);
	СтруктураАдресныхДанных.Вставить("НаселенныйПункт", АдресМестаДеятельности.НаселенныйПункт);
	СтруктураАдресныхДанных.Вставить("Улица",           АдресМестаДеятельности.Улица);
	СтруктураАдресныхДанных.Вставить("Дом",             АдресМестаДеятельности.Дом);
	СтруктураАдресныхДанных.Вставить("Корпус",          АдресМестаДеятельности.Корпус);
	СтруктураАдресныхДанных.Вставить("Квартира",        АдресМестаДеятельности.Квартира);
	
	ФормаВводаАдреса.НачальноеЗначениеВыбора = СтруктураАдресныхДанных;
	ЗаполнитьЗначенияСвойств(ФормаВводаАдреса, АдресМестаДеятельности, "ТипДома, ТипКорпуса, ТипКвартиры");
	
	Если ФормаВводаАдреса.ОткрытьМодально() Тогда
		
		ЗаполнитьЗначенияСвойств(АдресМестаДеятельности, ФормаВводаАдреса.НачальноеЗначениеВыбора);
		ЗаполнитьЗначенияСвойств(АдресМестаДеятельности, ФормаВводаАдреса, "ТипДома, ТипКорпуса, ТипКвартиры");
		АдресМестаДеятельности.КодРегиона = ФормаВводаАдреса.НачальноеЗначениеВыбора.Регион;
		
		Модифицированность = Истина;
		
		Для Инд = 1 По 6 Цикл
			ИмяОбластиДляВывода = "П000000000202_" + Инд + "_" + ПорядковыйНомер;
			ЗначениеДляВывода = Сред(АдресМестаДеятельности.Индекс, Инд, 1);
			ПТД_Лицензии.Области[ИмяОбластиДляВывода].Значение = ЗначениеДляВывода;
		КонецЦикла;
		
		Для Инд = 1 По 2 Цикл
			ИмяОбластиДляВывода = "П000000000203_" + Инд + "_" + ПорядковыйНомер;
			ЗначениеДляВывода = Сред(АдресМестаДеятельности.КодРегиона, Инд, 1);
			ПТД_Лицензии.Области[ИмяОбластиДляВывода].Значение = ЗначениеДляВывода;
		КонецЦикла;
		
		ПТД_Лицензии.Области["П000000000204_" + ПорядковыйНомер].Значение = АдресМестаДеятельности.Район;
		ПТД_Лицензии.Области["П000000000205_" + ПорядковыйНомер].Значение = ?(ПустаяСтрока(АдресМестаДеятельности.Город), АдресМестаДеятельности.НаселенныйПункт, АдресМестаДеятельности.Город);
		ПТД_Лицензии.Области["П000000000206_" + ПорядковыйНомер].Значение = АдресМестаДеятельности.Улица;
		
		СтрокаДома = АдресМестаДеятельности.Дом;
		СтрокаДома = СтрокаДома + ?(ПустаяСтрока(АдресМестаДеятельности.Корпус), "", ", " + АдресМестаДеятельности.Корпус);
		СтрокаДома = СтрокаДома + ?(ПустаяСтрока(АдресМестаДеятельности.Квартира), "", ", " + АдресМестаДеятельности.Квартира);
		ПТД_Лицензии.Области["П000000000207_" + ПорядковыйНомер].Значение = СтрокаДома;
		
	КонецЕсли;
	
	АктивнаяСтраница.ДополнительныеДанные.Вставить(ИмяЭлементаСтруктуры, АдресМестаДеятельности);
	
КонецПроцедуры

Процедура ВвестиАдресПеревозчика(Элемент, Область, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИмяОбласти = Область.Имя;
	ИмяГруппы = РегламентированнаяОтчетность.роРазделТекущейОбласти(ЭтаФорма, Область);
	
	СтраницыРазделаДекларация = мСтруктураМногостраничныхРазделов.Декларация;
	
	АктивнаяСтраница = СтраницыРазделаДекларация.Найти(Истина, "АктивнаяСтраница");
	
	Если ТипЗнч(АктивнаяСтраница.ДополнительныеДанные) <> Тип("Структура") Тогда
		АктивнаяСтраница.ДополнительныеДанные = Новый Структура;
	КонецЕсли;
	
	ДополнительныеДанныеСтраницы = АктивнаяСтраница.ДополнительныеДанные;
	
	АдресПеревозчика = Новый Структура;
	АдресПеревозчика.Вставить("Страна",          "");
	АдресПеревозчика.Вставить("Индекс",          "");
	АдресПеревозчика.Вставить("Регион",          "");
	АдресПеревозчика.Вставить("Район",           "");
	АдресПеревозчика.Вставить("Город",           "");
	АдресПеревозчика.Вставить("НаселенныйПункт", "");
	АдресПеревозчика.Вставить("Улица",           "");
	АдресПеревозчика.Вставить("Дом",             "");
	АдресПеревозчика.Вставить("Корпус",          "");
	АдресПеревозчика.Вставить("Квартира",        "");
	АдресПеревозчика.Вставить("ТипДома",         "");
	АдресПеревозчика.Вставить("ТипКорпуса",      "");
	АдресПеревозчика.Вставить("ТипКвартиры",     "");
	
	СохраненныйАдресПеревозчика = Неопределено;
	Если ДополнительныеДанныеСтраницы.Свойство(ИмяОбласти, СохраненныйАдресПеревозчика) Тогда
		ЗаполнитьЗначенияСвойств(АдресПеревозчика, СохраненныйАдресПеревозчика);
	КонецЕсли;
	
	ФормаВводаАдреса = РегламентированнаяОтчетность.роПолучитьОбщуюФорму("ВводРоссийскогоАдреса");
	ФормаВводаАдреса.НачальноеЗначениеВыбора = АдресПеревозчика;
	ЗаполнитьЗначенияСвойств(ФормаВводаАдреса, АдресПеревозчика, "ТипДома, ТипКорпуса, ТипКвартиры");
	
	Если ФормаВводаАдреса.ОткрытьМодально() Тогда
		
		ЗаполнитьЗначенияСвойств(АдресПеревозчика, ФормаВводаАдреса.НачальноеЗначениеВыбора);
		ЗаполнитьЗначенияСвойств(АдресПеревозчика, ФормаВводаАдреса, "ТипДома, ТипКорпуса, ТипКвартиры");
		
		Модифицированность = Истина;
		
		ИДКонфигурации = РегламентированнаяОтчетность.ИДКонфигурации();
		Если ИДКонфигурации = "УПП" ИЛИ ИДКонфигурации = "КА" Тогда
			Область.Значение = ФормаВводаАдреса.Представление;
		Иначе
			Область.Значение = ФормаВводаАдреса.ПредставлениеНаФорме;
		КонецЕсли;
		
		АктивнаяСтраница.ДополнительныеДанные.Вставить(ИмяОбласти, АдресПеревозчика);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьДоступностьПолейДекларация()
	
	СтраницыРаздела = мСтруктураМногостраничныхРазделов.Декларация;
	АктивнаяСтраница = СтраницыРаздела.Найти(Истина, "АктивнаяСтраница");
	ИндексСтраницы = СтраницыРаздела.Индекс(АктивнаяСтраница);
	
	РазрешитьВвод = (ИндексСтраницы = 0);
	
	ВариантРучногоВвода = ?(РазрешитьВвод, 0, 5);
	ВариантАвторасчета = ?(РазрешитьВвод, ?(ФлажокОтклАвтоРасчет, 0, 4), 5);
	
	НомерСтроки = 1;
	Постфикс = Формат(НомерСтроки, "ЧГ=0");
	Пока ПТД_Декларация.Области.Найти("П000010000101_" + Постфикс) <> Неопределено Цикл
		
		ИзменитьВариантЗаполненияОбласти(ПТД_Декларация, "П000010000101_" + Постфикс, ВариантРучногоВвода);
		ИзменитьВариантЗаполненияОбласти(ПТД_Декларация, "П000010000102_" + Постфикс, ВариантРучногоВвода);
		ИзменитьВариантЗаполненияОбласти(ПТД_Декларация, "П000010000103_" + Постфикс, ВариантРучногоВвода);
		ИзменитьВариантЗаполненияОбласти(ПТД_Декларация, "П000010000104_" + Постфикс, ВариантРучногоВвода);
		ИзменитьВариантЗаполненияОбласти(ПТД_Декларация, "П000010000105_" + Постфикс, ВариантРучногоВвода);
		ИзменитьВариантЗаполненияОбласти(ПТД_Декларация, "П000010000106_" + Постфикс, ВариантРучногоВвода);
		ИзменитьВариантЗаполненияОбласти(ПТД_Декларация, "П000010000107_" + Постфикс, ВариантРучногоВвода);
		ИзменитьВариантЗаполненияОбласти(ПТД_Декларация, "П000010000181_" + Постфикс, ВариантРучногоВвода);
		ИзменитьВариантЗаполненияОбласти(ПТД_Декларация, "П000010000182_" + Постфикс, ВариантРучногоВвода);
		ИзменитьВариантЗаполненияОбласти(ПТД_Декларация, "П000010000109_" + Постфикс, ВариантРучногоВвода);
		ИзменитьВариантЗаполненияОбласти(ПТД_Декларация, "П000010000110_" + Постфикс, ВариантРучногоВвода);
		ИзменитьВариантЗаполненияОбласти(ПТД_Декларация, "П000010000111_" + Постфикс, ВариантРучногоВвода);
		ИзменитьВариантЗаполненияОбласти(ПТД_Декларация, "П000010000112_" + Постфикс, ВариантРучногоВвода);
		ИзменитьВариантЗаполненияОбласти(ПТД_Декларация, "П000010000113_" + Постфикс, ВариантРучногоВвода);
		ИзменитьВариантЗаполненияОбласти(ПТД_Декларация, "П000010000114_" + Постфикс, ВариантРучногоВвода);
		ИзменитьВариантЗаполненияОбласти(ПТД_Декларация, "П000010000115_" + Постфикс, ВариантРучногоВвода);
		ИзменитьВариантЗаполненияОбласти(ПТД_Декларация, "П000010000116_" + Постфикс, ВариантРучногоВвода);
		ИзменитьВариантЗаполненияОбласти(ПТД_Декларация, "П000010000117_" + Постфикс, ВариантРучногоВвода);
		ИзменитьВариантЗаполненияОбласти(ПТД_Декларация, "П000010000118_" + Постфикс, ВариантРучногоВвода);
		ИзменитьВариантЗаполненияОбласти(ПТД_Декларация, "П000010000119_" + Постфикс, ВариантРучногоВвода);
		ИзменитьВариантЗаполненияОбласти(ПТД_Декларация, "П000010000120_" + Постфикс, ВариантРучногоВвода);
		ИзменитьВариантЗаполненияОбласти(ПТД_Декларация, "П000010000121_" + Постфикс, ВариантРучногоВвода);
		ИзменитьВариантЗаполненияОбласти(ПТД_Декларация, "П000010000122_" + Постфикс, ВариантРучногоВвода);
		ИзменитьВариантЗаполненияОбласти(ПТД_Декларация, "П000010000123_" + Постфикс, ВариантРучногоВвода);
		ИзменитьВариантЗаполненияОбласти(ПТД_Декларация, "П000010000124_" + Постфикс, ВариантРучногоВвода);
		
		НомерСтроки = НомерСтроки + 1;
		Постфикс = Формат(НомерСтроки, "ЧГ=0");
	КонецЦикла;
	
	ИзменитьВариантЗаполненияОбласти(ПТД_Декларация, "П000010000217", ВариантАвторасчета);
	ИзменитьВариантЗаполненияОбласти(ПТД_Декларация, "П000010000223", ВариантАвторасчета);
	ИзменитьВариантЗаполненияОбласти(ПТД_Декларация, "П000010000224", ВариантАвторасчета);
	
КонецПроцедуры

Процедура ИзменитьВариантЗаполненияОбласти(ПолеТабличногоДокумента, ИмяОбласти, ВариантЗаполнения);
	
	ОбрабатываемаяОбласть = ПолеТабличногоДокумента.Области[ИмяОбласти];
	
	Если ВариантЗаполнения = 4 ИЛИ ВариантЗаполнения = 5 Тогда
		ОбрабатываемаяОбласть.Защита = Истина;
	Иначе
		ОбрабатываемаяОбласть.Защита = Ложь;
	КонецЕсли;
	
	КлючЦветаФона = "_" + ВариантЗаполнения;
	ЦветФона = Неопределено;
	мСтруктураЦветовРаскраски.Свойство(КлючЦветаФона, ЦветФона);
	
	ОбрабатываемаяОбласть.ЦветФона = ЦветФона;
	
	Если ВариантЗаполнения = 5 Тогда
		ОбрабатываемаяОбласть.Очистить();
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьДоступностьКнопокДопСтрокДекларация(ПолеТабличногоДокумента)
	
	СтраницыРаздела = мСтруктураМногостраничныхРазделов.Декларация;
	АктивнаяСтраница = СтраницыРаздела.Найти(Истина, "АктивнаяСтраница");
	ИндексСтраницы = СтраницыРаздела.Индекс(АктивнаяСтраница);
	
	Если ИндексСтраницы <> 0 Тогда
		
		ИмяКолонки = Лев(ПолеТабличногоДокумента.ТекущаяОбласть.Имя, 13);
		Если мСтруктураМногострочныхРазделов[мИдГруппы1].Колонки.Найти(ИмяКолонки) <> Неопределено Тогда
			ЭлементыФормы.КнопкаДобавитьДопСтрокиДекларация.Доступность = Ложь;
			ЭлементыФормы.КнопкаУдалитьДопСтрокиДекларация.Доступность =  Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьСпискиВыбора()
	
	Если мДатаКонцаПериодаОтчета < '20131101' Тогда
		ИмяМакета = "Списки2012Кв3";
	Иначе
		ИмяМакета = "Списки2012Кв3_20131101";
	КонецЕсли;
	МакетСоставаПоказателей = ПолучитьМакет(ИмяМакета);
	
	КоллекцияСписковВыбора = Новый Соответствие;
	Для Каждого Область Из МакетСоставаПоказателей.Области Цикл
		Если Область.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Строки Тогда
			ВерхОбласти = Область.Верх;
			НизОбласти = Область.Низ;
			ТаблСписка = Новый ТаблицаЗначений;
			ТаблСписка.Колонки.Добавить("Код",,, МакетСоставаПоказателей.Область(ВерхОбласти, 1, ВерхОбласти, 1).ШиринаКолонки);
			ТаблСписка.Колонки.Добавить("Название",,, МакетСоставаПоказателей.Область(ВерхОбласти, 2, ВерхОбласти, 2).ШиринаКолонки);
			Для НомСтр = ВерхОбласти По НизОбласти Цикл
				КодПоказателя = СокрП(МакетСоставаПоказателей.Область(НомСтр, 1).Текст);
				Если КодПоказателя <> "###" Тогда
					НовСтрока = ТаблСписка.Добавить();
					НовСтрока.Код = КодПоказателя;
					НовСтрока.Название = СокрП(МакетСоставаПоказателей.Область(НомСтр, 2).Текст);
				КонецЕсли;
			КонецЦикла;
			КоллекцияСписковВыбора.Вставить(Область.Имя, ТаблСписка);
		КонецЕсли;
	КонецЦикла;
	
	СвойстваПоказателей = Новый ТаблицаЗначений;
	СвойстваПоказателей.Колонки.Добавить("Показатель");
	СвойстваПоказателей.Колонки.Добавить("Длина");
	СвойстваПоказателей.Колонки.Добавить("Тип");
	СвойстваПоказателей.Колонки.Добавить("ТекстПриВыборе");
	СвойстваПоказателей.Колонки.Добавить("ТаблицаЗначений");
	
	СвойстваПоказателей.Индексы.Добавить("Показатель");
	
	ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "П000000000105", 3, , "Выбор вида продукции", КоллекцияСписковВыбора["ВидыДеятельности"]);
	
	ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "П000010000101", 2, , "Выбор вида перевозки", КоллекцияСписковВыбора["ВидыПеревозок"]);
	ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "П000010000301", 2, , "Выбор вида перевозки", КоллекцияСписковВыбора["ВидыПеревозок"]);
	
	ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "П000010000102", 5, , "Выбор вида продукции", КоллекцияСписковВыбора["ВидыПродукции"]);
	ДобавитьСтрокуОписанияВвода(СвойстваПоказателей, "П000010000302", 5, , "Выбор вида продукции", КоллекцияСписковВыбора["ВидыПродукции"]);
	
КонецПроцедуры // СформироватьСпискиВыбора()

Функция ДобавитьСтрокуОписанияВвода(ТаблицаПараметров, Показатель, Длина, Тип = Неопределено, ТекстПриВыборе = Неопределено, Значения = Неопределено)
	
	НовСтр = ТаблицаПараметров.Добавить();
	НовСтр.Показатель = Показатель;
	НовСтр.Тип = Тип;
	НовСтр.Длина = Длина;
	НовСтр.ТекстПриВыборе = ТекстПриВыборе;
	НовСтр.ТаблицаЗначений = Значения;
	
КонецФункции

Процедура УстановитьВариантДляЯчеекЛицензии(Вариант)
	Цвет = мСтруктураЦветовРаскраски["_" + Вариант];
	Защита = Вариант > 0;
	
	ТабличныйДокумент = ЭтаФорма.ЭлементыФормы.ПолеТабличногоДокументаЛицензии;
	
	ТабличныйДокумент.Области.П000000000101.ЦветФона = Цвет;
	ТабличныйДокумент.Области.П000000000101.Защита = Защита;
	ТабличныйДокумент.Области.П000000000102.ЦветФона = Цвет;
	ТабличныйДокумент.Области.П000000000102.Защита = Защита;
	ТабличныйДокумент.Области.П000000000103.ЦветФона = Цвет;
	ТабличныйДокумент.Области.П000000000103.Защита = Защита;
	ТабличныйДокумент.Области.П000000000104.ЦветФона = Цвет;
	ТабличныйДокумент.Области.П000000000104.Защита = Защита;
	
	Если Защита Тогда
		ТабличныйДокумент.Области.П000000000101.Значение = "";
		ТабличныйДокумент.Области.П000000000102.Значение = "";
		ТабличныйДокумент.Области.П000000000103.Значение = '00010101';
		ТабличныйДокумент.Области.П000000000104.Значение = '00010101';
	КонецЕсли;
КонецПроцедуры

Процедура ЗаменитьВидДеятельностиНаВсехСтраницах(Код, Название)

    ИмяТекущейСтраницыПанели = "ТаблицаСтраницыЛицензии";
	ТаблицаСтраницРаздела = мСтруктураМногостраничныхРазделов.Лицензии;

	Для Каждого СтрТаблицаСтраницы Из ТаблицаСтраницРаздела Цикл
		Если СтрТаблицаСтраницы.АктивнаяСтраница Тогда
			Продолжить;
		КонецЕсли;
		СтрТаблицаСтраницы.Данные.П000000000101 = "";
		СтрТаблицаСтраницы.Данные.П000000000102 = "";
		СтрТаблицаСтраницы.Данные.П000000000103 = '00010101';
		СтрТаблицаСтраницы.Данные.П000000000104 = '00010101';
		СтрТаблицаСтраницы.Данные.П000000000105 = Название;
		
		Если СтрТаблицаСтраницы.ДополнительныеДанные = Неопределено Тогда
			СтрТаблицаСтраницы.ДополнительныеДанные = Новый Структура;
		КонецЕсли;
		СтрТаблицаСтраницы.ДополнительныеДанные.Вставить("П000000000105", Код);
	КонецЦикла;
КонецПроцедуры

Функция ЛицензируемаяДеятельность()
	Если мСтруктураМногостраничныхРазделов.Лицензии.Количество() < 1 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Данные = мСтруктураМногостраничныхРазделов.Лицензии[0].ДополнительныеДанные;
	
	ИмяОбласти = "П000000000105";
	
	Значение = Неопределено;
	Если Данные <> Неопределено 
		И Данные.Свойство(ИмяОбласти, Значение) 
		И Не ПустаяСтрока(Значение) Тогда
		Возврат Число(Значение) < 8;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции

Процедура ВвестиПоказатель(Область, КодПоказателя, Знач ЭтоСумма, Знач ТекстВыбора, ТаблицаВыбора, СтандартнаяОбработка, КолонкаПоиска = "Код") Экспорт
	
	Если ТаблицаВыбора = Неопределено ИЛИ мАвтоВыборКодов Тогда
		Возврат;
	КонецЕсли;
	
	Если Найти(КодПоказателя, "П000000000105") = 1 Тогда
		КолонкаПоиска = "Название";
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Если Область.Имя = КодПоказателя ИЛИ РегламентированнаяОтчетность.роПоказательВходитВМногострочнуюЧасть(ЭтаФорма, КодПоказателя) > 0 Тогда
		ИсходноеЗначение = ?(ТипЗнч(Область.Значение) = Тип("Строка"), СокрЛП(Область.Значение), Область.Значение);
	Иначе
		Разделитель = ?(Лев(Область.Имя, СтрДлина(КодПоказателя) + 1) = КодПоказателя + "_", "_", "");
		ИндексОбласти = 1;
		ИсходноеЗначение = "";
		Пока ТекущийЭлемент.Области.Найти(КодПоказателя + Разделитель + Формат(ИндексОбласти, "ЧГ=")) <> Неопределено Цикл
			ИсходноеЗначение = ИсходноеЗначение + Строка(ТекущийЭлемент.Области[КодПоказателя + Разделитель + Формат(ИндексОбласти, "ЧГ=")].Значение);
			ИндексОбласти = ИндексОбласти + 1;
		КонецЦикла;
		ИсходноеЗначение = СокрЛП(ИсходноеЗначение);
	КонецЕсли;
	ИсходнаяСтрока = ТаблицаВыбора.Найти(ИсходноеЗначение, КолонкаПоиска);
	
	ФормаВыбора = Обработки.ОбщиеОбъектыРегламентированнойОтчетности.ПолучитьФорму("ФормаВыбора");
	ФормаВыбора.Заголовок = ТекстВыбора;
	ФормаВыбора.СписокКодов = ТаблицаВыбора;
	Если ИсходнаяСтрока <> Неопределено Тогда
		ФормаВыбора.КодИсходнойСтроки = ИсходнаяСтрока.Код;
	КонецЕсли;
	
	РезультатВыбора = ФормаВыбора.ОткрытьМодально();
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Область.Имя = КодПоказателя ИЛИ РегламентированнаяОтчетность.роПоказательВходитВМногострочнуюЧасть(ЭтаФорма, КодПоказателя) > 0 Тогда
		Если КодПоказателя = "П000000000105" И мДатаКонцаПериодаОтчета >= '20131101' Тогда
			БылаЛицензируемая = ЛицензируемаяДеятельность();
			СталаЛицензируемая = ?(ПустаяСтрока(РезультатВыбора.Код), Истина, Число(РезультатВыбора.Код) < 8);
			
			// В отчете нельзя совмещать лицензионную или нелицензионную деятельность
			Если Не СталаЛицензируемая 
				И СталаЛицензируемая <> БылаЛицензируемая 
				И мСтруктураМногостраничныхРазделов.Лицензии.Количество() > 1 Тогда
				Результат = Вопрос(НСтр("ru='Выбор этого вида деятельности приведет к очистке сведений о лицензиях организации со всех страниц этого раздела. Если в отчете есть нелицензионный вид деятельности, он должен быть единственным." + Символы.ПС + "Продолжить?'"), 
					   РежимДиалогаВопрос.ДаНет);
				Если Результат = КодВозвратаДиалога.Нет Тогда
					Возврат;
				КонецЕсли;
			КонецЕсли;
			
			Код = РезультатВыбора.Код;
			Название = ?(ПустаяСтрока(Код), "", РезультатВыбора.Название);
			
			Если СталаЛицензируемая И НЕ БылаЛицензируемая Тогда
				ЗаменитьВидДеятельностиНаВсехСтраницах("", "");
			ИначеЕсли Не СталаЛицензируемая Тогда
				ЗаменитьВидДеятельностиНаВсехСтраницах(Код, Название);
			КонецЕсли;
			
			Если СталаЛицензируемая Тогда
			    УстановитьВариантДляЯчеекЛицензии(0);
			Иначе
				УстановитьВариантДляЯчеекЛицензии(5);
			КонецЕсли;
		КонецЕсли;
		
		Область.Значение = РезультатВыбора[КолонкаПоиска];
		ЗаполнитьСвязанныеПоказатели(Область.Имя, РезультатВыбора);
	Иначе
		СтрокаРезультатВыбора = Строка(РезультатВыбора[КолонкаПоиска]);
		Для Инд = СтрДлина(СтрокаРезультатВыбора) + 1 По ИндексОбласти - 1 Цикл
			СтрокаРезультатВыбора = " " + РезультатВыбора[КолонкаПоиска];
		КонецЦикла;
		Для Инд = 1 По ИндексОбласти - 1 Цикл
			ТекущийЭлемент.Области[КодПоказателя + Разделитель + Формат(Инд, "ЧГ=")].Значение = СокрЛП(Сред(РезультатВыбора[КолонкаПоиска], Инд, 1));
		КонецЦикла;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	Расчет();
	
КонецПроцедуры

Процедура ЗаполнитьСвязанныеПоказатели(ИмяОбласти, РезультатВыбора)
	
	Если Найти(ИмяОбласти, "П000000000105") = 1 Тогда
		ИмяДополнительныхДанных = "П000000000105";
		
		ТаблицаСтраницЛицензии = Неопределено;
		мСтруктураМногостраничныхРазделов.Свойство("Лицензии", ТаблицаСтраницЛицензии);
		
		АктивнаяСтраница = ТаблицаСтраницЛицензии.Найти(Истина, "АктивнаяСтраница");
		
		Если ТипЗнч(АктивнаяСтраница.ДополнительныеДанные) <> Тип("Структура") Тогда
			АктивнаяСтраница.ДополнительныеДанные = Новый Структура;
		КонецЕсли;
		АктивнаяСтраница.ДополнительныеДанные.Вставить(ИмяДополнительныхДанных, РезультатВыбора.Код);
		
		Если ПустаяСтрока(РезультатВыбора.Код) Тогда
			ПТД_Лицензии.Области[ИмяОбласти].Значение = "";
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьСтруктуруМногостраничныхРазделов(ИмяСтраницы = Неопределено)
	
	РегламентированнаяОтчетность.роОбновитьСтруктуруМногостраничныхРазделов(ЭтаФорма, ИмяСтраницы);
	
КонецПроцедуры

Процедура УдалитьДополнительнуюСтраницу(ИмяТекущейСтраницыПанели = Неопределено)
	
	РегламентированнаяОтчетность.роУдалитьДополнительнуюСтраницу(ЭтаФорма, ИмяТекущейСтраницыПанели);
	
КонецПроцедуры

Процедура Очистить() Экспорт
	
	Если РегламентированнаяОтчетность.роОчистить(ЭтаФорма) Тогда
		
		РегламентированнаяОтчетность.роОчиститьВариантыЗаполнения(ЭтаФорма);
		
		РегламентированнаяОтчетность.ОтключитьОбработчикАвтосохранения(ЭтаФорма);
		РегламентированнаяОтчетность.ВключитьОбработчикАвтосохранения(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОчиститьСлужебныеДанныеРаздела(ИмяСтраницыПанели)
	
	Если мСтруктураМногостраничныхРазделов.Свойство(ИмяСтраницыПанели) Тогда
		СтраницыРазделаПриложение = мСтруктураМногостраничныхРазделов[ИмяСтраницыПанели];
		
		АктивнаяСтраница = СтраницыРазделаПриложение.Найти(Истина, "АктивнаяСтраница");
		Если АктивнаяСтраница = Неопределено ИЛИ ТипЗнч(АктивнаяСтраница.ДополнительныеДанные) <> Тип("Структура") Тогда
			Возврат;
		КонецЕсли;
		
		АктивнаяСтраница.ДополнительныеДанные = Новый Структура;
		
	Иначе
		ДополнительныеДанные[ИмяСтраницыПанели] = Неопределено;
		ВосстановитьСтруктуруДополнительныхДанных();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОчиститьСтруктуру(ОчищаемаяСтруктура)
	
	Если ТипЗнч(ОчищаемаяСтруктура) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ЭлементСтруктуры Из ОчищаемаяСтруктура Цикл
		Если ТипЗнч(ЭлементСтруктуры.Значение) = Тип("Структура") Тогда
			ОчиститьСтруктуру(ОчищаемаяСтруктура[ЭлементСтруктуры.Ключ]);
		Иначе
			ОчищаемаяСтруктура[ЭлементСтруктуры.Ключ] = ОбщегоНазначения.ПустоеЗначениеТипа(ТипЗнч(ЭлементСтруктуры.Значение));
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Выводит в табличный документ данные страницы многостраничного раздела формы.
// Одновременно убирая признак у активной страницы и сохраняя в нее данные текущего табличного поля
//
// Параметры:
//  Шаг                      - число,  количество перелистываемых страниц;
//  ИмяТекущейСтраницыПанели - строка, имя страницы основной панели формы, содержащий многостраничный раздел;
//  ПоказатьПоИндексу        - булево, управляет выводом страницы по заданному индексу в коллекции страниц.
//
Процедура ПоказатьСтраницуМногостраничногоРаздела(Шаг, ИмяТекущейСтраницыПанели, ПоказатьПоИндексу = Ложь) Экспорт
	
	РегламентированнаяОтчетность.роПоказатьСтраницуМногостраничногоРаздела(ЭтаФорма, Шаг, ИмяТекущейСтраницыПанели, ПоказатьПоИндексу);
	
КонецПроцедуры

// Вывод необходимого номера страницы
// Выводит в табличный документ данные страницы многостраничного раздела формы.
//
// Параметры:
//  Шаг                      - число,  номер страницы, на которую необходимо перейти;
//  ИмяТекущейСтраницыПанели - строка, имя страницы основной панели формы, содержащий многостраничный раздел;
//  УчестьАктивнуюСтраницу - Если истина, то учитывается страница, которая была ранее активной, иначе нет
//
Процедура ПоказатьСтраницуМногостраничногоРазделаПриВосстановлении(Шаг, ИмяТекущейСтраницыПанели, УчестьАктивнуюСтраницу = Ложь)
	
	РегламентированнаяОтчетность.роПоказатьСтраницуМногостраничногоРазделаПриВосстановлении(ЭтаФорма, Шаг, ИмяТекущейСтраницыПанели, УчестьАктивнуюСтраницу);
	
	Если ИмяТекущейСтраницыПанели = "Декларация" Тогда
		УстановитьДоступностьПолейДекларация();
	КонецЕсли;
	
КонецПроцедуры

// Определяет варианты заполнения ячеек табличного документа
// по имени страницы основной панели формы.
//
// Параметры:
//   ИмяСтраницыПанели - строка, имя страницы основной панели формы.
//   ПолучитьЭталонную - булево, если истина, получаем эталонную
//                               (исходную) таблицу вариантов заполнения.
//
// Возвращаемое значение:
//   таблица значений, содержащая информацию о вариантах автозаполнения ячеек.
//
Функция ПолучитьТаблицуВариантовЗаполнения(ИмяСтраницыПанели, ПолучитьЭталонную = Ложь)

	Возврат РегламентированнаяОтчетность.роПолучитьТаблицуВариантовЗаполнения(ЭтаФорма, ИмяСтраницыПанели, ПолучитьЭталонную);

КонецФункции // ПолучитьТаблицуВариантовЗаполнения()

// Формирует дерево значений - структуру страниц отчета,
// содержащей настройки показа страниц, вывода на печать и выгрузки в ИФНС.
//
Процедура ФормироватьСтруктуруСвойствСтраниц() Экспорт
	
	ОписаниеТиповСтрока100 = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(100);
	
	ОписаниеТиповСтрока50 = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(50);
	
	ОписаниеТиповЧисло1    = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(1);
	
	
	мСвойстваРазделовДекларации           = Новый ДеревоЗначений;
	
	мСвойстваРазделовДекларации.Колонки.Добавить("ИмяСтраницы",              ОписаниеТиповСтрока100);
	
	мСвойстваРазделовДекларации.Колонки.Добавить("МногострочностьВРазделе",  ОписаниеТиповЧисло1);
	// 0 - нет
	// 1 - есть одна многострочная часть
	// 2 - и более, точное кол-во число многострочных частей на листе
	
	// Позволяет записывать многострочность в многостраничных документ либо как ТаблицуЗначений, либо как Структуру
	// В случае, если как Структура, то необходима еще одна процедура СформироватьСтруктуруДанныхДопСтрокСтраницы
	// Только для Многостраничных документов !
	мСвойстваРазделовДекларации.Колонки.Добавить("СохранятьМногострКакТЗ",  мОписаниеТиповБулево);
	
	мСвойстваРазделовДекларации.Колонки.Добавить("МногостраничностьВРазделе",  мОписаниеТиповБулево);
	// Ложь   - нет
	// Истина - да
	
	// 3. Будет ли очищаться раздел по кнопки "Очистить"
	// Название параметра
	мСвойстваРазделовДекларации.Колонки.Добавить("ОчищатьРаздел",  мОписаниеТиповБулево);
	// Ложь   - нет
	// Истина - да
	
	// 4. Если раздел многостраничный, параметр МногостраничностьВРазделе = Истина, тогда имя поля, по которому
	// будет осуществляться Представление доп. страницы
	// Название параметра
	мСвойстваРазделовДекларации.Колонки.Добавить("ИмяПредставления",  ОписаниеТиповСтрока50);
	
	// 5. Есть ли в разделе значения, отображаемые в квадратном представлении
	// Название параметра
	мСвойстваРазделовДекларации.Колонки.Добавить("ПредставлениеДанных",  мОписаниеТиповБулево);
	
	// 6. Обязательный раздел для выгрузки или нет
	// Название параметра
	мСвойстваРазделовДекларации.Колонки.Добавить("РазделОбязателенДляВыгрузки",  мОписаниеТиповБулево);
	// Ложь   - нет
	// Истина - да
	
	// 7. Обязательный раздел для выгрузки или нет
	// Название параметра
	мСвойстваРазделовДекларации.Колонки.Добавить("НазваниеПанелиТабличногоПоляРаздела",  ОписаниеТиповСтрока50);
	
	мСвойстваРазделовДекларации.Колонки.Добавить("РазделЯвляетсяАвтозаполняемым", мОписаниеТиповБулево);
	
	мСвойстваРазделовДекларации.Колонки.Добавить("ПерестановкаСтраницЗапрещена", мОписаниеТиповБулево);
	
	мСвойстваРазделовДекларации.Колонки.Добавить("ЗапускатьОтдельнуюОбработкуПослеОчисткиЛиста", мОписаниеТиповБулево);
	
	мСвойстваРазделовДекларации.Строки.Очистить();
	
	СтрокаУровня1 = мСвойстваРазделовДекларации.Строки.Добавить();
	
	СтрокаУровня1.ИмяСтраницы                          = "Титульный";
	СтрокаУровня1.МногострочностьВРазделе              = 0;
	СтрокаУровня1.МногостраничностьВРазделе            = Ложь;
	СтрокаУровня1.ОчищатьРаздел                        = Ложь;
	СтрокаУровня1.ИмяПредставления                     = "";
	СтрокаУровня1.ПредставлениеДанных                  = Истина;
	СтрокаУровня1.РазделОбязателенДляВыгрузки          = Неопределено;
	СтрокаУровня1.НазваниеПанелиТабличногоПоляРаздела  = "ОсновнаяПанель";
	СтрокаУровня1.СохранятьМногострКакТЗ               = Неопределено;
	СтрокаУровня1.РазделЯвляетсяАвтозаполняемым        = Ложь;
	СтрокаУровня1.ЗапускатьОтдельнуюОбработкуПослеОчисткиЛиста = Ложь;
	
	СтрокаУровня1 = мСвойстваРазделовДекларации.Строки.Добавить();
	
	СтрокаУровня1.ИмяСтраницы                          = "Лицензии";
	СтрокаУровня1.МногострочностьВРазделе              = 0;
	СтрокаУровня1.МногостраничностьВРазделе            = Истина;
	СтрокаУровня1.ОчищатьРаздел                        = Ложь;
	СтрокаУровня1.ИмяПредставления                     = "";
	СтрокаУровня1.ПредставлениеДанных                  = Ложь;
	СтрокаУровня1.РазделОбязателенДляВыгрузки          = Неопределено;
	СтрокаУровня1.НазваниеПанелиТабличногоПоляРаздела  = "ОсновнаяПанель";
	СтрокаУровня1.СохранятьМногострКакТЗ               = Неопределено;
	СтрокаУровня1.РазделЯвляетсяАвтозаполняемым        = Ложь;
	СтрокаУровня1.ЗапускатьОтдельнуюОбработкуПослеОчисткиЛиста = Истина;
	
	СтрокаУровня1 = мСвойстваРазделовДекларации.Строки.Добавить();
	СтрокаУровня1.ИмяСтраницы                          = "Декларация";
	СтрокаУровня1.МногострочностьВРазделе              = 2;
	СтрокаУровня1.МногостраничностьВРазделе            = Истина;
	СтрокаУровня1.ОчищатьРаздел                        = Истина;
	СтрокаУровня1.ИмяПредставления                     = "";
	СтрокаУровня1.ПредставлениеДанных                  = Ложь;
	СтрокаУровня1.РазделОбязателенДляВыгрузки          = Неопределено;
	СтрокаУровня1.НазваниеПанелиТабличногоПоляРаздела  = "ОсновнаяПанель";
	СтрокаУровня1.СохранятьМногострКакТЗ               = Ложь;
	СтрокаУровня1.РазделЯвляетсяАвтозаполняемым        = Ложь; // Не инициализировать элементы  управления корректировкой автозаполняемых значений
	СтрокаУровня1.ЗапускатьОтдельнуюОбработкуПослеОчисткиЛиста = Истина;
	
КонецПроцедуры // ФормироватьСтруктуруСтраницОтчета()

Функция СформироватьСтруктуруДанныхДопСтрокСтраницы(ИмяСтраницы) Экспорт
	
	Перем СтруктураДанныхДопСтрокРаздела;
	
	Если ИмяСтраницы = "Декларация" Тогда
		
		// Так как раздел содержит несколько многострочных блоков,
		// то поочередно сохраняем данные дополнительных строк каждого блока.
		ОбновитьСтруктуруМногострочногоРаздела(мИдГруппы1);
		ОбновитьСтруктуруМногострочногоРаздела(мИдГруппы2);
		
		СтруктураДанныхДопСтрокРаздела = Новый Структура;
		
		// Обновим структуру, хранящую данные строк многострочных блоков.
		ПромежуточнаяТаблица = Неопределено;
		мСтруктураМногострочныхРазделов.Свойство(мИдГруппы1, ПромежуточнаяТаблица);
		ТаблицаДопСтрокГруппы11 = ПромежуточнаяТаблица.Скопировать();
		СтруктураДанныхДопСтрокРаздела.Вставить(мИдГруппы1, ТаблицаДопСтрокГруппы11);
		
		ПромежуточнаяТаблица = Неопределено;
		мСтруктураМногострочныхРазделов.Свойство(мИдГруппы2, ПромежуточнаяТаблица);
		ТаблицаДопСтрокГруппы12 = ПромежуточнаяТаблица.Скопировать();
		СтруктураДанныхДопСтрокРаздела.Вставить(мИдГруппы2, ТаблицаДопСтрокГруппы12);
		
	КонецЕсли;
	
	Возврат СтруктураДанныхДопСтрокРаздела;
	
КонецФункции

// Функция определяет поле табличного документа формы, содержащее 
// многострочный раздел.по имени ИдГруппы.
//
// Параметры:
//  ИдГруппы - строка, имя многострочного раздела.
//
// Возвращаемое значение:
//  Поле табличного документа.
//
Функция ОпределитьТабличноеПолеПоИдМногострочногоРаздела(ИдГруппы) Экспорт
	
	Перем ТекТабличноеПоле;
	
	Если ИдГруппы = мИдГруппы1 ИЛИ ИдГруппы = мИдГруппы2 Тогда
		ТекТабличноеПоле = ПТД_Декларация;
	КонецЕсли;
	
	Возврат ТекТабличноеПоле;
	
КонецФункции // ОпределитьТабличноеПолеПоИдМногострочногоРаздела()

// Определяет многострочный раздел табличного документа
// на активной странице основной панели формы.
//
// Параметры:
//  Нет;
//
// Возвращаемое значение:
//  строка, имя многострочного раздела.
//
Функция ОпределитьИдМногострочногоРазделаПоАктивнойСтранице(ИмяЛиста = Неопределено) Экспорт
	
	Перем ИмяГруппы;
	
	Если ИмяЛиста = Неопределено Тогда
		ТекущаяСтраницаПанели    = ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница;
		ИмяТекущейСтраницыПанели = ТекущаяСтраницаПанели.Имя;
	Иначе
		ИмяТекущейСтраницыПанели = ИмяЛиста;
	КонецЕсли;
	
	Если ИмяТекущейСтраницыПанели = "Декларация" Тогда
		ИмяГруппы = РегламентированнаяОтчетность.роРазделТекущейОбласти(ЭтаФорма, ПТД_Декларация.ТекущаяОбласть);
	КонецЕсли;
	
	Возврат ИмяГруппы;
	
КонецФункции // ОпределитьИдМногострочногоРазделаПоАктивнойСтранице()

// Функция собирает значения редактирукемых ячеек табличного документа
// в коллекцию значений типа "структура".
//
// Параметры:
//  ТабличноеПоле - поле табличного документа текущей формы.
//
// Возвращаемое значение:
//  Структура.
//
Функция СобратьДанныеТекущегоТаблПоля(ТабличноеПоле) Экспорт
	
	ТабличноеПолеИмя = ТабличноеПоле.Имя;
	
	СтруктураДанныхПоля = Новый Структура;
	
	Для Инд = 0 По ТабличноеПоле.Области.Количество() - 1 Цикл
		ТекущаяОбласть = ТабличноеПоле.Области[Инд];
		
		Если Не ТекущаяОбласть.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекущаяОбласть.СодержитЗначение <> Истина Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяПоказателя      = ТекущаяОбласть.Имя;
		ЗначениеПоказателя = ТекущаяОбласть.Значение;
		
		СтруктураДанныхПоля.Вставить(ИмяПоказателя, ЗначениеПоказателя);
	КонецЦикла;
	
	Возврат СтруктураДанныхПоля;
	
КонецФункции // СобратьДанныеТекущегоТаблПоля()

Процедура ЗаполнитьПоляНомераКорректировки()
	
	ВыбранКорректирующийВидДок = (ВидДокумента = 1);
	
	Если ВыбранКорректирующийВидДок Тогда
		ПредставлениеНомера = Формат(НомерКорректировки, "ЧЦ=2; ЧВН=");
	Иначе
		ПредставлениеНомера = "";
	КонецЕсли;
	
	РегламентированнаяОтчетность.роПроставитьВКвадратыЗначения(ЭтаФорма, "Титульный", "НомКорр", 2, ПредставлениеНомера);
	
КонецПроцедуры

// Обновляет пользовательские настройки по вариантам заполнения
// показателей отчета в случае изменения версии формы отчета.
//
// Возвращаемое значение:
//   Булево - признак модифицированность пользовательских настроек.
//
Функция ОбновитьВариантыЗаполнения()
	
	Перем МассивВариантовЗаполненияСтраниц;
	
	ЕстьИзменения = Ложь;
	
	ОписаниеТиповСтрока15  = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(15);
	ОписаниеТиповСтрока50  = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(50);
	ОписаниеТиповСтрока100 = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(100);
	
	ОписаниеТиповЧисло1    = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(1);
	ОписаниеТиповЧисло15   = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15,2);
	
	ЕстьИзменения = Ложь;
	
	МакетСоставаПоказателей = ЭтотОбъект.ПолучитьМакет(мСоставПоказателей);
	
	Для Инд = 0 По МакетСоставаПоказателей.Области.Количество() - 1 Цикл
		// организован цикл по именованным областям макета,
		// содержащим состав показателей и варианты автозаполнения
		// таблиц на каждой странице основной панели формы.

		ТекОбласть  = МакетСоставаПоказателей.Области[Инд];
		ИмяСтраницы = ТекОбласть.Имя;

		// Таблица значений содержит информацию о вариантах заполнения показателя отчета.
		// В колонках таблицы значений отображается следующая информация:
		//   - имя ячейки (код показателя);
		//   - вариант заполнения ячейки;
		//   - вычисленное по данным ИБ значение ячейки;
		//   - величина автокорректировки значения ячейки;
		//   - примечание к ячейке.
		//
		ТаблицаВариантыЗаполнения = Новый ТаблицаЗначений;
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "КодПоказателя",     ОписаниеТиповСтрока50  );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "ВариантЗаполнения", ОписаниеТиповЧисло1    );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "ЗначениеАвто",      ОписаниеТиповЧисло15   );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "Дельта",            ОписаниеТиповЧисло15   );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "Комментарий",       ОписаниеТиповСтрока100 );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "КодПоказателяПоСтруктуре",       ОписаниеТиповСтрока50 );

		Для Ном = ТекОбласть.Верх По ТекОбласть.Низ Цикл
			// перебираем строки макета

			// Код показателя (по составу показателей) определяется по первой колонке макета
			КодПоказателя = СокрП(МакетСоставаПоказателей.Область(Ном, 1).Текст);
			ИмяПоказателя = КодПоказателя;

			Если КодПоказателя = "===" Тогда         // признак конечной строки
				Прервать;
			КонецЕсли;

			Если Лев(КодПоказателя, 2) = "//" Тогда  // пропускаем комментарии
				Продолжить;
			КонецЕсли;

			// код показателя по форме отчете (имя ячейки в полях табличного документа формы)
			КодПоказателяПоФорме = СокрЛП(МакетСоставаПоказателей.Область(Ном, 2).Текст);
			// код показателя по форме отчете (имя ячейки в полях табличного документа формы)
			КодПоказателяПоСтруктуре = СокрЛП(МакетСоставаПоказателей.Область(Ном, 9).Текст);

			// вариант заполнения ячейки определяется по колонке 6 макета
			стрВариантЗаполнения = РегламентированнаяОтчетность.ПолучитьВариантЗаполненияПоказателяРегОтчета(МакетСоставаПоказателей, Ном);

			чВариантЗаполнения   = ? (ПустаяСтрока(стрВариантЗаполнения), 0, Число(стрВариантЗаполнения));
			Если чВариантЗаполнения <> 0 Тогда
				// Если в колонке 6 задан варианет заполнения показателя, 
				// то его значение может определяться по данным ИБ.
				// Дополняем таблицу значений вариантов заполнения
				НоваяСтрока = ТаблицаВариантыЗаполнения.Добавить();
				НоваяСтрока.КодПоказателя      = ? (Не ПустаяСтрока(КодПоказателяПоФорме), КодПоказателяПоФорме, КодПоказателя);
				НоваяСтрока.ВариантЗаполнения  = чВариантЗаполнения;
				НоваяСтрока.КодПоказателяПоСтруктуре  = КодПоказателяПоСтруктуре;
			КонецЕсли;

		КонецЦикла;

		Если Не мСтруктураВариантыЗаполнения.Свойство(ИмяСтраницы, МассивВариантовЗаполненияСтраниц) Тогда
			Если ТаблицаВариантыЗаполнения.Количество() > 0 Тогда
				// Рассмотрим вариант, когда в предыдущей версии формы по данной странице
				// не было предусмотрено автозаполнение по данным ИБ.
				// Добавим такую возможность в пользовательскую настройку

				// Для обеспечения назначения разных вариантов заполнения одного и того же показателя
				// на разных страницах, создаем массив, хранящий информацию о вариантах заполнения
				// таблиц по каждой странице многостраничного раздела.
				МассивВариантовЗаполненияСтраниц = Новый Массив;
				// Первым элементом массива является "эталонная" таблица значений вариантов заполнения
				МассивВариантовЗаполненияСтраниц.Добавить(ТаблицаВариантыЗаполнения);

				// сохраняем варианты заполнения таблиц на данной странице
				мСтруктураВариантыЗаполнения.Вставить(ИмяСтраницы, МассивВариантовЗаполненияСтраниц);

				ЕстьИзменения = Истина;
			КонецЕсли;
		Иначе
			Если ТаблицаВариантыЗаполнения.Количество() = 0 Тогда
				// В новой версии формы не предусмотрено задание вариантов автозаполнения для текущей страницы.
				мСтруктураВариантыЗаполнения.Удалить(ИмяСтраницы);
				
				ЕстьИзменения = Истина;
			Иначе
				// Актуализация вариантов автозаполнения текущей страницы.
				Для Каждого ЭлементМассива Из МассивВариантовЗаполненияСтраниц Цикл
					Для Каждого СтрокаТаблицы Из ТаблицаВариантыЗаполнения Цикл
						КодПоказателя     = СтрокаТаблицы.КодПоказателя;
						ВариантЗаполнения = СтрокаТаблицы.ВариантЗаполнения;
						
						ДействующийВариантАвтозаполнения = ЭлементМассива.Найти(КодПоказателя, "КодПоказателя");
						Если ДействующийВариантАвтозаполнения = Неопределено Тогда
							// Новый автозаполняемый показатель.
							НоваяСтрока = ЭлементМассива.Добавить();
							НоваяСтрока.КодПоказателя     = КодПоказателя;
							НоваяСтрока.ВариантЗаполнения = ВариантЗаполнения;
							
							ЕстьИзменения = Истина;
							
						Иначе
							// Показатель должен соответствовать эталону, кроме удовлетворяющих следующим критериям:
							// - вариант эталона не 4 (авторасчет)и не 5 (запрет ввода значения);
							// - действующий вариант допускает корректировку (варианты 1-3).
							ИспользоватьДействующийВариант = ((ДействующийВариантАвтозаполнения.ВариантЗаполнения = 1
							                             ИЛИ ДействующийВариантАвтозаполнения.ВариантЗаполнения = 2
							                             ИЛИ ДействующийВариантАвтозаполнения.ВариантЗаполнения = 3)
							                               И (ВариантЗаполнения <> 4) И (ВариантЗаполнения <> 5));
							Если Не ИспользоватьДействующийВариант Тогда
								ДействующийВариантАвтозаполнения.КодПоказателя     = КодПоказателя;
								ДействующийВариантАвтозаполнения.ВариантЗаполнения = ВариантЗаполнения;
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
					
					ТаблДляЦикла = ЭлементМассива.Скопировать();
					Для Каждого СтрокаТаблицы Из ТаблДляЦикла Цикл
						КодПоказателя = СтрокаТаблицы.КодПоказателя;
						
						Если ТаблицаВариантыЗаполнения.Найти(КодПоказателя, "КодПоказателя") = Неопределено Тогда
							// В новой версии отчета автозаполнение показателя не предусмотрено.
							СтрокаВЭлементеМассива = ЭлементМассива.Найти(КодПоказателя, "КодПоказателя");
							Если  СтрокаВЭлементеМассива <> Неопределено Тогда
								ЭлементМассива.Удалить(СтрокаВЭлементеМассива);
								
								ЕстьИзменения = Истина;
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		// Обработка флага авторасчета отчета.
		ЭлементДереваСтраниц = мДеревоСтраницОтчета.Строки.Найти(ИмяСтраницы, "ИмяСтраницы");
		Если ЭлементДереваСтраниц <> Неопределено Тогда
			
			ТаблицаВариантыЗаполнения = РегламентированнаяОтчетность.роПолучитьТаблицуВариантовЗаполнения(ЭтаФорма, ИмяСтраницы);
			
			Если НЕ ТаблицаВариантыЗаполнения = Неопределено Тогда
				
				Для Каждого СтрокаВариантаЗаполнения Из ТаблицаВариантыЗаполнения Цикл
					Если СтрокаВариантаЗаполнения.ВариантЗаполнения = 4 ИЛИ СтрокаВариантаЗаполнения.ВариантЗаполнения = 0 Тогда
						СтрокаВариантаЗаполнения.ВариантЗаполнения = ?(ФлажокОтклАвтоРасчет, 0, 4);
					КонецЕсли;
				КонецЦикла;
				
			КонецЕсли;
			
			УстановитьЦветФонаЯчейки(, , ИмяСтраницы);
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЕстьИзменения;
	
КонецФункции // ОбновитьВариантыЗаполнения()

// Обновляет пользовательские настройки страниц отчета
// в случае изменения версии формы отчета.
//
// Возвращаемое значение:
//   Булево - признак модифицированность пользовательских настроек.
//
Функция ОбновитьНастройкиСтраниц()

	ЕстьИзменения = Ложь;

	// формируем дерево значений - пользовательских настройки страниц отчета
	мПользовательскиеНастройкиСтраниц = мДеревоСтраницОтчета.Скопировать();

	// формируем дерево значений - настройки страниц отчета обновленной формы
	ФормироватьСтруктуруСтраницОтчета();

	// Сначала проверяем, не изменилась ли структура настроек страниц (колонки дерева)
	Для каждого ИсхКолонка Из мДеревоСтраницОтчета.Колонки Цикл

		Если мПользовательскиеНастройкиСтраниц.Колонки.Найти(ИсхКолонка.Имя) = Неопределено Тогда
			// добавиляем новую колонку
			мПользовательскиеНастройкиСтраниц.Колонки.Добавить(ИсхКолонка.Имя, ИсхКолонка.ТипЗначения, ИсхКолонка.Заголовок, ИсхКолонка.Ширина);
			мПользовательскиеНастройкиСтраниц.ЗагрузитьКолонку(мДеревоСтраницОтчета.ВыгрузитьКолонку(ИсхКолонка.Имя), ИсхКолонка.Имя);

			ЕстьИзменения = Истина;
		КонецЕсли;

	КонецЦикла;

	// Выполняем обратную задачу:
	// удаляем отсутствующие колонки
	Для каждого ТекКолонка Из мПользовательскиеНастройкиСтраниц.Колонки Цикл

		Если мДеревоСтраницОтчета.Колонки.Найти(ТекКолонка.Имя) = Неопределено Тогда
			// удаляем отсутствующую колонку
			мПользовательскиеНастройкиСтраниц.Колонки.Удалить(мПользовательскиеНастройкиСтраниц.Колонки.Индекс(ТекКолонка));

			ЕстьИзменения = Истина;
		КонецЕсли;

	КонецЦикла;

	// Обновляем строки дерева значений - пользовательских настроек:
	//
	// добавляем новые строки и удаляем лишние:
	Если мДеревоСтраницОтчета.Строки.Количество() <> мПользовательскиеНастройкиСтраниц.Строки.Количество() Тогда

		КолвоКолонок     = мДеревоСтраницОтчета.Колонки.Количество();

		// добавляем новые строки
		Для каждого Строка Из мДеревоСтраницОтчета Цикл

			Если мПользовательскиеНастройкиСтраниц.Найти(Строка.ИмяСтраницы, "ИмяСтраницы") = Неопределено Тогда

				НоваяСтрока = мПользовательскиеНастройкиСтраниц.Строки.Добавить();
				Для НомКолонки = 0 По (КолвоКолонок - 1) Цикл
				
					НоваяСтрока[НомКолонки] = Строка[НомКолонки];
				
				КонецЦикла;

			КонецЕсли;

			// обновляем строки второго уровня
			Если Строка.Строки.Количество() > 0 Тогда

				Для каждого Строка2 Из Строка.Строки Цикл

					Если мПользовательскиеНастройкиСтраниц.Найти(Строка2.ИмяСтраницы, "ИмяСтраницы") = Неопределено Тогда

						НоваяСтрока2 = НоваяСтрока.Строки.Добавить();
						Для НомКолонки = 0 По (КолвоКолонок - 1) Цикл

							НоваяСтрока2[НомКолонки] = Строка2[НомКолонки];

						КонецЦикла;

					КонецЕсли;

				КонецЦикла; 

			КонецЕсли; 

		КонецЦикла;

		// удаляем лишние строки
		Для каждого Строка Из мПользовательскиеНастройкиСтраниц Цикл

			Если мДеревоСтраницОтчета.Найти(Строка.ИмяСтраницы, "ИмяСтраницы") = Неопределено Тогда

				мПользовательскиеНастройкиСтраниц.Строки.Удалить(мПользовательскиеНастройкиСтраниц.Строки.Индекс(Строка));

			КонецЕсли;

		КонецЦикла;

		ЕстьИзменения = Истина;
	КонецЕсли;

	Для каждого ТекСтрока Из мДеревоСтраницОтчета.Строки Цикл

		НомСтр = мДеревоСтраницОтчета.Строки.Индекс(ТекСтрока);

		// устанавливаем признаки предопределенных установок выгружаемых страниц (значение флажка = 2);
		Если ТекСтрока.ВыгрузитьСтраницу = 2 Тогда
			
			Если мПользовательскиеНастройкиСтраниц.Строки[НомСтр].ВыгрузитьСтраницу <> 2 Тогда
				мПользовательскиеНастройкиСтраниц.Строки[НомСтр].ВыгрузитьСтраницу = 2;
				ЕстьИзменения = Истина;
			КонецЕсли;

		КонецЕсли;

		// обновляем представление
		Если ТекСтрока.Представление <> мПользовательскиеНастройкиСтраниц.Строки[НомСтр].Представление Тогда
			мПользовательскиеНастройкиСтраниц.Строки[НомСтр].Представление = ТекСтрока.Представление;
			ЕстьИзменения = Истина;
		КонецЕсли;

		// обновляем имя поля табличного документа
		Если ТекСтрока.ИмяПоляТабличногоДокум <> мПользовательскиеНастройкиСтраниц.Строки[НомСтр].ИмяПоляТабличногоДокум Тогда
			мПользовательскиеНастройкиСтраниц.Строки[НомСтр].ИмяПоляТабличногоДокум = ТекСтрока.ИмяПоляТабличногоДокум;
			ЕстьИзменения = Истина;
		КонецЕсли;

	КонецЦикла; 

	мДеревоСтраницОтчета =  мПользовательскиеНастройкиСтраниц.Скопировать();

	Возврат ЕстьИзменения;

КонецФункции // ОбновитьНастройкиСтраниц()

// Процедура управляет цветом фона автозаполняемых ячеек при:
//  а) автозаполнении;
//  б) ручном выборе другого варианта заполнения;
//  в) добавлении/удалении и листании дополнительных страниц.
//
// Цвет фона устанавливается в зависимости от варианта заполнения ячейки.
//
// Параметры:
//  ИмяЯчейки                - строка, имя ячейки, для которой устанавливаем цвет фона;
//  ВариантЗаполнения        - число,  вариант заполнения ячейки;
//  ИмяТекущейСтраницыПанели - строка, имя страницы основной панели, на которой для всех автозаполняемых
//                             ячеек табличного документа следует установить цвет фона.
//
Процедура УстановитьЦветФонаЯчейки(ИмяЯчейки = Неопределено, ВариантЗаполнения = Неопределено, ИмяТекущейСтраницыПанели = "") Экспорт
	Перем ЦветФона;
	Перем Комментарий;
	
	Если мБезОткрытияФормы Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ИмяЯчейки = Неопределено Тогда
		// устанавливаем цвет фона конкретной ячейки
		ИмяСтраницыПанели = РегламентированнаяОтчетность.роОпределитьСтраницуПанелиПоКодуПоказателя(ЭтаФорма, ИмяЯчейки);
		Если ИмяСтраницыПанели = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ИмяПоляТаблДокумента = "ПолеТабличногоДокумента" + ИмяСтраницыПанели;
		Ключ = "_" + ВариантЗаполнения;
		мСтруктураЦветовРаскраски.Свойство(Ключ, ЦветФона);
		
		ЗначениеВКвадратах = РегламентированнаяОтчетность.роОбластьЯвляетсяЯчейкойСоставногоПоля(ЭтаФорма, ИмяЯчейки);
		Если ВариантЗаполнения = 4 ИЛИ ВариантЗаполнения = 5 Тогда
			Если ЗначениеВКвадратах Тогда
				// если ЗНАЧЕНИЕ КВАДРАТИЧНОЕ
				РегламентированнаяОтчетность.роСвойстваГрупповыхЗначений(ЭтаФорма, ИмяЯчейки,"УстановитьЗащиту");
			Иначе
				ЭлементыФормы[ИмяПоляТаблДокумента].Области[ИмяЯчейки].Защита = Истина;
			КонецЕсли;
		Иначе
			Если ЗначениеВКвадратах Тогда
				// если ЗНАЧЕНИЕ КВАДРАТИЧНОЕ
				РегламентированнаяОтчетность.роСвойстваГрупповыхЗначений(ЭтаФорма, ИмяЯчейки,"СнятьЗащиту");
			Иначе
				ЭлементыФормы[ИмяПоляТаблДокумента].Области[ИмяЯчейки].Защита = Ложь;
			КонецЕсли;
		КонецЕсли;
		// установим цвет ячейки
		Если ЗначениеВКвадратах Тогда
			РегламентированнаяОтчетность.роСвойстваГрупповыхЗначений(ЭтаФорма, ИмяЯчейки,"ПроставитьЦветРаскраски", ЦветФона);
		Иначе
			ЭлементыФормы[ИмяПоляТаблДокумента].Области[ИмяЯчейки].ЦветФона = ЦветФона;
		КонецЕсли;
	Иначе
		// устанавливаем цвет фона всех автозаполняемых ячеек активной страницы
		ТаблицаВариантыЗаполнения = ПолучитьТаблицуВариантовЗаполнения(ИмяТекущейСтраницыПанели);
		Если ТаблицаВариантыЗаполнения = Неопределено Тогда
			Возврат;
		КонецЕсли;
		Для Каждого Строка Из ТаблицаВариантыЗаполнения Цикл
			ИмяПоказателя     = Строка.КодПоказателя;
			ВариантЗаполнения = Строка.ВариантЗаполнения;
			Комментарий       = Строка.Комментарий;
			КодПоказателяПоСтруктуре =  Строка.КодПоказателяПоСтруктуре;
			
			КолвоСтрокВМногострочнойЧасти = РегламентированнаяОтчетность.роПоказательВходитВМногострочнуюЧасть(ЭтаФорма, ИмяПоказателя);
			
			Если КолвоСтрокВМногострочнойЧасти > 0 Тогда
				Многострочность = Истина;
				БазоваяЧасть = ИмяПоказателя;
			Иначе
				Многострочность = Ложь;
				КолвоСтрокВМногострочнойЧасти = 1;
			КонецЕсли;
			
			Для Инд = 1 по КолвоСтрокВМногострочнойЧасти Цикл
				
				Если Многострочность Тогда
					ИмяПоказателя = БазоваяЧасть + "_" + Формат(Инд, "ЧГ=0");
				КонецЕсли;
				
				ТекОбластьЯчеек = ЭлементыФормы["ПолеТабличногоДокумента" + ИмяТекущейСтраницыПанели].Области[ИмяПоказателя];
				Если (Не ПустаяСтрока(Комментарий)) ИЛИ (Не ПустаяСтрока(ТекОбластьЯчеек.Примечание.Текст)) Тогда
					ТекОбластьЯчеек.Примечание.Текст = Комментарий;
				КонецЕсли;
				
				Ключ = "_" + ВариантЗаполнения;
				мСтруктураЦветовРаскраски.Свойство(Ключ, ЦветФона);
				
				//ЗначениеВКвадратах = РегламентированнаяОтчетность.роОбластьЯвляетсяЯчейкойСоставногоПоля(ЭтаФорма, ИмяПоказателя);
				ЗначениеВКвадратах = ?(ПустаяСтрока(КодПоказателяПоСтруктуре), Ложь, Истина);
				
				Если ВариантЗаполнения = 4 ИЛИ ВариантЗаполнения = 5 Тогда
					
					Если ЗначениеВКвадратах Тогда
						// если ЗНАЧЕНИЕ КВАДРАТИЧНОЕ
						РегламентированнаяОтчетность.роСвойстваГрупповыхЗначений(ЭтаФорма, ИмяПоказателя, "УстановитьЗащиту");
					Иначе
						ЭлементыФормы["ПолеТабличногоДокумента" + ИмяТекущейСтраницыПанели].Области[ИмяПоказателя].Защита = Истина;
					КонецЕсли;
					
				Иначе
					
					Если ЗначениеВКвадратах Тогда
						// если ЗНАЧЕНИЕ КВАДРАТИЧНОЕ
						РегламентированнаяОтчетность.роСвойстваГрупповыхЗначений(ЭтаФорма, ИмяПоказателя,"СнятьЗащиту");
					Иначе
						ЭлементыФормы["ПолеТабличногоДокумента" + ИмяТекущейСтраницыПанели].Области[ИмяПоказателя].Защита = Ложь;
					КонецЕсли;
				КонецЕсли;
				
				Если ЗначениеВКвадратах Тогда
					РегламентированнаяОтчетность.роСвойстваГрупповыхЗначений(ЭтаФорма, ИмяПоказателя,"ПроставитьЦветРаскраски", ЦветФона);
				Иначе
					ЭлементыФормы["ПолеТабличногоДокумента" + ИмяТекущейСтраницыПанели].Области[ИмяПоказателя].ЦветФона = ЦветФона;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры // УстановитьЦветФонаЯчейки()

// Обновляет структуру многострочнго раздела, заданного
// уникальным идентификатором. Заполняет таблицу значений
// многострочного раздела значениями соответствующих ячеек.
//
// Параметры:
//  ИмяГруппы - строка - идентификатор многострочного раздела.
//
Процедура ОбновитьСтруктуруМногострочногоРаздела(ИмяГруппы) Экспорт
	
	Перем СтруктураГруппы;
	
	мСтруктураМногострочныхРазделов.Свойство(ИмяГруппы, СтруктураГруппы);
	ТекТабличноеПоле = ОпределитьТабличноеПолеПоИдМногострочногоРаздела(ИмяГруппы);
	
	Для НСтр = 1 По СтруктураГруппы.Количество() Цикл
		
		ТекСтрокаТаблицы = СтруктураГруппы[НСтр - 1];
		
		Для Каждого Колонка Из СтруктураГруппы.Колонки Цикл
			ИмяКолонки = Колонка.Имя;
			ИмяЯчейки = ИмяКолонки + "_" + Формат(НСтр, "ЧГ=0");
			
			ОбластьЯчейки = ТекТабличноеПоле.Области[ИмяЯчейки];
			Если ОбластьЯчейки.СодержитЗначение = Истина Тогда
				ЗначениеЯчейки = ОбластьЯчейки.Значение;
			Иначе
				ЗначениеЯчейки = ОбластьЯчейки.Текст;
			КонецЕсли;
			
			ТекСтрокаТаблицы[ИмяКолонки] = ЗначениеЯчейки;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры // ОбновитьСтруктуруМногострочногоРаздела()

// Обновляет структуру многострочных разделов.
// Заполняет таблицу значений многострочного раздела
// значениями соответствующих ячеек.
//
Процедура ОбновитьСтруктуруМногострочныхРазделов()
	// Заполняет таблицу значений многострочного раздела
	// указанными в соответствующих ячейках значениями

	// Заполнение выполняется синхронно для всех многострочных разделов
	Для Каждого ЭлементСтруктуры Из мСтруктураМногострочныхРазделов Цикл
		ИмяГруппы = ЭлементСтруктуры.Ключ;

		ОбновитьСтруктуруМногострочногоРаздела(ИмяГруппы);
	КонецЦикла;

КонецПроцедуры // ОбновитьСтруктуруМногострочныхРазделов()

Процедура ПоказатьПериод()
	
	СтрПериодОтчета = ПредставлениеПериода( НачалоДня(мДатаНачалаПериодаОтчета), КонецДня(мДатаКонцаПериодаОтчета), "ФП = Истина" );
	
	// Период в заголовке формы отчета.
	Заголовок = ЭтотОбъект.Метаданные().Формы.ОсновнаяФорма.Представление() + " (за " + СтрПериодОтчета + ")";
	
	Если (мДокументВосстановлен) И (мСкопированаФорма = Неопределено) Тогда
		Возврат;
	КонецЕсли;
	
	//Если мСтруктураМногостраничныхРазделов.Титульный.Количество() > 1 Тогда
	//	НомерАктивнойСтраницы = мСтруктураМногостраничныхРазделов.Титульный.Найти(Истина, "АктивнаяСтраница");
	//	ИндексАктивногоЭкз = мСтруктураМногостраничныхРазделов.Титульный.Индекс(НомерАктивнойСтраницы);
	//	Если ИндексАктивногоЭкз > 0 Тогда
	//		// Переход на первый экземпляр титульного листа.
	//		ПоказатьСтраницуМногостраничногоРаздела(0, "Титульный", Истина);
	//	КонецЕсли;
	//КонецЕсли;
	
	НомерМесяца   = Формат(мДатаКонцаПериодаОтчета, "ДФ=М");
	НомерКвартала = Формат(мДатаКонцаПериодаОтчета, "ДФ=к");
	НомерГода     = Формат(мДатаКонцаПериодаОтчета, "ДФ=гггг");
	
	// Отображение на титульном листе отчетного периода.
	Значение1Кв = ?(НомерКвартала = "1", "V", "");
	ПТД_Титульный.Области.ОтчетныйПериод1Кв.Значение = Значение1Кв;
	
	Значение2Кв = ?(НомерКвартала = "2", "V", "");
	ПТД_Титульный.Области.ОтчетныйПериод2Кв.Значение = Значение2Кв;
	
	Значение3Кв = ?(НомерКвартала = "3", "V", "");
	ПТД_Титульный.Области.ОтчетныйПериод3Кв.Значение = Значение3Кв;
	
	Значение4Кв = ?(НомерКвартала = "4", "V", "");
	ПТД_Титульный.Области.ОтчетныйПериод4Кв.Значение = Значение4Кв;
	
	ПТД_Титульный.Области.ОтчетныйГод1.Значение   = Сред(НомерГода, 1, 1);
	ПТД_Титульный.Области.ОтчетныйГод2.Значение   = Сред(НомерГода, 2, 1);
	ПТД_Титульный.Области.ОтчетныйГод3.Значение   = Сред(НомерГода, 3, 1);
	ПТД_Титульный.Области.ОтчетныйГод4.Значение   = Сред(НомерГода, 4, 1);
	
	//Если мСтруктураМногостраничныхРазделов.Титульный.Количество() > 1 Тогда
	//	Если ИндексАктивногоЭкз > 0 Тогда
	//		ПоказатьСтраницуМногостраничногоРаздела(ИндексАктивногоЭкз, "Титульный", Истина);
	//	КонецЕсли;
	//КонецЕсли;
	
КонецПроцедуры // ПоказатьПериод()

// Заполняет дату подписания отчета должностными лицами, в зависимости от
// наличия данных по этим лицам.
//
Процедура ЗаполнитьДатуВЯчейках() Экспорт

	ДатаПодписиРук = "";
	ДатаПодписиБух = "";
	ДатаПодписиУП = "";

	Если РегламентированнаяОтчетность.ПустоеЗначение(ПТД_Титульный.Области.ОргДиректор.Значение) Тогда
		ДатаПодписиРук = "        ";
	Иначе
		ДатаПодписиРук = ДатаПодписи;
	КонецЕсли;

	Если РегламентированнаяОтчетность.ПустоеЗначение(ПТД_Титульный.Области.ОргБухгалтер.Значение) Тогда
		ДатаПодписиБух = "        ";
	Иначе
		ДатаПодписиБух = ДатаПодписи;
	КонецЕсли;

	Для НомЯчейки = 1 По 8 Цикл
		
		ПТД_Титульный.Области["ДатаПодписиРук" + НомЯчейки].Значение = Сред(Формат(ДатаПодписиРук, "ДФ = 'ддММгггг'"), НомЯчейки, 1);
		ПТД_Титульный.Области["ДатаПодписиБух" + НомЯчейки].Значение = Сред(Формат(ДатаПодписиБух, "ДФ = 'ддММгггг'"), НомЯчейки, 1);
		
	КонецЦикла;

КонецПроцедуры // ЗаполнитьДатуВЯчейках()

// Процедура управляет выводом сведений о выбранной организации
// в соответствующие ячейки табличного документа.
//
// Параметры:
// ВПрограммеИзмененаДатаПодписи - Истина, если процедура запускается для обновления параметров завясящих
// от даты подписания документа, например ФИО должностных лиц
//
Процедура ЗаполнитьСведенияОбОрганизации(ВПрограммеИзмененаДатаПодписи = Ложь)
	
	Если (Организация  = Неопределено) Или (Организация = ОбщегоНазначения.ПустоеЗначениеТипа("СправочникСсылка.Организации")) Тогда
		Возврат
	КонецЕсли;
	
	// Составляем список данных, необходимых для вывода в декларацию
	Сведения = "";
	Если НЕ ВПрограммеИзмененаДатаПодписи Тогда
		Сведения = "ИННЮЛ, КППЮЛ, НаимЮЛПол, АдрЮР, ТелОрганизации, ";
		СведенияОбОрганизации = РегламентированнаяОтчетность.ПолучитьСведенияОбОрганизации(Организация, ДатаПодписи, Сведения);
		
		ИНН = СокрЛП(СведенияОбОрганизации.ИННЮЛ);
		Для Инд = СтрДлина(ИНН) + 1 По 12 Цикл
			ИНН = "0" + ИНН ;
		КонецЦикла;
		ПроставитьНаВсехЛистах("ИНН1_", ИНН);
		
		КПП = СокрЛП(СведенияОбОрганизации.КППЮЛ);
		ПроставитьНаВсехЛистах("КПП1_", КПП);
		
		ЮридическийАдрес = СведенияОбОрганизации.АдрЮр;
		Если НЕ ПустаяСтрока(ЮридическийАдрес) Тогда
			СтруктураАдреса = РегламентированнаяОтчетность.РазложитьАдрес(ЮридическийАдрес);
		КонецЕсли;
		
		АдресОрганизации = ДополнительныеДанные.Титульный.АдресОрганизации;
		ОчиститьСтруктуру(АдресОрганизации);
		
		Если Не СтруктураАдреса = Неопределено Тогда
			АдресОрганизации.КодСтраны       = СокрЛП(СтруктураАдреса.Страна);
			АдресОрганизации.Индекс          = СокрЛП(СтруктураАдреса.Индекс);
			АдресОрганизации.КодРегиона      = РегламентированнаяОтчетность.КодРегионаПоНазванию(СокрЛП(СтруктураАдреса.Регион));
			АдресОрганизации.Район           = СокрЛП(СтруктураАдреса.Район);
			АдресОрганизации.Город           = СокрЛП(СтруктураАдреса.Город);
			АдресОрганизации.НаселенныйПункт = СокрЛП(СтруктураАдреса.НаселенныйПункт);
			АдресОрганизации.Улица           = СокрЛП(СтруктураАдреса.Улица);
			АдресОрганизации.Дом             = СокрЛП(СтруктураАдреса.Дом);
			АдресОрганизации.Корпус          = СокрЛП(СтруктураАдреса.Корпус);
			АдресОрганизации.Квартира        = СокрЛП(СтруктураАдреса.Квартира);
		КонецЕсли;
		
		ЗаполнитьПоляАдресаОрганизации(АдресОрганизации);
		ДополнительныеДанные.Титульный.Вставить("АдресОрганизации", АдресОрганизации);
		
	КонецЕсли;
	Сведения = Сведения + "ФИОРук, ФИОБух";
	
	Результат = РегламентированнаяОтчетность.роЗаполнитьСведенияОбОрганизации(ЭтаФорма, Сведения);
	
	Если Результат <> Неопределено Тогда
		ЗаполнитьСведенияОбОтвественныхЛицах(Результат.ФИОРук, Результат.ФИОБух);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоляАдресаОрганизации(Адрес)
	
	РегламентированнаяОтчетность.роПроставитьВКвадратыЗначения(ЭтаФорма, "Титульный", "ПочтовыйИндекс", 6, Адрес.Индекс);
	РегламентированнаяОтчетность.роПроставитьВКвадратыЗначения(ЭтаФорма, "Титульный", "КодРегиона", 2, Адрес.КодРегиона);
	ПТД_Титульный.Области.АдресРайон.Значение = Адрес.Район;
	ПТД_Титульный.Области.АдресГород.Значение = ?(ПустаяСтрока(Адрес.Город), Адрес.НаселенныйПункт, Адрес.Город);
	ПТД_Титульный.Области.АдресУлица.Значение = Адрес.Улица;
	
	СтрокаДома = Адрес.Дом;
	СтрокаДома = СтрокаДома + ?(ПустаяСтрока(Адрес.Корпус), "", ", " + Адрес.Корпус);
	СтрокаДома = СтрокаДома + ?(ПустаяСтрока(Адрес.Квартира), "", ", " + Адрес.Квартира);
	ПТД_Титульный.Области.АдресДом.Значение = СтрокаДома;
	
КонецПроцедуры

Процедура ЗаполнитьСведенияОбОтвественныхЛицах(Знач ОргДиректор, Знач ОргБухгалтер)
	
	ПТД_Декларация.Области.Руководитель.Значение = ОргДиректор;
	ПТД_Декларация.Области.Бухгалтер.Значение = ОргБухгалтер;
	
КонецПроцедуры

// УправлениеЛистами
// Процедура раскрашивает ячейки листов, в зависимости от типов организаций.
// Запускается в ЗаполнитьСведенияОбОрганизации и при отрктыии всегда.
// Параметры:
//  ТипНП          - число, тип налогоплательщика, если не указано, то берется из ИБ.
//
Процедура УправлениеЛистами(ТипНП = Неопределено) Экспорт
	// Процедура "заглушка". Для совместимости с общими механизмами работы с многостраничными разделами.
	Возврат;
КонецПроцедуры // УправлениеЛистами

// Выводит строки многострочного раздела в табличный документ.
// При отсутствии в табличном документе необходимого количества строк
// добавляет отсутствующие строки.
//
// Параметры:
//  ИмяГруппы       - строка, имя многострочного раздела;
//  СтруктураГруппы - таблица значений, содержащая значения показателей многострочного раздела.
//
Процедура ВывестиРазделВТабличныйДокумент(ИмяГруппы, СтруктураГруппы) Экспорт
	
	Перем ИсходноеКоличествоСтрокГруппы;
	
	мСтруктураИсхКолвоСтрокРазделов.Свойство(ИмяГруппы, ИсходноеКоличествоСтрокГруппы);
	
	ТекущееКоличествоСтрокГруппы = СтруктураГруппы.Количество();
	
	ТекТабличноеПоле = ОпределитьТабличноеПолеПоИдМногострочногоРаздела(ИмяГруппы);
	
	// Защита от случая, когда передают многострочную часть с количеством строк менее чем исходное.
	Если ИсходноеКоличествоСтрокГруппы > ТекущееКоличествоСтрокГруппы Тогда
		Для Инд = 1 По (ИсходноеКоличествоСтрокГруппы - ТекущееКоличествоСтрокГруппы) Цикл
			СтруктураГруппы.Добавить();
		КонецЦикла;
	КонецЕсли;
	
	Если ТекущееКоличествоСтрокГруппы > ИсходноеКоличествоСтрокГруппы Тогда
		
		ИмяПервойЯчейкиПоследнейСтроки = СтруктураГруппы.Колонки[0].Имя + "_" + Формат(ИсходноеКоличествоСтрокГруппы, "ЧГ=0");
		ОбластьЯчейкиПоследнейСтроки = ТекТабличноеПоле.Области[ИмяПервойЯчейкиПоследнейСтроки];
		
		ПрототипДобавляемойОбласти = ТекТабличноеПоле.ПолучитьОбласть(ОбластьЯчейкиПоследнейСтроки.Верх, , ОбластьЯчейкиПоследнейСтроки.Низ, );
		ПомежуточныйТабличныйДокумент = Новый ТабличныйДокумент;
		
		Для НомерНовойСтроки = ИсходноеКоличествоСтрокГруппы + 1 По ТекущееКоличествоСтрокГруппы Цикл
			ПомежуточныйТабличныйДокумент.Вывести(ПрототипДобавляемойОбласти);
			
			// Переопределение имен областей вставленной строки.
			Для Каждого Колонка Из СтруктураГруппы.Колонки Цикл
				
				ИмяКолонки = Колонка.Имя;
				ИмяОбластиПрототипа = ИмяКолонки + "_" + Формат(ИсходноеКоличествоСтрокГруппы, "ЧГ=0");
				НазначаемоеИмяОбластиДобавленнойСтроки = ИмяКолонки + "_" + Формат(НомерНовойСтроки, "ЧГ=0");
				
				ОбластьДобавленнойСтроки = ПомежуточныйТабличныйДокумент.Области[ИмяОбластиПрототипа];
				ОбластьДобавленнойСтроки.Имя = НазначаемоеИмяОбластиДобавленнойСтроки;
				
				Если ОбластьДобавленнойСтроки.СодержитЗначение = Истина Тогда
					ОбластьДобавленнойСтроки.Очистить();
				КонецЕсли;
				
			КонецЦикла;
		КонецЦикла;
		
		ВставляемаяОбласть = ПомежуточныйТабличныйДокумент.Область(1, , ПомежуточныйТабличныйДокумент.ВысотаТаблицы, );
		ПозицияВставки = ТекТабличноеПоле.Область(ОбластьЯчейкиПоследнейСтроки.Верх + 1, , ОбластьЯчейкиПоследнейСтроки.Низ + 1, );
		ТекТабличноеПоле.ВставитьОбласть(ВставляемаяОбласть, ПозицияВставки, ТипСмещенияТабличногоДокумента.ПоВертикали);
		
	КонецЕсли;
	
	// Вывод значений показателей по строкам.
	Для Каждого СтрокаТаблицы Из СтруктураГруппы Цикл
		Для Каждого КолонкаТаблицы Из СтруктураГруппы.Колонки Цикл
			
			НомерСтрокиВТаблице      = СтруктураГруппы.Индекс(СтрокаТаблицы);
			НомерСтрокиВФормеТаблицы = НомерСтрокиВТаблице + 1;
			ИмяЯчейки                = КолонкаТаблицы.Имя + "_" + Формат(НомерСтрокиВФормеТаблицы, "ЧГ=0");
			ОбластьЯчейкиТаблицы     = ТекТабличноеПоле.Области[ИмяЯчейки];
			ЗначениеЯчейки           = СтрокаТаблицы[КолонкаТаблицы.Имя];
			
			Если ОбластьЯчейкиТаблицы.СодержитЗначение = Истина Тогда
				ОбластьЯчейкиТаблицы.Значение = ЗначениеЯчейки;
			Иначе
				ОбластьЯчейкиТаблицы.Текст    = ЗначениеЯчейки;
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;
	
	мСтруктураМногострочныхРазделов[ИмяГруппы] = СтруктураГруппы.Скопировать();
	
КонецПроцедуры // ВывестиРазделВТабличныйДокумент()

Процедура ВставитьДополнительнуюСтроку()
	
	Перем СтруктураГруппы;
	Перем МаксимальноеКоличествоСтрокГруппы;
	
	ИмяГруппы = ОпределитьИдМногострочногоРазделаПоАктивнойСтранице();
	
	Если ИмяГруппы = Неопределено Тогда
		Сообщить("Не выбран многострочный раздел, в который необходимо добавить строку.", СтатусСообщения.Внимание);
		Возврат;
	КонецЕсли;
	
	мСтруктураИсхКолвоСтрокРазделов.Свойство(ИмяГруппы, МаксимальноеКоличествоСтрокГруппы); // Исходное количество строк группы
	
	ТекТабличноеПоле = ОпределитьТабличноеПолеПоИдМногострочногоРаздела(ИмяГруппы);
	мСтруктураМногострочныхРазделов.Свойство(ИмяГруппы, СтруктураГруппы);
	
	КолСтрок = СтруктураГруппы.Количество();
	ИмяПервойЯчейкиПоследнейСтроки = СтруктураГруппы.Колонки[0].Имя + "_" + Формат(КолСтрок, "ЧГ=0");
	
	// Область первой ячейки последней строки многострочного раздела.
	ОбластьЯчейки = ТекТабличноеПоле.Области[ИмяПервойЯчейкиПоследнейСтроки];
	
	НомерВерхЯчейкиОбласти = ОбластьЯчейки.Верх;
	НомерНижнЯчейкиОбласти = ОбластьЯчейки.Низ;
	
	СдвигСтрок = НомерНижнЯчейкиОбласти - НомерВерхЯчейкиОбласти + 1;
	
	НомерВерхЯчейкиНовойОбласти = НомерВерхЯчейкиОбласти + СдвигСтрок;
	НомерНижнЯчейкиНовойОбласти = НомерНижнЯчейкиОбласти + СдвигСтрок;
	
	ВставляемаяОбласть = ТекТабличноеПоле.Область(НомерВерхЯчейкиОбласти, , НомерНижнЯчейкиОбласти, );
	ТекТабличноеПоле.ВставитьОбласть(ВставляемаяОбласть, ,ТипСмещенияТабличногоДокумента.ПоВертикали);
	
	// Переопределение имен вставленной области.
	Для Каждого Колонка Из СтруктураГруппы.Колонки Цикл
		
		ИмяКолонки          = Колонка.Имя;
		ИмяОбластиЯчейки    = ИмяКолонки + "_" + Формат(КолСтрок, "ЧГ=0");
		ИмяНовОбластиЯчейки = ИмяКолонки + "_" + Формат((КолСтрок + 1), "ЧГ=0");
		
		ОбластьЯчейки = ТекТабличноеПоле.Области[ИмяОбластиЯчейки];
		ОбластьЯчейки.Имя = ИмяНовОбластиЯчейки;
		
		// Границы ячейки исходной области.
		НомерВерхСтрокиЯчейки  = ОбластьЯчейки.Верх - СдвигСтрок;
		НомерНижнСтрокиЯчейки  = ОбластьЯчейки.Низ  - СдвигСтрок;
		НомерЛевоКолонкиЯчейки = ОбластьЯчейки.Лево;
		НомерПравКолонкиЯчейки = ОбластьЯчейки.Право;
		
		// Назначаем имя ячейки исходной области, т.к. в результате вставки новой области оно было очищено.
		ОбластьЯчейкиИсходнойОбласти = ТекТабличноеПоле.Область(НомерВерхСтрокиЯчейки, НомерЛевоКолонкиЯчейки, НомерНижнСтрокиЯчейки, НомерПравКолонкиЯчейки);
		ОбластьЯчейкиИсходнойОбласти.Имя = ИмяОбластиЯчейки;
		
		Если ОбластьЯчейки.СодержитЗначение = Истина Тогда
			ОбластьЯчейки.Очистить();
		КонецЕсли;
	КонецЦикла;
	
	ИмяТекущейОбласти = СтруктураГруппы.Колонки[0].Имя + "_" + Формат((КолСтрок + 1), "ЧГ=0");
	ТекТабличноеПоле.ТекущаяОбласть = ТекТабличноеПоле.Области[ИмяТекущейОбласти];
	ТекущийЭлемент = ТекТабличноеПоле;
	
	СтруктураГруппы.Добавить();
	
	Модифицированность = Истина;
	
КонецПроцедуры

Процедура УдалитьДополнительнуюСтроку()
	
	Перем ИсходноеКоличествоСтрокГруппы;
	Перем СтруктураГруппы;
	
	ИмяГруппы = ОпределитьИдМногострочногоРазделаПоАктивнойСтранице();
	Если ИмяГруппы = Неопределено Тогда
		Сообщить("Не выбрана удаляемая строка многострочного раздела.", СтатусСообщения.Внимание);
		Возврат;
	КонецЕсли;
	
	мСтруктураИсхКолвоСтрокРазделов.Свойство(ИмяГруппы, ИсходноеКоличествоСтрокГруппы);
	
	ТекТабличноеПоле = ОпределитьТабличноеПолеПоИдМногострочногоРаздела(ИмяГруппы);
	ИмяРаздела = Сред(ТекТабличноеПоле.Имя, 24);
	
	мСтруктураМногострочныхРазделов.Свойство(ИмяГруппы, СтруктураГруппы);
	
	// Проверка вхождения текущей (выделенной) ячейки в многострочный раздел.
	Область    = ТекТабличноеПоле.ТекущаяОбласть;
	ИмяОбласти = Область.Имя;
	ИмяГруппы  = РегламентированнаяОтчетность.роРазделТекущейОбласти(ЭтаФорма, Область);
	
	Если ИмяГруппы = Неопределено Тогда
		Сообщить("Не выбрана удаляемая строка многострочного раздела.", СтатусСообщения.Внимание);
		Возврат;
	КонецЕсли;
	
	Ответ = Вопрос("Удалить выбранную строку?" + Символы.ПС
	      + "(Внимание! данная операция необратима)", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьСтруктуруМногострочногоРаздела(ИмяГруппы); // Сохранение данных многострочной части из табличного документа в таблицу значений
	
	НомерУдаляемойСтрокиТекстовый = Сред(ИмяОбласти, Найти(ИмяОбласти, "_") + 1);
	НомУдаляемойСтроки = Число(НомерУдаляемойСтрокиТекстовый);
	
	ИндексСтрокиТаблицы = НомУдаляемойСтроки - 1;
	
	// Удаление дополнительных данных.
	СтраницыРаздела = Неопределено;
	РазделМногостраничный = мСтруктураМногостраничныхРазделов.Свойство(ИмяРаздела, СтраницыРаздела);
	АктивнаяСтраница = Неопределено;
	
	Если РазделМногостраничный Тогда
		АктивнаяСтраница = СтраницыРаздела.Найти(Истина, "АктивнаяСтраница");
		Если ТипЗнч(АктивнаяСтраница.ДополнительныеДанные) = Тип("Структура") Тогда
			Если ТипЗнч(АктивнаяСтраница.ДополнительныеДанные) = Тип("Структура") Тогда
				Для Каждого Колонка Из СтруктураГруппы.Колонки Цикл
					ИмяДополнительныхДанных = Колонка.Имя + "_" + НомерУдаляемойСтрокиТекстовый;
					Если АктивнаяСтраница.ДополнительныеДанные.Свойство(ИмяДополнительныхДанных) Тогда
						АктивнаяСтраница.ДополнительныеДанные.Удалить(ИмяДополнительныхДанных);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Для Каждого Колонка Из СтруктураГруппы.Колонки Цикл
			ИмяДополнительныхДанных = Колонка.Имя + "_" + НомерУдаляемойСтрокиТекстовый;
			Если ДополнительныеДанные[ИмяРаздела].Свойство(ИмяДополнительныхДанных) Тогда
				ДополнительныеДанные[ИмяРаздела].Удалить(ИмяДополнительныхДанных);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
		
	Если СтруктураГруппы.Количество() > ИсходноеКоличествоСтрокГруппы Тогда
		
		ИмяПервойЯчейкиУдаляемойСтроки = СтруктураГруппы.Колонки[0].Имя + "_" + НомерУдаляемойСтрокиТекстовый;
		ОбластьЯчейки = ТекТабличноеПоле.Области[ИмяПервойЯчейкиУдаляемойСтроки];
		
		НомерВерхЯчейкиОбласти = ОбластьЯчейки.Верх;
		НомерНижнЯчейкиОбласти = ОбластьЯчейки.Низ;
		
		УдаляемаяОбласть = ТекТабличноеПоле.Область(НомерВерхЯчейкиОбласти, , НомерНижнЯчейкиОбласти, );
		ТекТабличноеПоле.УдалитьОбласть( УдаляемаяОбласть,ТипСмещенияТабличногоДокумента.ПоВертикали );
		
		КолвоСдвигаемыхСтрок = СтруктураГруппы.Количество() - НомУдаляемойСтроки;
		
		// Переименование ячеек сдвинутых строк.
		Если КолвоСдвигаемыхСтрок > 0 Тогда
			
			Для Инд = НомУдаляемойСтроки + 1 По СтруктураГруппы.Количество() Цикл
				
				ТекСтрока = СтруктураГруппы[Инд - 1];
				
				Для Каждого Колонка Из СтруктураГруппы.Колонки Цикл
					ИмяСдвинутойЯчейки = Колонка.Имя + "_" + Формат(Инд, "ЧГ=0");
					НовоеИмяСдвинутойЯчейки = Колонка.Имя + "_" + Формат(Инд - 1, "ЧГ=0");
					
					ОбластьСдвинутойЯчейки     = ТекТабличноеПоле.Области[ИмяСдвинутойЯчейки];
					ОбластьСдвинутойЯчейки.Имя = НовоеИмяСдвинутойЯчейки;
					
					// Изменение имени дополнительных данных ячейки.
					Если РазделМногостраничный Тогда
						Если ТипЗнч(АктивнаяСтраница.ДополнительныеДанные) = Тип("Структура") Тогда
							Если АктивнаяСтраница.ДополнительныеДанные.Свойство(ИмяСдвинутойЯчейки) Тогда
								АктивнаяСтраница.ДополнительныеДанные.Вставить(НовоеИмяСдвинутойЯчейки, АктивнаяСтраница.ДополнительныеДанные[ИмяСдвинутойЯчейки]);
								АктивнаяСтраница.ДополнительныеДанные.Удалить(ИмяСдвинутойЯчейки);
							КонецЕсли;
						КонецЕсли;
					Иначе
						Если ДополнительныеДанные[ИмяРаздела].Свойство(ИмяСдвинутойЯчейки) Тогда
							ДополнительныеДанные[ИмяРаздела].Вставить(НовоеИмяСдвинутойЯчейки, ДополнительныеДанные[ИмяРаздела][ИмяСдвинутойЯчейки]);
							ДополнительныеДанные[ИмяРаздела].Удалить(ИмяСдвинутойЯчейки);
						КонецЕсли;
					КонецЕсли;
					
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
		
		СтруктураГруппы.Удалить(ИндексСтрокиТаблицы);
		
	Иначе
		// Строка не удаляется в форме табличного документа, а осуществляется сдвиг строк.
		СтруктураГруппы.Удалить(ИндексСтрокиТаблицы);
		ДобавляемаяСтрока = СтруктураГруппы.Добавить();
		
		Для Каждого СтрокаТаблицы Из СтруктураГруппы Цикл
			Для Каждого КолонкаТаблицы Из СтруктураГруппы.Колонки Цикл
				НомерСтрокиВТаблице      = СтруктураГруппы.Индекс(СтрокаТаблицы);
				НомерСтрокиВФормеТаблицы = НомерСтрокиВТаблице + 1;
				ИмяЯчейки                = КолонкаТаблицы.Имя + "_" + Формат(НомерСтрокиВФормеТаблицы, "ЧГ=0");
				ОбластьЯчейкиТаблицы     = ТекТабличноеПоле.Области[ИмяЯчейки];
			КонецЦикла;
		КонецЦикла;
		
		ВывестиРазделВТабличныйДокумент(ИмяГруппы, СтруктураГруппы);
		
	КонецЕсли;
	
	НомТекущейСтроки  = Мин( НомУдаляемойСтроки, СтруктураГруппы.Количество() );
	ИмяТекущейОбласти = СтруктураГруппы.Колонки[0].Имя + "_" + Формат(НомТекущейСтроки,"ЧГ=0");
	ТекТабличноеПоле.ТекущаяОбласть = ТекТабличноеПоле.Области[ИмяТекущейОбласти];
	ТекущийЭлемент = ТекТабличноеПоле;
	Модифицированность = Истина;
	
КонецПроцедуры // УдалитьДополнительнуюСтроку()

Процедура ПронумероватьТаблицыДекларация()
	
	ЭтоПерваяСтраница = мСтруктураМногостраничныхРазделов.Декларация[0].АктивнаяСтраница;
	
	Если ЭтоПерваяСтраница Тогда
		ПроставитьНомераВТаблицеДекларация("П0000100001");
	КонецЕсли;
	
	ПроставитьНомераВТаблицеДекларация("П0000100003");
	
КонецПроцедуры

Процедура ПроставитьНомераВТаблицеДекларация(ПрефиксИменТаблицы)
	
	ИмяГрафыНомераПП = ПрефиксИменТаблицы + "00";
	
	НомерСтроки = 1;
	Постфикс = "_" + Формат(НомерСтроки, "ЧГ=0");
	Пока ПТД_Декларация.Области.Найти(ИмяГрафыНомераПП + Постфикс) <> Неопределено Цикл
		ПТД_Декларация.Области[ИмяГрафыНомераПП + Постфикс].Значение = НомерСтроки;
		
		НомерСтроки = НомерСтроки + 1;
		Постфикс = "_" + Формат(НомерСтроки, "ЧГ=0");
	КонецЦикла;
	
	
КонецПроцедуры

Процедура ОбновитьПользовательскиеНастройки()

	Сообщить("Изменилась версия формы отчета.
				|Выполняется обновление пользовательских настроек.", СтатусСообщения.Информация);

	// Обновляем варианты заполнения показателей отчета
	ЕстьИзмененияВариантыЗаполнения = ОбновитьВариантыЗаполнения();

	// Обновляем настройки страниц отчета (содержание отчета)
	ЕстьИзмененияНастройкаСтраниц   = ОбновитьНастройкиСтраниц();

	Если ЕстьИзмененияВариантыЗаполнения ИЛИ ЕстьИзмененияНастройкаСтраниц Тогда
		// Если были изменения в пользовательских настройках,
		// то устанавливаем флаг модифицированности формы.
		Модифицированность = Истина;

		Сообщить("Обновление пользовательских настроек успешно завершено!");

	Иначе

		Сообщить("Изменения в структуре пользовательских настроек не обнаружены!");

	КонецЕсли;

КонецПроцедуры // ОбновитьПользовательскиеНастройки()

// Открывает форму документа ФормаНастройкиОтчета с целью выбора 
// выводимых на печать листов отчета.
//
// Параметры:
//   ВариантВыбора - строка, назначение выбора страниц.
//
Процедура ОткрытьФормуВыбораСтраниц(ВариантВыбора)

	ФормаНастройкиОтчета = РегламентированнаяОтчетность.роПолучитьОбщуюФорму("НастройкиОтчета", ЭтаФорма);

	Если ФормаНастройкиОтчета.Открыта() Тогда
		ФормаНастройкиОтчета.Активизировать();
		Если Вопрос("Предыдущая операция выбора листов не завершена.
			|Завершить?",РежимДиалогаВопрос.ДаНет,30,КодВозвратаДиалога.Да)=КодВозвратаДиалога.Да Тогда
			ФормаНастройкиОтчета.Закрыть();
		КонецЕсли;
	КонецЕсли;

	мДеревоВыбранныхСтраниц = "";

	ДеревоСтраницОтчета = мДеревоСтраницОтчета.Скопировать();

	ФормаНастройкиОтчета.НачальноеЗначениеВыбора = ДеревоСтраницОтчета;

	ФормаНастройкиОтчета.мПараметры.Выгрузка = Ложь;

	РезультатВыбора = ФормаНастройкиОтчета.ОткрытьМодально();
	Если РезультатВыбора <> Неопределено Тогда
		мДеревоВыбранныхСтраниц = РезультатВыбора;
		Модифицированность = Истина;
	Иначе
		мДеревоВыбранныхСтраниц = ДеревоСтраницОтчета;
	КонецЕсли;

КонецПроцедуры // ОткрытьФормуВыбораСтраниц()

// Процедура очищает содержимое редактируемых и вычисляемых ячеек
// поля табличного документа, переданного параметром ВыбТабличноеПоле.
//
// Параметры:
//  ВыбТабличноеПоле - поле табличного документа.
//
Процедура ОчиститьТабличноеПоле(ВыбТабличноеПоле) Экспорт
	
	ИмяТабличногоПоля = ВыбТабличноеПоле.Имя;
	ИмяСтраницыФормы  = СтрЗаменить(ИмяТабличногоПоля, "ПолеТабличногоДокумента", "");
	
	// Сначала получим структуру данных многострочных блоков табличного поля.
	СтруктураДанныхДопСтрокРаздела = СформироватьСтруктуруДанныхДопСтрокСтраницы(ИмяСтраницыФормы);
	
	// Предварительно проверим наличие дополнительных строк,
	// в заданном поле табличного документа.
	// При наличии таких строк удаляем их.
	Если СтруктураДанныхДопСтрокРаздела <> Неопределено Тогда
		// Табличное поле содержит многострочные блоки.
		// Удалим все созданные дополнительные строки в блоке.
		Для Каждого МногострочныйБлок Из СтруктураДанныхДопСтрокРаздела Цикл
			
			ИмяГруппыДопСтрок = МногострочныйБлок.Ключ;
			
			// Получим таблицу значений, содержащую данные дополнительных строк.
			ПромТаблицаДопСтрокРаздела = Новый ТаблицаЗначений;
			мСтруктураМногострочныхРазделов.Свойство(ИмяГруппыДопСтрок, ПромТаблицаДопСтрокРаздела);
			
			ИсходноеКоличествоСтрокГруппы = 0;
			мСтруктураИсхКолвоСтрокРазделов.Свойство(ИмяГруппыДопСтрок, ИсходноеКоличествоСтрокГруппы);
			
			ТекущееКоличествоСтрок = ПромТаблицаДопСтрокРаздела.Количество();
			
			Если ТекущееКоличествоСтрок > ИсходноеКоличествоСтрокГруппы Тогда
				// Имеются дополнительные строки.
				// Удаляем их.
				
				НСтр = ПромТаблицаДопСтрокРаздела.Количество();
				Пока НСтр > ИсходноеКоличествоСтрокГруппы Цикл
					
					ТекСтрокаТаблицы = ПромТаблицаДопСтрокРаздела[НСтр - 1];
					
					ИмяПервойКолонки = ПромТаблицаДопСтрокРаздела.Колонки[0].Имя;
					ИмяПервойЯчейкиУдаляемойСтроки  = ИмяПервойКолонки + "_" + Формат(НСтр, "ЧГ=0");
					
					// область первой ячейки удаляемой строки многострочного раздела
					ОбластьЯчейки = ВыбТабличноеПоле.Области[ИмяПервойЯчейкиУдаляемойСтроки];
					
					// номера строк удаляемой области
					НомерВерхЯчейкиОбласти = ОбластьЯчейки.Верх;
					НомерНижнЯчейкиОбласти = ОбластьЯчейки.Низ;
					
					// Определим область удаляемой строки
					УдаляемаяОбласть = ВыбТабличноеПоле.Область(НомерВерхЯчейкиОбласти, , НомерНижнЯчейкиОбласти, );
					
					// непосредственно удаляем область строки табличного документа
					ВыбТабличноеПоле.УдалитьОбласть( УдаляемаяОбласть,ТипСмещенияТабличногоДокумента.ПоВертикали );
					
					// удаляем из структуры многострочного раздела соответствующую строку
					ПромТаблицаДопСтрокРаздела.Удалить(ТекСтрокаТаблицы);
					
					НСтр = НСтр - 1;
					
				КонецЦикла;
				
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	// Непосредственно очищаем ячейки табличного документа.
	Для Инд = 0 По ВыбТабличноеПоле.Области.Количество() - 1 Цикл
		ТекущаяОбласть = ВыбТабличноеПоле.Области[Инд];
		
		Если Не ТекущаяОбласть.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекущаяОбласть.СодержитЗначение <> Истина Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяОбласти = ТекущаяОбласть.Имя;
		Если ТекущаяОбласть.Защита И НЕ Найти(ИмяОбласти, "П0000") = 1 Тогда
			Продолжить;
		КонецЕсли;
		
		ТекущаяОбласть.Очистить();
		
	КонецЦикла;
	
	Если СтруктураДанныхДопСтрокРаздела <> Неопределено Тогда
		Для Каждого МногострочныйБлок Из СтруктураДанныхДопСтрокРаздела Цикл
			ИмяГруппыДопСтрок = МногострочныйБлок.Ключ;
			ОбновитьСтруктуруМногострочногоРаздела(ИмяГруппыДопСтрок);
		КонецЦикла;
	КонецЕсли;
	
	Расчет(ИмяСтраницыФормы);
	
КонецПроцедуры // ОчиститьТабличноеПоле()

Процедура СпециальныеОперацииПослеОчисткиЛиста(ИмяСтраницы, УдалятьсяВсеДопЭкземпляры) Экспорт
	
	ОчиститьСлужебныеДанныеРаздела(ИмяСтраницы);
	
КонецПроцедуры

// Обрабатывает полученный из формы ФормаНастройкиОтчета дерево значений,
// определяет состав помеченных для вывода на печать листов.
//
// Параметры:
//  ВидПечати - строка, задающая способ печати:
//              "ПоказатьБланк" - непосредственный вывод на печать;
//              "ПечататьСразу" - с предварительным просмотром.
//
Процедура Печать(ВидПечати, ЧтоИскать = Неопределено) Экспорт
	
	Если ЧтоИскать = Неопределено Тогда
		Отказ = Ложь;
		РегламентированнаяОтчетность.ПередПечатьюРегламентированногоОтчета(ЭтаФорма, Отказ);
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ЧтоИскать = Неопределено И НЕ РегламентированнаяОтчетность.ПринтерДоступен() Тогда
		Предупреждение("Перед формированием печатных форм необходимо определить в системе принтер и
		               |задать его в качестве используемого по умолчанию!");
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(мДеревоВыбранныхСтраниц) <> Тип("ДеревоЗначений") Тогда
		Возврат;
	КонецЕсли;
	
	мПечатныеФормы.Очистить();
	
	СтруктураПараметров = Новый Структура;
	
	мРежимПечати = Истина;
	Для Каждого СтрокаУровня1 Из мДеревоВыбранныхСтраниц.Строки Цикл
		Пометка = РегламентированнаяОтчетность.ПечататьРаздел(ЭтаФорма, СтрокаУровня1, ЧтоИскать);
		
		ИмяСтраницыПанели  = СтрокаУровня1.ИмяСтраницы;
		НаименованиеЛиста  = СтрокаУровня1.Представление;
		ОриентацияЛиста    = СтрокаУровня1.ОриентацияСтраницы;
		ИмяТабличногоПоля  = СтрокаУровня1.ИмяПоляТабличногоДокум;
		ИмяОбластиТаблПоля = СтрокаУровня1.ИмяОбластиТабличногоПоля;
		
		Если Пометка = 0 И ЧтоИскать = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураПараметров.Очистить();
		СтруктураПараметров.Вставить("НаименованиеЛиста",  НаименованиеЛиста);
		СтруктураПараметров.Вставить("ОриентацияЛиста",    ОриентацияЛиста);
		СтруктураПараметров.Вставить("ИмяТабличногоПоля",  ИмяТабличногоПоля);
		Структурапараметров.Вставить("ИмяОбластиТаблПоля", ИмяОбластиТаблПоля);
		СтруктураПараметров.Вставить("ВидПечати",          ВидПечати);
		СтруктураПараметров.Вставить("ЧтоИскать",          ЧтоИскать);
		
		Если Не мСтруктураМногостраничныхРазделов.Свойство(ИмяСтраницыПанели) Тогда
			ВывестиНаПечатьЛист(СтруктураПараметров);
		ИначеЕсли ЧтоИскать = Неопределено И ИмяСтраницыПанели = "Декларация" Тогда
			СтруктураПараметров.Вставить("ИмяСтраницыПанели", ИмяСтраницыПанели);
			ВывестиНаПечатьДекларация(СтруктураПараметров);
		Иначе
			// Вывод на печать всех страниц многостраничного раздела.
			СтруктураПараметров.Вставить("ИмяСтраницыПанели", ИмяСтраницыПанели);
			ВывестиНаПечатьДополнительныеСтраницы(СтруктураПараметров);
		КонецЕсли;
	КонецЦикла;
	
	// Обновляем исходное дерево значений.
	мДеревоСтраницОтчета = мДеревоВыбранныхСтраниц.Скопировать();
	мРежимПечати = Ложь;
	
	Если ЧтоИскать = Неопределено Тогда
		РегламентированнаяОтчетность.ПроставитьНомераЛистов(ЭтаФорма, ВидПечати);
	КонецЕсли;
	
КонецПроцедуры // Печать()

// Процедура управляет выводом отдельных страниц отчета на печать.
//
// Параметры:
//   Парам1 - структура, содержащая параметры печаты страницы:
//      - Имя выводимого на печать листа;
//      - ориентация страницы;
//      - имя табличного поля, который выводится на печать;
//      - имя области дабличного документа, выводимой на печать;
//      - строка, задающая способ печати.Принимает значения:
//           "ПечататьСразу" - непосредственный вывод на печать;
//           "ПечататьБланк" - с предварительным просмотром.
//
Процедура ВывестиНаПечатьЛист(Парам1)
	
	Если Парам1.ЧтоИскать <> Неопределено Тогда
		РегламентированнаяОтчетность.роНайтиЗначение(ЭтаФорма, Парам1);
	Иначе
		РегламентированнаяОтчетность.ВывестиНаПечатьЛистОбщая(ЭтаФорма, Парам1);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВывестиНаПечатьДекларация(СтруктураПараметровСтраницы)
	
	ЭтоПБОЮЛ = РегламентированнаяОтчетность.ЭтоПБОЮЛ(Организация);
	
	МакетПечати = ПолучитьМакет("ФормаПечати2012Кв3_Декларация");
	
	ПечатнаяФорма = Новый ТабличныйДокумент();
	ПечатнаяФорма.ОтображатьЗаголовки = Ложь;
	ПечатнаяФорма.ОтображатьСетку     = Ложь;
	ПечатнаяФорма.ЧерноБелыйПросмотр  = Истина;
	ПечатнаяФорма.ЧерноБелаяПечать    = Истина;
	ПечатнаяФорма.Автомасштаб         = Истина;
	ПечатнаяФорма.ОриентацияСтраницы  = СтруктураПараметровСтраницы.ОриентацияЛиста;
	
	Область_Шапка = МакетПечати.ПолучитьОбласть("Шапка");
	ПечатнаяФорма.Вывести(Область_Шапка);
	
	Область_РеквизитыОрганизации = МакетПечати.ПолучитьОбласть("РеквизитыОрганизации");
	Область_РеквизитыОрганизации.Области.ЗаголовокРеквизитов.Текст = "Сведения по организации";
	Наименование = СокрЛП(ПТД_Титульный.Области.ОргНазв.Значение);
	ИНН = РегламентированнаяОтчетность.роВернутьЗначениеПараметраСЛиста(ЭтаФорма, "Титульный", "ИНН1_");
	Если НЕ ЭтоПБОЮЛ И Лев(ИНН, 2) = "00" Тогда
		ИНН = Сред (ИНН, 3); // Удаление нулей, добавленных в соответствии с порядком заполнения.
	КонецЕсли;
	КПП = РегламентированнаяОтчетность.роВернутьЗначениеПараметраСЛиста(ЭтаФорма, "Титульный", "КПП1_");
	Адрес = Неопределено;
	АдресОрганизации = ДополнительныеДанныеПоАдресу(ДополнительныеДанные, "Титульный.АдресОрганизации");
	Если АдресОрганизации <> Неопределено Тогда
		АдресОрганизации.Свойство("Адрес", Адрес);
	КонецЕсли;
	Если Адрес= Неопределено Тогда
		Сведения = "АдрЮР";
		СведенияОбОрганизации = РегламентированнаяОтчетность.ПолучитьСведенияОбОрганизации(Организация, ДатаПодписи, Сведения);
		Адрес = РегламентированнаяОтчетность.ПредставлениеАдресаВФормате9Запятых(СведенияОбОрганизации.АдрЮр);
	КонецЕсли;
	ТекстРеквизитов = Наименование + ", ИНН " + ИНН + ", КПП " + КПП + ", Адрес: " + Адрес;
	Область_РеквизитыОрганизации.Области.Реквизиты.Текст = ТекстРеквизитов;
	ПечатнаяФорма.Вывести(Область_РеквизитыОрганизации);
	
	ДанныеПоОрганизации = мСтруктураМногостраничныхРазделов.Декларация[0].ДанныеДопСтрок.П0000100001.Скопировать();
	Для Каждого СтрокаДанных Из ДанныеПоОрганизации Цикл
		Область_Строка = МакетПечати.ПолучитьОбласть("Строка");
		Для Каждого Колонка Из ДанныеПоОрганизации.Колонки Цикл
			ИмяКолонки = Колонка.Имя;
			ИмяКолонкиМакета = СтрЗаменить(ИмяКолонки, "П0000100001", "П0000100003");
			Если Область_Строка.Области.Найти(ИмяКолонкиМакета) <> Неопределено Тогда
				Область_Строка.Области[ИмяКолонкиМакета].Значение = СтрокаДанных[ИмяКолонки];
			КонецЕсли;
		КонецЦикла;
		ПечатнаяФорма.Вывести(Область_Строка);
	КонецЦикла;
	
	ДанныеПоОрганизации.Свернуть("П000010000102, П000010000120", "П000010000117, П000010000123, П000010000124");
	Для Каждого СтрокаДанных Из ДанныеПоОрганизации Цикл
		Если НЕ (ЗначениеЗаполнено(СтрокаДанных.П000010000102) ИЛИ ЗначениеЗаполнено(СтрокаДанных.П000010000120)) Тогда
			Продолжить;
		КонецЕсли;
		
		Область_Строка = МакетПечати.ПолучитьОбласть("СтрокаИтого");
		Область_Строка.Области.ОписаниеИтого.Текст = "Итого по виду продукции " + СтрокаДанных.П000010000102 + " и грузополучателю " + СтрокаДанных.П000010000120;
		Для Каждого Колонка Из ДанныеПоОрганизации.Колонки Цикл
			ИмяКолонки = Колонка.Имя;
			ИмяКолонкиМакета = СтрЗаменить(ИмяКолонки, "П0000100001", "П0000100004");
			Если Область_Строка.Области.Найти(ИмяКолонкиМакета) <> Неопределено Тогда
				Область_Строка.Области[ИмяКолонкиМакета].Значение = СтрокаДанных[ИмяКолонки];
			КонецЕсли;
		КонецЦикла;
		ПечатнаяФорма.Вывести(Область_Строка);
	КонецЦикла;
	
	Область_Строка = МакетПечати.ПолучитьОбласть("СтрокаИтого");
	Область_Строка.Области.ОписаниеИтого.Текст = "ИТОГО";
	Для Каждого ЭлементДанных Из мСтруктураМногостраничныхРазделов.Декларация[0].Данные Цикл
		ИмяКолонкиМакета = ЭлементДанных.Ключ;
		Если Найти(ИмяКолонкиМакета, "П0000100002") = 1 Тогда
			ИмяКолонкиМакета = СтрЗаменить(ИмяКолонкиМакета, "П0000100002", "П0000100004");
			Если Область_Строка.Области.Найти(ИмяКолонкиМакета) <> Неопределено Тогда
				Область_Строка.Области[ИмяКолонкиМакета].Значение = ЭлементДанных.Значение;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	ПечатнаяФорма.Вывести(Область_Строка);
	
	Для Каждого Лист Из мСтруктураМногостраничныхРазделов.Декларация Цикл
		
		Область_ПустаяСтрока = МакетПечати.ПолучитьОбласть("ПустаяСтрока");
		ПечатнаяФорма.Вывести(Область_ПустаяСтрока);
		
		ДанныеЛиста = Лист.Данные;
		ДанныеПоПодразделению = Лист.ДанныеДопСтрок.П0000100003.Скопировать();
		Область_РеквизитыОрганизации = МакетПечати.ПолучитьОбласть("РеквизитыОрганизации");
		Область_РеквизитыОрганизации.Области.ЗаголовокРеквизитов.Текст = "Сведения по обособленному подразделению (с указанием ИНН, КПП и адреса)";
		Область_РеквизитыОрганизации.Области.Реквизиты.Текст = ДанныеЛиста.ОбособленноеПодразделение;
		ПечатнаяФорма.Вывести(Область_РеквизитыОрганизации);
		
		Для Каждого СтрокаДанных Из ДанныеПоПодразделению Цикл
			Область_Строка = МакетПечати.ПолучитьОбласть("Строка");
			Для Каждого Колонка Из ДанныеПоПодразделению.Колонки Цикл
				ИмяКолонки = Колонка.Имя;
				ИмяКолонкиМакета = ИмяКолонки;
				Если Область_Строка.Области.Найти(ИмяКолонкиМакета) <> Неопределено Тогда
					Область_Строка.Области[ИмяКолонкиМакета].Значение = СтрокаДанных[ИмяКолонки];
				КонецЕсли;
			КонецЦикла;
			ПечатнаяФорма.Вывести(Область_Строка);
		КонецЦикла;
		
		ДанныеПоПодразделению.Свернуть("П000010000302, П000010000320", "П000010000317, П000010000323, П000010000324");
		Для Каждого СтрокаДанных Из ДанныеПоПодразделению Цикл
			Если НЕ (ЗначениеЗаполнено(СтрокаДанных.П000010000302) ИЛИ ЗначениеЗаполнено(СтрокаДанных.П000010000320)) Тогда
				Продолжить;
			КонецЕсли;
			Область_Строка = МакетПечати.ПолучитьОбласть("СтрокаИтого");
			Область_Строка.Области.ОписаниеИтого.Текст = "Итого по виду продукции " + СтрокаДанных.П000010000302 + " и грузополучателю " + СтрокаДанных.П000010000320;
			Для Каждого Колонка Из ДанныеПоПодразделению.Колонки Цикл
				ИмяКолонки = Колонка.Имя;
				ИмяКолонкиМакета = СтрЗаменить(ИмяКолонки, "П0000100003", "П0000100004");
				Если Область_Строка.Области.Найти(ИмяКолонкиМакета) <> Неопределено Тогда
					Область_Строка.Области[ИмяКолонкиМакета].Значение = СтрокаДанных[ИмяКолонки];
				КонецЕсли;
			КонецЦикла;
			ПечатнаяФорма.Вывести(Область_Строка);
		КонецЦикла;
		
		Область_Строка = МакетПечати.ПолучитьОбласть("СтрокаИтого");
		Область_Строка.Области.ОписаниеИтого.Текст = "ИТОГО";
		Для Каждого ЭлементДанных Из Лист.Данные Цикл
			ИмяКолонкиМакета = ЭлементДанных.Ключ;
			Если Найти(ИмяКолонкиМакета, "П0000100004") = 1 Тогда
				Если Область_Строка.Области.Найти(ИмяКолонкиМакета) <> Неопределено Тогда
					Область_Строка.Области[ИмяКолонкиМакета].Значение = ЭлементДанных.Значение;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		ПечатнаяФорма.Вывести(Область_Строка);
		
	КонецЦикла;
	
	Область_Подвал = МакетПечати.ПолучитьОбласть("Подвал");
	Область_Подвал.Области.Руководитель.Значение = мСтруктураМногостраничныхРазделов.Декларация[0].Данные.Руководитель;
	Область_Подвал.Области.Бухгалтер.Значение = мСтруктураМногостраничныхРазделов.Декларация[0].Данные.Бухгалтер;
	ПечатнаяФорма.Вывести(Область_Подвал);
	
	ПромежуточныйСписок = Новый СписокЗначений;
	ПромежуточныйСписок.Добавить(ПечатнаяФорма, СтруктураПараметровСтраницы.НаименованиеЛиста);
	мПечатныеФормы.Добавить(ПромежуточныйСписок[0], СтруктураПараметровСтраницы.НаименованиеЛиста);
	
КонецПроцедуры

// Процедура управляет выводом всех страниц многостраничного раздела на печать.
//
// Параметры:
//   СтруктураПараметровСтраницы - структура, содержащая параметры печаты отчета:
//
//      - имя страницы основной панели формы (многостраничного раздела);
//      - имя выводимого на печать листа;
//      - ориентация страницы;
//      - имя табличного документа, который выводится на печать;
//      - имя области табличного документа, выводимой на печать (содержит не пустое значение,
//        когда выводится на печать не весь табличный документ, а часть его);
//      - строка, задающая способ печати.Принимает значения:
//           "ПечататьСразу" - непосредственный вывод на печать;
//           "ПечататьБланк" - с предварительным просмотром.
//
Процедура ВывестиНаПечатьДополнительныеСтраницы(СтруктураПараметровСтраницы, ТабличныеДокументы = Неопределено)
	Перем ИмяСтраницы; // имя страницы основной панели формы;
	Перем ТаблицаСтраницРаздела;
	
	СтруктураПараметровСтраницы.Свойство("ИмяСтраницыПанели", ИмяСтраницы);
	
	Если Не мСтруктураМногостраничныхРазделов.Свойство(ИмяСтраницы, ТаблицаСтраницРаздела) Тогда
		Возврат;
	КонецЕсли;
	
	АктивнаяСтраницаРаздела = ТаблицаСтраницРаздела.Найти(Истина, "АктивнаяСтраница");
	Если АктивнаяСтраницаРаздела = Неопределено Тогда
		ВывестиНаПечатьЛист(СтруктураПараметровСтраницы);
		
		Возврат;
	КонецЕсли;
	
	ИсходныйИндекс = ТаблицаСтраницРаздела.Индекс(АктивнаяСтраницаРаздела);
	
	Для Каждого Страница Из ТаблицаСтраницРаздела Цикл
		// выводим на печать каждую страницу многостраничного раздела
		ТекущийИндекс = ТаблицаСтраницРаздела.Индекс(Страница);
		ПоказатьСтраницуМногостраничногоРаздела(ТекущийИндекс, ИмяСтраницы, Истина);
		ВывестиНаПечатьЛист(СтруктураПараметровСтраницы);
	КонецЦикла;
	
	// вывод в форму данных исходной (активной) страницы
	ПоказатьСтраницуМногостраничногоРаздела(ИсходныйИндекс, ИмяСтраницы, Истина);
	
КонецПроцедуры // ВывестиНаПечатьДополнительныеСтраницы()

// Формирует структуру данных отчета и помещает в реквизит типа ХранилищеЗначения
// документа РегламентированныйОтчет.
// 
// При первом сохранении данных отчета создает новый документ этого вида.
// Структура данных отчета состоит из элементов:
//   - значения редактируемых реквизитов отчета;
//   - данные по многострочным разделам отчета;
//   - данные по  многостраничным разделам отчета;
//   - данные по вариантам заполнения реквизитов,
//     участвующих в автозаполнении.
//
Функция Сохранить(Автосохранение = Ложь) Экспорт
	
	Перем ТаблицаСтраниц;
	
	Вариант = ВидДокумента * НомерКорректировки;
	
	КодИФНС = "";
	
	КПП = "";
	Для Инд = 1 по 9 Цикл
		КПП = КПП + ПТД_Титульный.Области["КПП1_" + Строка(Инд)].Значение;
	КонецЦикла;

	Если НЕ РегламентированнаяОтчетность.ПриЗаписиРегламентированногоОтчета(ЭтаФорма, КодИФНС, Автосохранение, Вариант, , КПП) Тогда
		Возврат Ложь;
	КонецЕсли;

	мСохраненныйДок.КПП                = КПП;
	мСохраненныйДок.ИсточникОтчета     = ЭтотОбъект.Метаданные().Имя;
	мСохраненныйДок.НаименованиеОтчета = ЭтотОбъект.Метаданные().Формы.ОсновнаяФорма.Представление();
	мСохраненныйДок.ДатаНачала         = мДатаНачалаПериодаОтчета;
	мСохраненныйДок.ДатаОкончания      = мДатаКонцаПериодаОтчета;
	мСохраненныйДок.Периодичность      = мПериодичность;
	мСохраненныйДок.Комментарий        = Комментарий;
	мСохраненныйДок.ВыбраннаяФорма     = мВыбраннаяФорма;
	мСохраненныйДок.Организация        = Организация;
	мСохраненныйДок.ДатаПодписи        = ДатаПодписи;
	мСохраненныйДок.ВидОтчетности      = Перечисления.ВидыОтчетности.РегламентированнаяОтчетность;
	мСохраненныйДок.КодИМНС            = КодИФНС;
	
	мСохраненныйДок.Вид                = Вариант;
	
	мВариант = Вариант;
	
	мСохраненныйДок.ФорматВыгрузки = "Автоматически";
	
	ОбновитьСтруктуруМногострочныхРазделов();
	
	ОбновитьСтруктуруМногостраничныхРазделов();
	
	ПоказателиОтчета = Новый Структура();
	
	Для Каждого СтраницаПанели из ЭлементыФормы.ОсновнаяПанель.Страницы Цикл
		
		ЭтоМногостраничныйРаздел = мСтруктураМногостраничныхРазделов.Свойство(СтраницаПанели.Имя, ТаблицаСтраниц);
		
		Если НЕ ЭтоМногостраничныйРаздел Тогда
			ИмяТекТабличногоПоля    = "ПолеТабличногоДокумента" + СтраницаПанели.Имя;
			ТекТабличноеПоле        = ЭлементыФормы[ИмяТекТабличногоПоля];
			
			ПоказателиТекущегоЛиста = СобратьДанныеТекущегоТаблПоля(ТекТабличноеПоле);
			ПоказателиОтчета.Вставить(ИмяТекТабличногоПоля, ПоказателиТекущегоЛиста);
		КонецЕсли;
		
	КонецЦикла;
	
	СписокСохранения = Новый Структура();
	
	СписокСохранения.Вставить("ПоказателиОтчета", ПоказателиОтчета);
	
	СписокСохранения.Вставить("ПризнакФормыОтчетности", ПризнакФормыОтчетности);
	
	СписокСохранения.Вставить("ДанныеМногострочныхРазделов", мСтруктураМногострочныхРазделов);
	
	СписокСохранения.Вставить("ДанныеМногостраничныхРазделов", мСтруктураМногостраничныхРазделов);
	
	СписокСохранения.Вставить("ДанныеВариантовАвтоЗаполнения", мСтруктураВариантыЗаполнения);
	
	СписокСохранения.Вставить("ФлПодсчетСтраниц", мСчетчикСтраниц);
	
	СписокСохранения.Вставить("ФлОтклВыборЗначений", мАвтоВыборКодов);
	
	СписокСохранения.Вставить("Автозаполнение", ФлажокОтклАвтоРасчет);
	
	СписокСохранения.Вставить("ВерсияФормы", мВерсияФормы);
	
	СписокСохранения.Вставить("ДополнительныеДанные", ДополнительныеДанные);
	
	ХранилищеДанных = Новый ХранилищеЗначения(СписокСохранения);
	мСохраненныйДок.ДанныеОтчета = ХранилищеДанных;
	
	Попытка
		мСохраненныйДок.Записать();
	Исключение
		Если НЕ Автосохранение Тогда
			Предупреждение(ОбщегоНазначения.СформироватьТекстСообщения(ОписаниеОшибки()));
		КонецЕсли;
		РегламентированнаяОтчетность.СкрытьКартинкуСохранение(ЭтаФорма);
		Возврат Ложь;
	КонецПопытки;
	
	РегламентированнаяОтчетность.ПослеЗаписиРегламентированногоОтчета(ЭтаФорма, "ФСРАР");
	
	СохранитьДеревоСтраницОтчета();
	
	КлючУникальности = мСохраненныйДок.Ссылка;
	
	Модифицированность = Ложь;
	
	Возврат Истина;
	
КонецФункции // Сохранить()

// Восстанавливает сохраненные данные отчета.
//
Процедура ВосстановитьСохраненныеДанные()
	Перем ДанныеМногострочныхРазделов;
	Перем ДанныеМногостраничныхРазделов;
	Перем ДанныеВариановАвтоЗаполнения;
	Перем ПоказателиОтчета;
	Перем ВерсияФормы;
	Перем Автозаполнение;
	Перем ТаблицаМногостраничногоДокумента;

	// В случае, если формы была скопирована по F9, то проверим, не изменилась ли организация.
	Если Организация <> мСохраненныйДок.Организация
	   И Организация <> ОбщегоНазначения.ПустоеЗначениеТипа("СправочникСсылка.Организации")
	   И Организация <> Неопределено Тогда
		// Меняем переменную, для того, чтобы запустилась процедура ЗаполнитьСведенияОбОрганизации
		мВПрограммеИзмененаОрганизация = Истина;
		мСохраненныйДок.Организация = Организация;
	Иначе
		// восстанавливаем реквизиты отчета
		Организация              = мСохраненныйДок.Организация;
	КонецЕсли;

	ДатаПодписи = мСохраненныйДок.ДатаПодписи;
	Комментарий = мСохраненныйДок.Комментарий;

	// восстанавливаем сохраненные данные отчета
	СписокСохранения = мСохраненныйДок.ДанныеОтчета.Получить();

	// список листов, выводимых на печать
	ДеревоНастройкиСтраниц = мСохраненныйДок.ДеревоНастройкиСтраниц.Получить();
	мДеревоСтраницОтчета   = ДеревоНастройкиСтраниц.Скопировать();

	// восстанавливаем сохраненные данные многострочных разделов
	Если СписокСохранения.Свойство( "ДанныеМногострочныхРазделов", ДанныеМногострочныхРазделов) Тогда
		мСтруктураМногострочныхРазделов = ДанныеМногострочныхРазделов;

		Для Каждого ЭлементСтруктуры Из мСтруктураМногострочныхРазделов Цикл
			ИмяГруппы       = ЭлементСтруктуры.Ключ;
			СтруктураГруппы = ЭлементСтруктуры.Значение;

			ВывестиРазделВТабличныйДокумент(ИмяГруппы, СтруктураГруппы);
		КонецЦикла;
	КонецЕсли;

	// восстанавливаем сохраненные данные вариантов автозаполнения ячеек
	Если СписокСохранения.Свойство("ДанныеВариантовАвтоЗаполнения", ДанныеВариановАвтоЗаполнения) Тогда
		мСтруктураВариантыЗаполнения = ДанныеВариановАвтоЗаполнения;
	КонецЕсли;

	// восстанавливаем версию формы
	Если СписокСохранения.Свойство("ВерсияФормы", ВерсияФормы) Тогда
		Если ВерсияФормы <> мВерсияФормы Тогда
			
			мСтараяВерсияФормы = ВерсияФормы;
			
			// Версия формы изменилась.
			// Обновляем пользовательские настройки.
			ОбновитьПользовательскиеНастройки();
		КонецЕсли;
	КонецЕсли;
	
	СписокСохранения.Свойство("ПоказателиОтчета", ПоказателиОтчета);
	
	СписокСохранения.Свойство("ПризнакФормыОтчетности", ПризнакФормыОтчетности);
	Если ПризнакФормыОтчетности = Неопределено Тогда
		ПризнакФормыОтчетности = 1;
	КонецЕсли;
	
	Если СписокСохранения.Свойство("ДанныеМногостраничныхРазделов", ДанныеМногостраничныхРазделов) Тогда
		мСтруктураМногостраничныхРазделов = ДанныеМногостраничныхРазделов;
		
		Если мСтруктураМногостраничныхРазделов.Лицензии.Колонки.Найти("ДополнительныеДанные") = Неопределено Тогда
			мСтруктураМногостраничныхРазделов.Лицензии.Колонки.Добавить("ДополнительныеДанные");
		КонецЕсли;
		Если мСтруктураМногостраничныхРазделов.Декларация.Колонки.Найти("ДополнительныеДанные") = Неопределено Тогда
			мСтруктураМногостраничныхРазделов.Декларация.Колонки.Добавить("ДополнительныеДанные");
		КонецЕсли;
		
		Для Каждого ЭлементСтруктуры Из мСтруктураМногостраничныхРазделов Цикл
			мСтруктураМногостраничныхРазделов.Свойство(ЭлементСтруктуры.Ключ, ТаблицаМногостраничногоДокумента);
			АктивнаяСтраница = ТаблицаМногостраничногоДокумента.Найти(Истина, "АктивнаяСтраница");
			ИндАктивнаяСтраница = ТаблицаМногостраничногоДокумента.Индекс(АктивнаяСтраница);
			ПоказатьСтраницуМногостраничногоРазделаПриВосстановлении(ИндАктивнаяСтраница, ЭлементСтруктуры.Ключ);
		КонецЦикла;
	КонецЕсли;
	
	СписокСохранения.Свойство("ФлПодсчетСтраниц", мСчетчикСтраниц);
	
	СписокСохранения.Свойство("ФлОтклВыборЗначений", мАвтоВыборКодов);
	Если мАвтоВыборКодов = Неопределено Тогда
		мАвтоВыборКодов = Ложь;
	КонецЕсли;

	СписокСохранения.Свойство("Автозаполнение", Автозаполнение);
	ФлажокОтклАвтоРасчет = Автозаполнение;

	СписокСохранения.Свойство("ДополнительныеДанные", ДополнительныеДанные);
	ВосстановитьСтруктуруДополнительныхДанных();
	
	Для Каждого ПоказателиСтраницы Из ПоказателиОтчета Цикл
		ИмяТекТабличноеПоле = ПоказателиСтраницы.Ключ;
		ТекТабличноеПоле    = ЭлементыФормы[ИмяТекТабличноеПоле];
		ПоказателиТаблПоле  = ПоказателиСтраницы.Значение;
		
		ПредставлениеДанных = РегламентированнаяОтчетность.роПолучитьСвойствоРаздела(ЭтаФорма, ИмяТекТабличноеПоле, "ПредставлениеДанных");
		
		Для Каждого Показатель Из ПоказателиТаблПоле Цикл
			ИмяПоказателя       = Показатель.Ключ;
			ЗначениеПоказателя  = Показатель.Значение;
				
			Если ПредставлениеДанных Тогда
				Если ОбновитьПеременныеМодуля(ИмяПоказателя, ЗначениеПоказателя) Тогда
					Продолжить; // Пропустим цикл
				КонецЕсли;
			КонецЕсли;
			
			Если ТекТабличноеПоле.Области.Найти(ИмяПоказателя) <> Неопределено Тогда
				ТекТабличноеПоле.Области[ИмяПоказателя].Значение = ЗначениеПоказателя;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	РегламентированнаяОтчетность.ОперацииПриВосстановленииРегламентированногоОтчета(ЭтаФорма);
	
	// Управляем видимостью страниц основной панели формы.
	мДеревоВыбранныхСтраниц = мДеревоСтраницОтчета.Скопировать();
	
	РегламентированнаяОтчетность.роПоказатьСтраницыОтчета(ЭтаФорма);
	
	Модифицированность = Ложь;
	
	мДокументВосстановлен = Истина;
	
КонецПроцедуры // ВосстановитьСохраненныеДанные()

// Процедура создает(удаляет) таблицу значений, содержащий параметры
// автозаполнения показателей дополнительного листа многолистового раздела.
//
// Параметры:
//  Парам - число, принимает значение:
//           1 - при создании дополнительного листа;
//          -1 - при удалении дополнительного листа;
// Лист - имя листа, необходимо для интерактивного добавления
// если Лист - неопределно, то текущий берется, равносильно нажатию на кнопку Добавить или Удалить
//
Процедура ИзменитьТаблицуПараметровАвтозаполнения(Парам, Лист = Неопределено)
	Перем МассивВариантовЗаполненияСтраниц;
	Перем ТаблицаВариантыЗаполнения;
	Перем ТаблицаСтраницРаздела;

	Если Лист = Неопределено Тогда
		ТекущаяСтраницаПанели    = ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница;
		ИмяТекущейСтраницыПанели = ТекущаяСтраницаПанели.Имя;
		Иначе
		//ТекущаяСтраницаПанели    = ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница;
		ИмяТекущейСтраницыПанели = Лист;
	КонецЕсли;

	Если Не мСтруктураВариантыЗаполнения.Свойство(ИмяТекущейСтраницыПанели, МассивВариантовЗаполненияСтраниц) Тогда
		Возврат;
	КонецЕсли;

	НомерТекущейСтраницы = 1;

	Если мСтруктураМногостраничныхРазделов.Свойство(ИмяТекущейСтраницыПанели, ТаблицаСтраницРаздела) Тогда
		// Раздел является многостраничным, определим активную страницу раздела:
		СтрТаблицаСтраницы = ТаблицаСтраницРаздела.Найти(Истина, "АктивнаяСтраница");
		Если СтрТаблицаСтраницы <> Неопределено Тогда
			НомерТекущейСтраницы = ТаблицаСтраницРаздела.Индекс(СтрТаблицаСтраницы) + 1;
		КонецЕсли;
	КонецЕсли;

	Если Парам = 1 Тогда // создаем таблицу параметров автозаполнения ячеек добавленного листа

		// При добавлении новой страницы параметры автозаполнения ее ячеек определяем
		// по эталонной модели
		НоваяТаблицаВариантыЗаполнения = РегламентированнаяОтчетность.роПолучитьТаблицуВариантовЗаполнения(ЭтаФорма, ИмяТекущейСтраницыПанели, НЕ ФлажокОтклАвтоРасчет, мОписаниеТиповЧислоЕ);

		Если НоваяТаблицаВариантыЗаполнения <> Неопределено Тогда

			ТаблВариантов = НоваяТаблицаВариантыЗаполнения.Скопировать();
			// Добавляем в массив таблицу значений, содержащей варианты заполнения
			// для добавленной страницы.
			МассивВариантовЗаполненияСтраниц.Добавить(ТаблВариантов);

		КонецЕсли;

	Иначе // Парам = -1. Удаляем таблицу параметров автозаполнения ячеек удаленного листа

		Если МассивВариантовЗаполненияСтраниц.Количество() > 1 Тогда 
			МассивВариантовЗаполненияСтраниц.Удалить(НомерТекущейСтраницы - 1);
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры // ИзменитьТаблицуПараметровАвтозаполнения()

// Выполняет расчет вычисляемых показателей на заданной странице отчета
// (ячеек, выделенных зеленым цветом). В случае отсутствия заданной
// страницы вызывает расчет всех вычисляемых показателей отчета.
//
// Параметры:
//  ИмяТекущейСтраницыПанели - строка, имя страницы основной панели формы.
//
Процедура Расчет(ИмяТекущейСтраницыПанели = "") Экспорт
	
	Если ФлажокОтклАвтоРасчет Или мРежимПечати Тогда
		Возврат;
	КонецЕсли;
	
	Если ИмяТекущейСтраницыПанели = "Декларация" Тогда
		РасчетДекларация();
		
	ИначеЕсли ИмяТекущейСтраницыПанели = "Лицензии" Тогда
		// Раздел не рассчитывается.
		
	ИначеЕсли ИмяТекущейСтраницыПанели = "" Тогда
		ПолныйРасчетДекларация();
		
	Иначе
		ВызватьИсключение "Вызов процедуры Расчет() с непредусмотренным параметром """ + ИмяТекущейСтраницыПанели + """";
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолныйРасчетДекларация()
	
	СтруктураРаздела = Неопределено;
	мСтруктураМногостраничныхРазделов.Свойство("Декларация", СтруктураРаздела);
	
	Для Каждого СтраницаРаздела Из СтруктураРаздела Цикл
		ОбработкаВТабличномПоле = СтраницаРаздела.АктивнаяСтраница;
		Если ОбработкаВТабличномПоле Тогда
			РазмещениеДанных = ПТД_Декларация;
		Иначе
			РазмещениеДанных = СтраницаРаздела.Данные;
		КонецЕсли;
		РасчетДекларация(РазмещениеДанных);
	КонецЦикла;
	
КонецПроцедуры

Процедура РасчетДекларация(Знач МестоРазмещенияДанных = Неопределено)
	
	Если ФлажокОтклАвтоРасчет Тогда
		Возврат;
	КонецЕсли;
	
	Если МестоРазмещенияДанных = Неопределено Тогда
		МестоРазмещенияДанных = ПТД_Декларация;
	КонецЕсли;
	
	РасчетДекларацияПоСекции(МестоРазмещенияДанных, мИдГруппы1, "П0000100001", "П0000100002");
	РасчетДекларацияПоСекции(МестоРазмещенияДанных, мИдГруппы2, "П0000100003", "П0000100004");
	
КонецПроцедуры

Процедура РасчетДекларацияПоСекции(МестоРазмещенияДанных, ИдГруппы, КодСекции, КодИтога)
	
	СписокСуммируемыхГраф = "17,23,24";
	
	СтруктураГруппы = Неопределено;
	мСтруктураМногострочныхРазделов.Свойство(ИдГруппы, СтруктураГруппы);
	
	МассивНомеровГраф = ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок(СписокСуммируемыхГраф);
	СуммыПоГрафам = Новый Соответствие;
	Для Каждого Графа Из МассивНомеровГраф Цикл
		СуммыПоГрафам.Вставить(Графа, 0);
	КонецЦикла;
	
	Если ТипЗнч(МестоРазмещенияДанных) = Тип("Структура") Тогда
		НомерСтроки = 1;
		ПостфиксСтроки = "_" + Формат(НомерСтроки, "ЧГ=0");
		Пока МестоРазмещенияДанных.Свойство(КодСекции + Графа + ПостфиксСтроки) Цикл
			КоличествоСтрок = НомерСтроки;
			НомерСтроки = НомерСтроки + 1;
			ПостфиксСтроки = "_" + Формат(НомерСтроки, "ЧГ=0");
		КонецЦикла;
	Иначе
		КоличествоСтрок = СтруктураГруппы.Количество();
	КонецЕсли;
	
	Для Инд = 1 По КоличествоСтрок Цикл
		ПостфиксСтроки = "_" + Формат(Инд, "ЧГ=0");
		
		// Расчет значений для строки ИТОГО.
		Для Каждого Графа Из МассивНомеровГраф Цикл
			ИмяПоказателя = КодСекции + Графа + ПостфиксСтроки;
			ЗначениеГрафы = ПолучитьДанныеИзМестаРазмещения(МестоРазмещенияДанных, ИмяПоказателя);
			СуммыПоГрафам.Вставить(Графа, СуммыПоГрафам[Графа] + ЗначениеГрафы);
		КонецЦикла;
	КонецЦикла;
	
	Для Каждого Графа Из МассивНомеровГраф Цикл
		ПоместитьДанныеПоМестуРазмещения(МестоРазмещенияДанных, КодИтога + Графа, СуммыПоГрафам[Графа]);
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьДанныеИзМестаРазмещения(МестоРазмещенияДанных, АдресДанных)
	
	Если ТипЗнч(МестоРазмещенияДанных) = Тип("Структура") Тогда
		ЗначениеВСтруктуре = Неопределено;
		Если МестоРазмещенияДанных.Свойство(АдресДанных, ЗначениеВСтруктуре) Тогда
			Возврат ЗначениеВСтруктуре;
		Иначе
			ВызватьИсключение "Невозможно получить данные из структуры: поле " + АдресДанных + " не существует";
		КонецЕсли;
	Иначе
		// Это должны быть данные в поле табличного документа.
		Возврат МестоРазмещенияДанных.Области[АдресДанных].Значение;
	КонецЕсли;
	
КонецФункции

Процедура ПоместитьДанныеПоМестуРазмещения(МестоРазмещенияДанных, АдресДанных, ЗначениеДанных)
	
	Если ТипЗнч(МестоРазмещенияДанных) = Тип("Структура") Тогда
		МестоРазмещенияДанных.Вставить(АдресДанных, ЗначениеДанных);
	Иначе
		МестоРазмещенияДанных.Области[АдресДанных].Значение = ЗначениеДанных;
	КонецЕсли;
	
КонецПроцедуры

Процедура Инициализация(БезОткрытияФормы = Ложь) Экспорт
	
	Перем ИсходноеКоличествоСтрокГруппы;
	
	мБезОткрытияФормы = БезОткрытияФормы;
	
	ИнициализироватьПоляТабличныхДокументов();
	ИнициализироватьДополнительныеДанные();
	
	// Формируем список элементов подменю, связанное с кнопкой <Печать>.
	ЭлементМеню = ЭлементыФормы.КоманднаяПанельДействий.Кнопки.ПодменюПечати;
	
	ЭлементМеню.Кнопки.Добавить("ПечататьСразу", ТипКнопкиКоманднойПанели.Действие,
	                            "Печатать сразу", Новый Действие("КоманднаяПанельДействийДействиеВыбратьПечатнуюФормы"));
	
	ЭлементМеню.Кнопки.Добавить("ПоказатьБланк", ТипКнопкиКоманднойПанели.Действие,
	                            "Показать бланк", Новый Действие("КоманднаяПанельДействийДействиеВыбратьПечатнуюФормы"));
	
	ЗаполнитьНачальныеНастройки();
	ФормироватьСтруктуруСвойствСтраниц();
	мРежимПечати = Ложь;
	
	мАвтоВыборКодов = Ложь;
	мВПрограммеИзмененаОрганизация = Ложь;
	мСтараяВерсияФормы = мВерсияФормы;
	мДокументВосстановлен = Ложь;
	
	ВидДокумента = 0;
	ЭлементыФормы.НомерКорректировки.Доступность = (ВидДокумента = 1);
	НомерКорректировки = 1;
	
	Если НЕ БезОткрытияФормы Тогда
		РегламентированнаяОтчетность.СформироватьСоставКнопкиОчиститьДляРегОтчетов(ЭтаФорма);
		РегламентированнаяОтчетность.роЗаполнитьЭлементыФормыДляСистемыАвтоЗаполнения(ЭтаФорма);
		СформироватьСпискиВыбора();
	КонецЕсли;
	
	Если мСохраненныйДок = Неопределено Тогда
		
		// Исходная структура многострочных разделов.
		Для Каждого ЭлементСтруктуры Из мСтруктураМногострочныхРазделов Цикл
			ИмяГруппы = ЭлементСтруктуры.Ключ;
			СтруктураГруппы = ЭлементСтруктуры.Значение;
			
			ТекТабличноеПоле = ОпределитьТабличноеПолеПоИдМногострочногоРаздела(ИмяГруппы);
			
			мСтруктураИсхКолвоСтрокРазделов.Свойство(ИмяГруппы, ИсходноеКоличествоСтрокГруппы);
			
			Для НСтр = 1 По  ИсходноеКоличествоСтрокГруппы Цикл
				
				СтрокаГруппы = СтруктураГруппы.Добавить();
				
				Для Каждого Колонка Из СтруктураГруппы.Колонки Цикл
					
					ИмяКолонки    = Колонка.Имя;
					КодПоказателя = ИмяКолонки + "_" + Формат(НСтр, "ЧГ=0");
					
					ОбластьЯчейки = ТекТабличноеПоле.Области[КодПоказателя];
					
					Если ОбластьЯчейки.СодержитЗначение = Истина Тогда
						ЗначениеЯчейки = ОбластьЯчейки.Значение;
					Иначе
						ЗначениеЯчейки = ОбластьЯчейки.Текст;
					КонецЕсли;
					
					СтрокаГруппы[ИмяКолонки] = ЗначениеЯчейки;
					
				КонецЦикла;
			КонецЦикла;
			
			ПронумероватьТаблицыДекларация();
			
		КонецЦикла;
		
		Если мСкопированаФорма <> Неопределено Тогда
			// Документ скопирован.
			ВосстановитьСохраненныеДанные();
		Иначе
			// Это новый документ.
			// Установим реквизиты формы по умолчанию.
			мДеревоВыбранныхСтраниц = мДеревоСтраницОтчета.Скопировать();
			ПТД_Титульный.Области.ВидДокументаПервичный.Значение = "V";
			РасставитьНомераСтраниц();
		КонецЕсли;
		
	Иначе
		// При открытии или при копировании сохраненного
		// отчета восстанавливаем сохраненные данные.
		ВосстановитьСохраненныеДанные();
		
		Если мСкопированаФорма <> Неопределено Тогда
			// Из-за смены формата выгрузки изменились коды видов деятельности
			ДатаКопируемогоОтчета = мСохраненныйДок.ДатаОкончания;
			ДатаЭтогоОтчета = мДатаКонцаПериодаОтчета;
			РазныеСпискиВидовДеятельности = (ДатаКопируемогоОтчета >= '20131101' И ДатаЭтогоОтчета < '20131101')
			                            Или (ДатаКопируемогоОтчета < '20131101' И ДатаЭтогоОтчета >= '20131101');
			Если РазныеСпискиВидовДеятельности Тогда
				Для Каждого Страница Из мСтруктураМногостраничныхРазделов.Лицензии Цикл
					Страница.Данные.П000000000105 = "";
					Страница.ДополнительныеДанные.П000000000105 = "";
				КонецЦикла;
				ПТД_Лицензии.Область("П000000000105").Значение = "";
				Сообщить(НСтр("ru='При копировании были очищены виды деятельности в разделе лицензий.'"));
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	
	ПоказатьПериод();
	
	Если мСохраненныйДок = Неопределено Или мВПрограммеИзмененаОрганизация Тогда
		ЗаполнитьСведенияОбОрганизации();
	Иначе
		Если НЕ мБезОткрытияФормы Тогда
			УправлениеЛистами();
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого СтрокаУровня1 Из мСвойстваРазделовДекларации.Строки Цикл
		// Считываем название раздела и его свойство в системе автозаполнения.
		НазваниеСтраницы           = СтрокаУровня1.ИмяСтраницы;
		МногостраничностьВРазделе  = СтрокаУровня1.МногостраничностьВРазделе;
		
		// Пропустим титульный лист, физлица, т.к. они не входят в систему автозаполнения.
		Если НазваниеСтраницы = "Титульный" Тогда
			Продолжить;
		КонецЕсли;
		
		// Если док-т восстановлен и многостраничный, то пропустим установку цветности, т.к. при восстановлении многостраничного раздела
		// цветность проставляется, чтобы дважды не выполнялся код, поставим ограничение.
		Если (мДокументВосстановлен) И (МногостраничностьВРазделе) Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ мБезОткрытияФормы Тогда
			УстановитьЦветФонаЯчейки( , , НазваниеСтраницы);
		КонецЕсли;
	
	КонецЦикла;
	
	Если мДатаКонцаПериодаОтчета >= '20131101' Тогда
		Если ЛицензируемаяДеятельность() Тогда
			УстановитьВариантДляЯчеекЛицензии(0);
		Иначе
			УстановитьВариантДляЯчеекЛицензии(5);
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ мБезОткрытияФормы Тогда
		РегламентированнаяОтчетность.роДоступностьКнопокДобавитьУдалить(ЭтаФорма);
		РегламентированнаяОтчетность.роПоказатьСтатикСтраницРаздела(ЭтаФорма, "Титульный");
	КонецЕсли;
	
	РегламентированнаяОтчетность.ПриИнициализацииФормыРегламентированногоОтчета(ЭтаФорма, "ФСРАР", Новый Действие("Подключаемый_КоманднаяПанельФормыПоказатьРезультат"));
	
КонецПроцедуры

Процедура ИнициализироватьПоляТабличныхДокументов()
	
	Макет = ЭтотОбъект.ПолучитьМакет("ФормаОтчета2012Кв3_Титульный");
	ПТД_Титульный = ЭлементыФормы.ПолеТабличногоДокументаТитульный;
	ПТД_Титульный.Очистить();
	ПТД_Титульный.Вывести(Макет);
	
	Макет = ЭтотОбъект.ПолучитьМакет("ФормаОтчета2012Кв3_Лицензии");
	ПТД_Лицензии = ЭлементыФормы.ПолеТабличногоДокументаЛицензии;
	ПТД_Лицензии.Очистить();
	ПТД_Лицензии.Вывести(Макет);
	
	Макет = ЭтотОбъект.ПолучитьМакет("ФормаОтчета2012Кв3_Декларация");
	ПТД_Декларация = ЭлементыФормы.ПолеТабличногоДокументаДекларация;
	ПТД_Декларация.Очистить();
	ПТД_Декларация.Вывести(Макет);
	
	ИнициализироватьПоляВводаТабличныхДокументов();
	
КонецПроцедуры

Процедура ИнициализироватьПоляВводаТабличныхДокументов()
	
	ПТД_Титульный.Области.СоставленаНа1.ЭлементУправления.Маска = "9";
	ПТД_Титульный.Области.СоставленаНа2.ЭлементУправления.Маска = "9";
	ПТД_Титульный.Области.СоставленаНа3.ЭлементУправления.Маска = "9";
	
	ПТД_Титульный.Области.Прил1.ЭлементУправления.Маска = "9";
	ПТД_Титульный.Области.Прил2.ЭлементУправления.Маска = "9";
	ПТД_Титульный.Области.Прил3.ЭлементУправления.Маска = "9";
	
	ПТД_Декларация.Области.П000010000102_1.ЭлементУправления.Маска = "999";
	ПТД_Декларация.Области.П000010000302_1.ЭлементУправления.Маска = "999";
	
	МаскаИНН = "9999999999";
	МаскаКПП = "999999999";
	
	ПТД_Декларация.Области.П000010000104_1.ЭлементУправления.Маска = МаскаИНН;
	ПТД_Декларация.Области.П000010000105_1.ЭлементУправления.Маска = МаскаКПП;
	
	ПТД_Декларация.Области.П000010000122_1.ЭлементУправления.Маска = МаскаКПП;
	
	ПТД_Декларация.Области.П000010000304_1.ЭлементУправления.Маска = МаскаИНН;
	ПТД_Декларация.Области.П000010000305_1.ЭлементУправления.Маска = МаскаКПП;
	
	ПТД_Декларация.Области.П000010000322_1.ЭлементУправления.Маска = МаскаКПП;
	
КонецПроцедуры

Процедура ИнициализироватьДополнительныеДанные()
	
	Если ТипЗнч(ДополнительныеДанные) <> Тип("Структура") Тогда
		ДополнительныеДанные = Новый Структура;
	КонецЕсли;
	
	Если НЕ ДополнительныеДанные.Свойство("Титульный")
	 ИЛИ ТипЗнч(ДополнительныеДанные.Титульный) <> Тип("Структура") Тогда
		ДополнительныеДанные.Вставить("Титульный", Новый Структура);
	КонецЕсли;
	
	АдресОрганизации = Новый Структура;
	АдресОрганизации.Вставить("КодСтраны",       "");
	АдресОрганизации.Вставить("Индекс",          "");
	АдресОрганизации.Вставить("КодРегиона",      "");
	АдресОрганизации.Вставить("Район",           "");
	АдресОрганизации.Вставить("Город",           "");
	АдресОрганизации.Вставить("НаселенныйПункт", "");
	АдресОрганизации.Вставить("Улица",           "");
	АдресОрганизации.Вставить("Дом",             "");
	АдресОрганизации.Вставить("Корпус",          "");
	АдресОрганизации.Вставить("Квартира",        "");
	
	Если ДополнительныеДанные.Титульный.Свойство("АдресОрганизации") Тогда
		ЗаполнитьЗначенияСвойств(АдресОрганизации, ДополнительныеДанные.Титульный.АдресОрганизации);
	КонецЕсли;
	ДополнительныеДанные.Титульный.Вставить("АдресОрганизации", АдресОрганизации);
	
	Если мСтруктураМногостраничныхРазделов.Лицензии.Колонки.Найти("ДополнительныеДанные") = Неопределено Тогда
		мСтруктураМногостраничныхРазделов.Лицензии.Колонки.Добавить("ДополнительныеДанные");
	КонецЕсли;
	
	Если мСтруктураМногостраничныхРазделов.Декларация.Колонки.Найти("ДополнительныеДанные") = Неопределено Тогда
		мСтруктураМногостраничныхРазделов.Декларация.Колонки.Добавить("ДополнительныеДанные");
	КонецЕсли;
	
КонецПроцедуры

Процедура ВосстановитьСтруктуруДополнительныхДанных()
	
	ИнициализироватьДополнительныеДанные();
	
КонецПроцедуры

Функция ОбновитьПеременныеМодуля(ИмяПоказателя, ЗначениеПоказателя) Экспорт
	
	ИзмененияПроизошли = Ложь;
	
	Если Лев(ИмяПоказателя, 1) = "_" Тогда
		ИзмененияПроизошли = Истина;
		Возврат ИзмененияПроизошли;
	КонецЕсли;
	
	Возврат ИзмененияПроизошли;
	
КонецФункции

Процедура СохранитьДеревоСтраницОтчета()

	Если мСохраненныйДок <> Неопределено Тогда

		Если Не мСохраненныйДок.ЭтоНовый() Тогда
			// Сохраняем выбранные листы для печати в хранилище
			ХранилищеДанных = Новый ХранилищеЗначения(мДеревоСтраницОтчета);
			мСохраненныйДок.ДеревоНастройкиСтраниц = ХранилищеДанных;
			Попытка
				мСохраненныйДок.Записать();
			Исключение
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // СохранитьДеревоСтраницОтчета

// Процедура вызывается при выборе пункта подменю "Печать" командной панели
// действий формы. 
// Отрабатывает выбор печатной формы.
//
// Обработчик уставливается динамически методом "Действие"
Процедура КоманднаяПанельДействийДействиеВыбратьПечатнуюФормы(Кнопка)

	Если Кнопка <> Неопределено Тогда 
		Печать(Кнопка.Имя);
	КонецЕсли;

КонецПроцедуры // КоманднаяПанельДействийДействиеВыбратьПечатнуюФормы()

Процедура ПолеВводаДатаСдачиПриИзменении(Элемент)
	
	ЗаполнитьСведенияОбОрганизации(Истина);
	мВПрограммеИзмененаОрганизация = Истина;
	
	ЗаполнитьДатуВЯчейках();
	
	мВПрограммеИзмененаОрганизация = Ложь;
	Модифицированность = Истина;
	
КонецПроцедуры

// Формирует дерево значений - структуру страниц отчета,
// содержащей настройки показа страниц, вывода на печать и выгрузки в ИМНС.
//
Процедура ФормироватьСтруктуруСтраницОтчета() Экспорт
	
	ОписаниеТиповСтрока100 = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(100);
	
	ОписаниеТиповЧисло1    = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(1);
	
	// Дерево содержит коллекцию страниц отчета.
	// В колонках дерева хранятся следующие данные:
	//    - представление листа;
	//    - имя страницы основной панели формы, содержащей выводимый на печать лист;
	//    - имя поля табличного документа;
	//    - имя области ячеек табличного документа (используется в случае вывода
	//      на печать отдельных страниц титульного листа);
	//    - ориентация страницы при печати;
	//    - число, метка вывода страницы на печать;
	//    - число, метка показа страницы на основной панели формы;
	//    - число, метка выгрузки данных страницы в ИМНС.
	//
	мДеревоСтраницОтчета               = Новый ДеревоЗначений;
	мДеревоСтраницОтчета.Колонки.Добавить("Представление",            ОписаниеТиповСтрока100);
	мДеревоСтраницОтчета.Колонки.Добавить("ИмяСтраницы",              ОписаниеТиповСтрока100);
	мДеревоСтраницОтчета.Колонки.Добавить("ИмяПоляТабличногоДокум",   ОписаниеТиповСтрока100);
	мДеревоСтраницОтчета.Колонки.Добавить("ИмяОбластиТабличногоПоля", ОписаниеТиповСтрока100);
	мДеревоСтраницОтчета.Колонки.Добавить("ОриентацияСтраницы");
	мДеревоСтраницОтчета.Колонки.Добавить("ВыводНаПечать",            ОписаниеТиповЧисло1);
	мДеревоСтраницОтчета.Колонки.Добавить("ПоказатьСтраницу",         ОписаниеТиповЧисло1);
	мДеревоСтраницОтчета.Колонки.Добавить("ВыгрузитьСтраницу",        ОписаниеТиповЧисло1);
	
	мДеревоСтраницОтчета.Строки.Очистить();
	
	СтрокаУровня1 = мДеревоСтраницОтчета.Строки.Добавить();
	СтрокаУровня1.ИмяСтраницы              = "Титульный";
	СтрокаУровня1.Представление            = "Титульный лист";
	СтрокаУровня1.ИмяПоляТабличногоДокум   = "ПолеТабличногоДокументаТитульный";
	СтрокаУровня1.ОриентацияСтраницы       = ОриентацияСтраницы.Портрет;
	СтрокаУровня1.ВыводНаПечать            = 1;
	СтрокаУровня1.ПоказатьСтраницу         = 1;
	СтрокаУровня1.ВыгрузитьСтраницу        = 2;
	
	СтрокаУровня1 = мДеревоСтраницОтчета.Строки.Добавить();
	СтрокаУровня1.ИмяСтраницы              = "Лицензии";
	СтрокаУровня1.Представление            = "Сведения лицензиях";
	СтрокаУровня1.ИмяПоляТабличногоДокум   = "ПолеТабличногоДокументаЛицензии";
	СтрокаУровня1.ОриентацияСтраницы       = ОриентацияСтраницы.Портрет;
	СтрокаУровня1.ВыводНаПечать            = 1;
	СтрокаУровня1.ПоказатьСтраницу         = 1;
	СтрокаУровня1.ВыгрузитьСтраницу        = 2;
	
	СтрокаУровня1 = мДеревоСтраницОтчета.Строки.Добавить();
	СтрокаУровня1.ИмяСтраницы              = "Декларация";
	СтрокаУровня1.Представление            = "Декларация";
	СтрокаУровня1.ИмяПоляТабличногоДокум   = "ПолеТабличногоДокументаДекларация";
	СтрокаУровня1.ОриентацияСтраницы       = ОриентацияСтраницы.Ландшафт;
	СтрокаУровня1.ВыводНаПечать            = 1;
	СтрокаУровня1.ПоказатьСтраницу         = 1;
	СтрокаУровня1.ВыгрузитьСтраницу        = 2;
	
КонецПроцедуры // ФормироватьСтруктуруСтраницОтчета()

// Выполняет начальные настройки параметров отчета.
// По данным макета "СоствПоказателей" формирует:
//   - состав показателей отчета;
//   - варианты заполнения показателей отчета;
//   - дерево выводимых на печать листов отчета.
//
Процедура ЗаполнитьНачальныеНастройки() Экспорт

	МакетСоставаПоказателей = ЭтотОбъект.ПолучитьМакет(мСоставПоказателей);

	ОписаниеТиповСтрока50  = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(50);

	ОписаниеТиповСтрока100 = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(100);

	ОписаниеТиповЧисло1    = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(1);

	Для Инд = 0 По МакетСоставаПоказателей.Области.Количество() - 1 Цикл

		ТекОбласть    = МакетСоставаПоказателей.Области[Инд];
		ИмяСтраницы   = ТекОбласть.Имя;

		// Таблица значений содержит информацию о вариантах заполнения показателя отчета.
		// В колонках таблицы значений отображается следующая информация:
		//   - имя ячейки (код показателя);
		//   - вариант заполнения ячейки;
		//   - вычисленное по данным ИБ значение ячейки;
		//   - величина автокорректировки значения ячейки;
		//   - примечание к ячейке.
		//
		ТаблицаВариантыЗаполнения = Новый ТаблицаЗначений;
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "КодПоказателя",     ОписаниеТиповСтрока50  );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "ВариантЗаполнения", ОписаниеТиповЧисло1    );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "ЗначениеАвто",      мОписаниеТиповЧислоЕ   );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "Дельта",            мОписаниеТиповЧислоЕ   );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "Комментарий",       ОписаниеТиповСтрока100 );
		ТаблицаВариантыЗаполнения.Колонки.Добавить( "КодПоказателяПоСтруктуре",       ОписаниеТиповСтрока50 );

		Для Ном = ТекОбласть.Верх По ТекОбласть.Низ Цикл
			// перебираем строки макета

			// Код показателя (по составу показателей) определяется по первой колонке макета
			КодПоказателя = СокрП(МакетСоставаПоказателей.Область(Ном, 1).Текст);
			ИмяПоказателя = КодПоказателя;

			Если КодПоказателя = "===" Тогда         // признак конечной строки
				Прервать;
			КонецЕсли;

			Если Лев(КодПоказателя, 2) = "//" Тогда  // пропускаем комментарии
				Продолжить;
			КонецЕсли;

			// код показателя по форме отчете (имя ячейки в полях табличного документа формы)
			КодПоказателяПоФорме = СокрЛП(МакетСоставаПоказателей.Область(Ном, 2).Текст);
			// признак многострочности определяется по третьей колонке макета
			ПризнМногострочность = СокрЛП(МакетСоставаПоказателей.Область(Ном, 3).Текст);
			// по четвертой колонке определяется тип данных реквизита
			ТипДанныхРеквизита   = СокрЛП(МакетСоставаПоказателей.Область(Ном, 4).Текст);
			// по колонке 5 определяется исходное количество строк многострочного раздела
			стрКолСтрокИсходный  = СокрЛП(МакетСоставаПоказателей.Область(Ном, 5).Текст);
			// вариант заполнения ячейки определяется по колонке 6 макета
			стрВариантЗаполнения = РегламентированнаяОтчетность.ПолучитьВариантЗаполненияПоказателяРегОтчета(МакетСоставаПоказателей, Ном);
			
			ДопОписание			 = СокрЛП(МакетСоставаПоказателей.Область(Ном, 7).Текст);
			// признак обязательности заполнения показателя
			Обязательность  	 = СокрЛП(МакетСоставаПоказателей.Область(Ном, 8).Текст);
			
			// Код показателя по структуре определяется по колонке 9 макета
			КодПоказателяПоСтруктуре = СокрЛП(МакетСоставаПоказателей.Область(Ном, 9).Текст);

			чКолСтрокИсходный    = ? (ПустаяСтрока(стрКолСтрокИсходный),  0, Число(стрКолСтрокИсходный));
			чВариантЗаполнения   = ? (ПустаяСтрока(стрВариантЗаполнения), 0, Число(стрВариантЗаполнения));

			// формируем таблицу значений, содержащей состав показателей отчета
			НоваяСтрока = мТаблицаСоставПоказателей.Добавить();
			НоваяСтрока.ИмяПоляТаблДокумента	= ИмяСтраницы;
			НоваяСтрока.КодПоказателяПоСоставу	= КодПоказателя;
			НоваяСтрока.КодПоказателяПоФорме	= КодПоказателяПоФорме;
			НоваяСтрока.ПризнМногострочности	= ПризнМногострочность;
			НоваяСтрока.ТипДанныхПоказателя		= ТипДанныхРеквизита;
			НоваяСтрока.ДопОписание				= ДопОписание;
			НоваяСтрока.Обязательность			= Обязательность;
			НоваяСтрока.КодПоказателяПоСтруктуре = КодПоказателяПоСтруктуре;

			Если чВариантЗаполнения <> 0 Тогда
				// Если в колонке 6 задан варианет заполнения показателя, 
				// то его значение может определяться по данным ИБ.
				// Дополняем таблицу значений вариантов заполнения
				НоваяСтрока = ТаблицаВариантыЗаполнения.Добавить();
				НоваяСтрока.КодПоказателя      = ? (Не ПустаяСтрока(КодПоказателяПоФорме), КодПоказателяПоФорме, КодПоказателя);
				НоваяСтрока.ВариантЗаполнения  = чВариантЗаполнения;
				НоваяСтрока.КодПоказателяПоСтруктуре  = КодПоказателяПоСтруктуре;
			КонецЕсли;

		КонецЦикла;

		Если ТаблицаВариантыЗаполнения.Количество() > 0 Тогда
			// имеются автоматически заполняемые ячейки

			// Для обеспечения возможности назначения разных вариантов заполнения одного и того же показателя
			// на разных страницах, создаем массив, хранящий информацию о вариантах заполнения ячеек по каждой
			// странице многостраничного раздела.
			МассивВариантовЗаполненияСтраниц = Новый Массив;
			МассивВариантовЗаполненияСтраниц.Добавить(ТаблицаВариантыЗаполнения);

			// Создаем структуру, содержащей варианты заполнения показателей отчета.
			//
			// В качестве ключа используется имя страницы основной панели формы, содержащей
			//   табличный документ с автоматически заполняемыми показателями.
			// В качестве значения принимаем Массив из таблицы значений - вариантов заполнения
			//   показателей по каждой странице многостраничного раздела.
			//
			мСтруктураВариантыЗаполнения.Вставить(ИмяСтраницы, МассивВариантовЗаполненияСтраниц);
		Конецесли;

	КонецЦикла;

	ФормироватьСтруктуруСтраницОтчета();

КонецПроцедуры // ЗаполнитьНачальныеНастройки()

Функция ПакетнаяПечать(ПечДок) Экспорт
	
КонецФункции

Функция ПроверитьПоказатели() Экспорт
	
	СформироватьСтруктуруПоказателей();
	Параметры = СформироватьСтруктуруПараметров();
	
	Если НЕ ОтчетЗаполненКорректно(Параметры) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ДеревоВыгрузки = ИзвлечьСтруктуруXML();
	ЗаполнитьДанными(ДеревоВыгрузки, Параметры);
	
	Если НЕ ПроверитьДанные(ДеревоВыгрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура Выгрузить() Экспорт
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Сообщить("Организация не выбрана! Выгрузка невозможна.",СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	Если Модифицированность и Вопрос("Перед выгрузкой отчета требуется сохранить введенную информацию." + Символы.ПС + "Продолжить?",РежимДиалогаВопрос.ДаНет)<>КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность И НЕ Сохранить() Тогда
		Возврат;
	КонецЕсли;
	
	НовыйДок = Документы.ВыгрузкаРегламентированныхОтчетов.СоздатьДокумент();
	НовыйДок.УстановитьВремя(РежимАвтоВремя.ТекущееИлиПоследним);
	СпДокОсн = Новый СписокЗначений;
	СпДокОсн.Добавить(мСохраненныйДок);
	
	НовыйДок.ПолучитьФорму(, ЭтаФорма).СформироватьИЗаписать(СпДокОсн)
	
КонецПроцедуры

Функция ОтчетЗаполненКорректно(Параметры)
	
	СтатусПроверки = Истина;
	
	ПроверитьЗаполнениеТитульный(Параметры, СтатусПроверки);
	
	ПроверитьЗаполнениеЛицензии(Параметры, СтатусПроверки);
	
	ПроверитьЗаполнениеДекларация(Параметры, СтатусПроверки);
	
	Возврат СтатусПроверки;
	
КонецФункции

Процедура ПроверитьЗаполнениеТитульный(Параметры, СтатусПроверки)
	
	Если ПустаяСтрока(Параметры.ИННЮЛ) Тогда
		ТекстОписанияОшибки = "Не задан ИНН организации1!";
		СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Титульный", "ИНН1_1");
	ИначеЕсли СтрДлина(СокрЛП(Параметры.ИННЮЛ))<> 10 Тогда
		ТекстОписанияОшибки = "Неверно задан ИНН организации1!";
		СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Титульный", "ИНН1_1");
	КонецЕсли;
	
	Если ПустаяСтрока(Параметры.КППЮЛ) Тогда
		ТекстОписанияОшибки = "Не задан КПП организации1!";
		СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Титульный", "КПП1_1");
	ИначеЕсли СтрДлина(СокрЛП(Параметры.КППЮЛ))<> 9 Тогда
		ТекстОписанияОшибки = "Неверно задан КПП организации1!";
		СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Титульный", "КПП1_1");
	КонецЕсли;
	
	Если ПустаяСтрока(Параметры.НаимЮЛ) Тогда
		ТекстОписанияОшибки = "Не указано полное наименование организации!";
		СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Титульный", "ОргНазв");
	КонецЕсли;
	
	Если ПустаяСтрока(Параметры.КодРегион) Тогда
		ТекстОписанияОшибки = "В адресе организации не указан код региона!";
		СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Титульный", "КодРегиона1");
	ИначеЕсли СтрДлина(СокрЛП(Параметры.КодРегион))<> 2 Тогда
		ТекстОписанияОшибки = "В адресе организации неверно указан код региона!";
		СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Титульный", "КодРегиона1");
	КонецЕсли;
	
	УказаныФИО = НЕ ПустаяСтрока(Параметры.РуководительФамилия) И НЕ ПустаяСтрока(Параметры.РуководительИмя);
	Если НЕ УказаныФИО Тогда
		ТекстОписанияОшибки = "Неверно указаны фамилия, имя и отчество руководителя!";
		СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Титульный", "ОргДиректор");
	КонецЕсли;
	
	УказаныФИО = НЕ ПустаяСтрока(Параметры.ГлавбухФамилия) И НЕ ПустаяСтрока(Параметры.ГлавбухИмя);
	Если НЕ УказаныФИО Тогда
		ТекстОписанияОшибки = "Неверно указаны фамилия, имя и отчество главного бухгалтера!";
		СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Титульный", "ОргДиректор");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеЛицензии(Параметры, СтатусПроверки)
	
	ДанныеРаздела = мДанныеОтчета.Лицензии;
	
	ЕстьЗаполненныйЛистРаздела = Ложь;
	
	Для Каждого ЛистРаздела Из ДанныеРаздела Цикл
		ДанныеЛиста = ЛистРаздела.Данные;
		ДополнительныеДанныеРаздела = ЛистРаздела.ДополнительныеДанные;
		
		Если НЕ УказанаЛицензия(ДанныеЛиста) Тогда
			Продолжить;
		КонецЕсли;
		ЕстьЗаполненныйЛистРаздела = Истина;
		
		НомерЛиста = ДанныеРаздела.Индекс(ЛистРаздела) + 1;
		НомерЛистаСтроковый = Формат(НомерЛиста, "ЧГ=0");
		
		Если ЛицензируемаяДеятельность() Или мДатаКонцаПериодаОтчета < '20131101' Тогда
			ТекстОписанияОшибки = "Не указана серия лицензии на листе " + НомерЛистаСтроковый + " раздела лицензий!";
			Если НЕ ЗначениеЗаполнено(ДанныеЛиста.П000000000101) Тогда
				СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Лицензии", "П000000000101", НомерЛиста);
			КонецЕсли;
			
			ТекстОписанияОшибки = "Не указан номер лицензии на листе " + НомерЛистаСтроковый + " раздела лицензий!";
			Если НЕ ЗначениеЗаполнено(ДанныеЛиста.П000000000102) Тогда
				СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Лицензии", "П000000000102", НомерЛиста);
			КонецЕсли;
			
			ТекстОписанияОшибки = "Не указана дата начала действия лицензии на листе " + НомерЛистаСтроковый + " раздела лицензий!";
			Если НЕ ЗначениеЗаполнено(ДанныеЛиста.П000000000103) Тогда
				СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Лицензии", "П000000000103", НомерЛиста);
			КонецЕсли;
			
			ТекстОписанияОшибки = "Не указана дата окончания действия лицензии на листе " + НомерЛистаСтроковый + " раздела лицензий!";
			Если НЕ ЗначениеЗаполнено(ДанныеЛиста.П000000000104) Тогда
				СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Лицензии", "П000000000104", НомерЛиста);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДанныеЛиста.П000000000103) И ЗначениеЗаполнено(ДанныеЛиста.П000000000104) Тогда
				ТекстОписанияОшибки = "На листе " + НомерЛистаСтроковый + " раздела лицензий указана дата окончания действия лицензии предшествующая дате начала действия лицензии!";
				Если ДанныеЛиста.П000000000103 > ДанныеЛиста.П000000000104 Тогда
					СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Лицензии", "П000000000104", НомерЛиста);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
			
		ВидДеятельности = ДанныеЛиста.П000000000105;
		КодВидаДеятельности = ДополнительныеДанныеПоАдресу(ДополнительныеДанныеРаздела, "П000000000105");
		Если ЗначениеЗаполнено(ВидДеятельности) И НЕ ЗначениеЗаполнено(КодВидаДеятельности) Тогда
			ТекстОписанияОшибки = "Нарушена внутренняя структура данных вида деятельности по лицензии на листе " + НомерЛистаСтроковый + " раздела лицензий. Требуется повторный выбор значения из списка!";
			СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Лицензии", "П000000000105", НомерЛиста);
		ИначеЕсли НЕ ЗначениеЗаполнено(ВидДеятельности) Тогда
			ТекстОписанияОшибки = "Не указан вид деятельности по лицензии на листе " + НомерЛистаСтроковый + " раздела лицензий!";
			СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Лицензии", "П000000000105", НомерЛиста);
		КонецЕсли;
	КонецЦикла;
	
	ТекстОписанияОшибки = "Не введено ни одной лицензии!";
	Если мДатаКонцаПериодаОтчета  >= '20131101' И НЕ ЕстьЗаполненныйЛистРаздела Тогда
		СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Лицензии", "П000000000101", 1);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеДекларация(Параметры, СтатусПроверки)
	
	ДанныеРаздела = мДанныеОтчета.Декларация;
	
	ЕстьЗаполненныйЛистРаздела = Ложь;
	
	Для Каждого ЛистРаздела Из ДанныеРаздела Цикл
		НомерЛиста = ДанныеРаздела.Индекс(ЛистРаздела) + 1;
		НомерЛистаСтроковый = Формат(НомерЛиста, "ЧГ=0");
		
		ДанныеЛиста = ЛистРаздела.Данные;
		
		Если НЕ ЗаполненЛистДекларация(ДанныеЛиста) Тогда
			Продолжить;
		КонецЕсли;
		ЕстьЗаполненныйЛистРаздела = Истина;
		
		Если ПустаяСтрока(ДанныеЛиста.ОбособленноеПодразделение) Тогда
			ТекстОписанияОшибки = "На листе " + НомерЛистаСтроковый + " декларации не указаны реквизиты обособленного подразделения!";
			СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", "ОбособленноеПодразделение", НомерЛиста);
		Иначе
			СведенияОбОП = Неопределено;
			Если ТипЗнч(ЛистРаздела.ДополнительныеДанные) = Тип("Структура") И ЛистРаздела.ДополнительныеДанные.Свойство("СведенияОбОП", СведенияОбОП) Тогда
				ИНН = СокрЛП(СведенияОбОП.ИНН);
				Если ПустаяСтрока(ИНН) Тогда
					ТекстОписанияОшибки = "На листе " + НомерЛистаСтроковый + " декларации не указан ИНН обособленного подразделения!";
					СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", "ОбособленноеПодразделение", НомерЛиста);
				ИначеЕсли СтрДлина(ИНН) <> 10 Тогда
					ТекстОписанияОшибки = "На листе " + НомерЛистаСтроковый + " декларации неверно указан ИНН обособленного подразделения!";
					СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", "ОбособленноеПодразделение", НомерЛиста);
				КонецЕсли;
				
				КПП = СокрЛП(СведенияОбОП.КПП);
				Если ПустаяСтрока(КПП) Тогда
					ТекстОписанияОшибки = "На листе " + НомерЛистаСтроковый + " декларации не указан КПП обособленного подразделения!";
					СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", "ОбособленноеПодразделение", НомерЛиста);
				ИначеЕсли СтрДлина(КПП) <> 9 Тогда
					ТекстОписанияОшибки = "На листе " + НомерЛистаСтроковый + " декларации неверно указан КПП обособленного подразделения!";
					СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", "ОбособленноеПодразделение", НомерЛиста);
				КонецЕсли;
				
				ТекстОписанияОшибки = "На листе " + НомерЛистаСтроковый + " декларации не указано наименование обособленного подразделения!";
				Если ПустаяСтрока(СведенияОбОП.Наименование) Тогда
					СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", "ОбособленноеПодразделение", НомерЛиста);
				КонецЕсли;
				
				Если ПустаяСтрока(СведенияОбОП.ПредставлениеАдреса) Тогда
					ТекстОписанияОшибки = "На листе " + НомерЛистаСтроковый + " декларации не указан адрес обособленного подразделения!";
					СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", "ОбособленноеПодразделение", НомерЛиста);
				ИначеЕсли ПустаяСтрока(СведенияОбОП.КодРегиона) Тогда
					ТекстОписанияОшибки = "На листе " + НомерЛистаСтроковый + " декларации в адресе обособленного подразделения не указан регион!";
					СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", "ОбособленноеПодразделение", НомерЛиста);
				КонецЕсли;
			Иначе
				ТекстОписанияОшибки = "На листе " + НомерЛистаСтроковый + " декларации нарушена внутренняя структура реквизитов обособленного подразделения, требуется повторный ввод данных!";
				СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", "ОбособленноеПодразделение", НомерЛиста);
			КонецЕсли;
		КонецЕсли;
		
		ДанныеДопСтрок = Неопределено;
		ДополнительныеДанныеЛиста = ЛистРаздела.ДополнительныеДанные;
		
		ЛистРаздела.ДанныеДопСтрок.Свойство(мИдГруппы1, ДанныеДопСтрок);
		ПроверитьЗаполнениеТаблицыДекларации(СтатусПроверки, ДанныеДопСтрок, ДополнительныеДанныеЛиста, НомерЛиста);
		
		ЛистРаздела.ДанныеДопСтрок.Свойство(мИдГруппы2, ДанныеДопСтрок);
		ПроверитьЗаполнениеТаблицыДекларации(СтатусПроверки, ДанныеДопСтрок, ДополнительныеДанныеЛиста, НомерЛиста);
		
	КонецЦикла;
	
	ТекстОписанияОшибки = "Не заполнен ни один лист раздела Декларация!";
	Если НЕ ЕстьЗаполненныйЛистРаздела Тогда
		СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", "ОбособленноеПодразделение", 1);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеТаблицыДекларации(СтатусПроверки, ДанныеДопСтрок, ДополнительныеДанные, НомерЛиста)
	
	НомерЛистаСтроковый = Формат(НомерЛиста, "ЧГ=0");
	
	ПрефиксИмени = Лев(ДанныеДопСтрок.Колонки[0].Имя, 11);
	
	Если ПрефиксИмени = "П0000100001" Тогда
		ИмяТаблицы = """по организации""";
	Иначе
		ИмяТаблицы = """по обособленному подразделению""";
	КонецЕсли;
	
	Для Каждого СтрокаДекларации Из ДанныеДопСтрок Цикл
		
		НомерСтроки = ДанныеДопСтрок.Индекс(СтрокаДекларации) + 1;
		НомерСтрокиСтроковый = ФОрмат(НомерСтроки, "ЧГ=0");
		
		СтрокаЗаполнена = ЗначениеЗаполнено(СтрокаДекларации[ПрефиксИмени + "01"])
		              ИЛИ ЗначениеЗаполнено(СтрокаДекларации[ПрефиксИмени + "02"])
		              ИЛИ ЗначениеЗаполнено(СтрокаДекларации[ПрефиксИмени + "03"])
		              ИЛИ ЗначениеЗаполнено(СтрокаДекларации[ПрефиксИмени + "04"])
		              ИЛИ ЗначениеЗаполнено(СтрокаДекларации[ПрефиксИмени + "05"])
		              ИЛИ ЗначениеЗаполнено(СтрокаДекларации[ПрефиксИмени + "06"])
		              ИЛИ ЗначениеЗаполнено(СтрокаДекларации[ПрефиксИмени + "07"])
		              ИЛИ ЗначениеЗаполнено(СтрокаДекларации[ПрефиксИмени + "81"])
		              ИЛИ ЗначениеЗаполнено(СтрокаДекларации[ПрефиксИмени + "82"])
		              ИЛИ ЗначениеЗаполнено(СтрокаДекларации[ПрефиксИмени + "09"])
		              ИЛИ ЗначениеЗаполнено(СтрокаДекларации[ПрефиксИмени + "10"])
		              ИЛИ ЗначениеЗаполнено(СтрокаДекларации[ПрефиксИмени + "11"])
		              ИЛИ ЗначениеЗаполнено(СтрокаДекларации[ПрефиксИмени + "12"])
		              ИЛИ ЗначениеЗаполнено(СтрокаДекларации[ПрефиксИмени + "13"])
		              ИЛИ ЗначениеЗаполнено(СтрокаДекларации[ПрефиксИмени + "14"])
		              ИЛИ ЗначениеЗаполнено(СтрокаДекларации[ПрефиксИмени + "15"])
		              ИЛИ ЗначениеЗаполнено(СтрокаДекларации[ПрефиксИмени + "16"])
		              ИЛИ ЗначениеЗаполнено(СтрокаДекларации[ПрефиксИмени + "17"])
		              ИЛИ ЗначениеЗаполнено(СтрокаДекларации[ПрефиксИмени + "18"])
		              ИЛИ ЗначениеЗаполнено(СтрокаДекларации[ПрефиксИмени + "19"])
		              ИЛИ ЗначениеЗаполнено(СтрокаДекларации[ПрефиксИмени + "20"])
		              ИЛИ ЗначениеЗаполнено(СтрокаДекларации[ПрефиксИмени + "21"])
		              ИЛИ ЗначениеЗаполнено(СтрокаДекларации[ПрефиксИмени + "22"])
		              ИЛИ ЗначениеЗаполнено(СтрокаДекларации[ПрефиксИмени + "23"])
		              ИЛИ ЗначениеЗаполнено(СтрокаДекларации[ПрефиксИмени + "24"]);
		Если НЕ СтрокаЗаполнена Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяГрафы = ПрефиксИмени + "01";
		ИмяПоказателя = ПрефиксИмени + "01_" + НомерСтрокиСтроковый;
		ЗначениеГрафы = СокрЛП(СтрокаДекларации[ИмяГрафы]);
		Если ПустаяСтрока(ЗначениеГрафы) Тогда
			ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации не задан вид перевозки!";
			СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателя, НомерЛиста);
		ИначеЕсли НЕ ЗначениеСодержитсяВСписке(ЗначениеГрафы, "АТ, ЖД, МП, РП, ВП") Тогда
			ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации вид перевозки должен быть в списке АТ, ЖД, МП, РП, ВП!";
			СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателя, НомерЛиста);
		КонецЕсли;
		
		ИмяГрафы = ПрефиксИмени + "02";
		ИмяПоказателя = ПрефиксИмени + "02_" + НомерСтрокиСтроковый;
		ЗначениеГрафы = СокрЛП(СтрокаДекларации[ИмяГрафы]);
		Если ПустаяСтрока(ЗначениеГрафы) Тогда
			ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации не задан вид продукции!";
			СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателя, НомерЛиста);
		ИначеЕсли НЕ ЗначениеСодержитсяВСписке(ЗначениеГрафы, "ЭС, АП, ССПП, ССНПП") Тогда
			ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации вид подукции должен быть в списке ЭС, АП, ССПП, ССНПП!";
			СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателя, НомерЛиста);
		КонецЕсли;
		
		ИмяГрафыНаименование = ПрефиксИмени + "03";
		ИмяПоказателяНаименование = ПрефиксИмени + "03_" + НомерСтрокиСтроковый;
		НаименованиеОрганизации = СокрЛП(СтрокаДекларации[ИмяГрафыНаименование]);
		
		ИмяГрафыИНН = ПрефиксИмени + "04";
		ИмяПоказателяИНН = ПрефиксИмени + "04_" + НомерСтрокиСтроковый;
		ИНН = СокрЛП(СтрокаДекларации[ИмяГрафыИНН]);
		
		ИмяГрафыКПП = ПрефиксИмени + "05";
		ИмяПоказателяКПП = ПрефиксИмени + "05_" + НомерСтрокиСтроковый;
		КПП = СокрЛП(СтрокаДекларации[ИмяГрафыКПП]);
		
		ИмяГрафыФИО = ПрефиксИмени + "06";
		ИмяПоказателяФИО = ПрефиксИмени + "06_" + НомерСтрокиСтроковый;
		ФИО = СокрЛП(СтрокаДекларации[ИмяГрафыФИО]);
		
		ИмяГрафыАдрес = ПрефиксИмени + "07";
		ИмяПоказателяАдрес = ПрефиксИмени + "07_" + НомерСтрокиСтроковый;
		Адрес = СокрЛП(СтрокаДекларации[ИмяГрафыАдрес]);
		
		ЗаполненыСведенияОбОрганизации = НЕ ПустаяСтрока(ИНН) ИЛИ НЕ ПустаяСтрока(КПП) ИЛИ НЕ ПустаяСтрока(НаименованиеОрганизации);
		ЗаполненыСведенияОбИП = НЕ ПустаяСтрока(ФИО) ИЛИ НЕ ПустаяСтрока(Адрес);
		
		Если ЗаполненыСведенияОбОрганизации И ЗаполненыСведенияОбИП Тогда
			ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации одновременно заполнены сведения о перевозчике-организации и перевозчике-индивидуальном предпринимателе!";
			СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателяНаименование, НомерЛиста);
		Иначе
			Если ЗаполненыСведенияОбОрганизации Тогда
				Если ПустаяСтрока(НаименованиеОрганизации) Тогда
					ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации не указано наименование организации-перевозчика!";
					СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателяНаименование, НомерЛиста);
				КонецЕсли;
				
				Если ПустаяСтрока(ИНН) Тогда
					ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации не указан ИНН организации-перевозчика!";
					СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателяИНН, НомерЛиста);
				ИначеЕсли СтрДлина(ИНН) <> 10 Тогда
					ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации неверно указан ИНН организации-перевозчика!";
					СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателяИНН, НомерЛиста);
				КонецЕсли;
				
				Если ПустаяСтрока(КПП) Тогда
					ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации не указан КПП организации-перевозчика!";
					СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателяКПП, НомерЛиста);
				ИначеЕсли СтрДлина(КПП) <> 9 Тогда
					ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации неверно указан КПП организации-перевозчика!";
					СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателяКПП, НомерЛиста);
				КонецЕсли;
				
			ИначеЕсли ЗаполненыСведенияОбИП Тогда
				Если ПустаяСтрока(ФИО) Тогда
					ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации не указаны Ф.И.О. индивидуального предпринимателя-перевозчика!";
					СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателяФИО, НомерЛиста);
				Иначе
					ФИО = РазложитьФИО(ФИО);
					Если ПустаяСтрока(ФИО.Имя) Тогда
						ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации у индивидуального предпринимателя-перевозчика не указано имя!";
						СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателяФИО, НомерЛиста);
					КонецЕсли;
				КонецЕсли;
				
				Если ПустаяСтрока(Адрес) Тогда
					ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации не указан адрес индивидуального предпринимателя-перевозчика!";
					СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателяАдрес, НомерЛиста);
				Иначе
					Адрес = Неопределено;
					Если ТипЗнч(ДополнительныеДанные) = Тип("Структура") И ДополнительныеДанные.Свойство(ИмяПоказателяАдрес, Адрес) Тогда
						ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации в адресе индивидуального предпринимателя-перевозчика не задан регион!";
						Если ПустаяСтрока(Адрес.Регион) Тогда
							СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателяАдрес, НомерЛиста);
						КонецЕсли;
					Иначе
						ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации нарушена внутренняя структура адреса индивидуального предпринимателя-перевозчика, требуется повторный ввод адреса!";
						СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателяАдрес, НомерЛиста);
					КонецЕсли;
				КонецЕсли;
			Иначе
				ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации не указаны сведения о перевозчике!";
				СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателяНаименование, НомерЛиста);
			КонецЕсли;
		КонецЕсли;
		
		ИмяГрафыСерии = ПрефиксИмени + "81";
		ИмяПоказателяСерии = ПрефиксИмени + "81_" + НомерСтрокиСтроковый;
		ЗначениеГрафыСерии = СокрЛП(СтрокаДекларации[ИмяГрафыСерии]);
		
		ИмяГрафыНомера = ПрефиксИмени + "82";
		ИмяПоказателяНомера = ПрефиксИмени + "82_" + НомерСтрокиСтроковый;
		ЗначениеГрафыНомера = СокрЛП(СтрокаДекларации[ИмяГрафыНомера]);
		
		Если ЗначениеЗаполнено(ЗначениеГрафыСерии) ИЛИ ЗначениеЗаполнено(ЗначениеГрафыНомера) Тогда
			Если ПустаяСтрока(ЗначениеГрафы) Тогда
				ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации не указана серия лицензии!";
				СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателяСерии, НомерЛиста);
			КонецЕсли;
			
			Если ПустаяСтрока(ЗначениеГрафы) Тогда
				ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации не указан номер лицензии!";
				СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателяНомера, НомерЛиста);
			КонецЕсли;
			
			ИмяГрафы = ПрефиксИмени + "09";
			ИмяПоказателя = ПрефиксИмени + "09_" + НомерСтрокиСтроковый;
			ЗначениеГрафы = СтрокаДекларации[ИмяГрафы];
			Если НЕ ЗначениеЗаполнено(ЗначениеГрафы) Тогда
				ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации не указана дата выдачи лицензии!";
				СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателя, НомерЛиста);
			КонецЕсли;
			
			ИмяГрафы = ПрефиксИмени + "10";
			ИмяПоказателя = ПрефиксИмени + "10_" + НомерСтрокиСтроковый;
			ЗначениеГрафы = СтрокаДекларации[ИмяГрафы];
			Если НЕ ЗначениеЗаполнено(ЗначениеГрафы) Тогда
				ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации не указана дата окончания лицензии!";
				СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателя, НомерЛиста);
			КонецЕсли;
			
			ИмяГрафы = ПрефиксИмени + "11";
			ИмяПоказателя = ПрефиксИмени + "11_" + НомерСтрокиСтроковый;
			ЗначениеГрафы = СокрЛП(СтрокаДекларации[ИмяГрафы]);
			Если ПустаяСтрока(ЗначениеГрафы) Тогда
				ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации не указано кем выдана лицензия!";
				СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателя, НомерЛиста);
			КонецЕсли;
		КонецЕсли;
		
		ИмяГрафы = ПрефиксИмени + "12";
		ИмяПоказателя = ПрефиксИмени + "12_" + НомерСтрокиСтроковый;
		ЗначениеГрафы = СокрЛП(СтрокаДекларации[ИмяГрафы]);
		Если ПустаяСтрока(ЗначениеГрафы) Тогда
			ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации не указан тип транспортного средства!";
			СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателя, НомерЛиста);
		КонецЕсли;
		
		ИмяГрафы = ПрефиксИмени + "13";
		ИмяПоказателя = ПрефиксИмени + "13_" + НомерСтрокиСтроковый;
		ЗначениеГрафы = СтрокаДекларации[ИмяГрафы];
		Если НЕ ЗначениеЗаполнено(ЗначениеГрафы) Тогда
			ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации не указана грузоподъемность транспортного средства!";
			СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателя, НомерЛиста);
		КонецЕсли;
		
		ИмяГрафы = ПрефиксИмени + "14";
		ИмяПоказателя = ПрефиксИмени + "14_" + НомерСтрокиСтроковый;
		ЗначениеГрафы = СокрЛП(СтрокаДекларации[ИмяГрафы]);
		Если ПустаяСтрока(ЗначениеГрафы) Тогда
			ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации не указан регистрационный номер транспортного средства!";
			СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателя, НомерЛиста);
		КонецЕсли;
		
		ИмяГрафы = ПрефиксИмени + "15";
		ИмяПоказателя = ПрефиксИмени + "15_" + НомерСтрокиСтроковый;
		ЗначениеГрафы = СтрокаДекларации[ИмяГрафы];
		Если НЕ ЗначениеЗаполнено(ЗначениеГрафы) Тогда
			ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации не указана дата транспортной накладной перевозчика!";
			СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателя, НомерЛиста);
		КонецЕсли;
		
		ИмяГрафы = ПрефиксИмени + "16";
		ИмяПоказателя = ПрефиксИмени + "16_" + НомерСтрокиСтроковый;
		ЗначениеГрафы = СокрЛП(СтрокаДекларации[ИмяГрафы]);
		Если ПустаяСтрока(ЗначениеГрафы) Тогда
			ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации не указан номер транспортной накладной перевозчика!";
			СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателя, НомерЛиста);
		КонецЕсли;
		
		ИмяГрафы = ПрефиксИмени + "17";
		ИмяПоказателя = ПрефиксИмени + "17_" + НомерСтрокиСтроковый;
		ЗначениеГрафы = СтрокаДекларации[ИмяГрафы];
		Если НЕ ЗначениеЗаполнено(ЗначениеГрафы) Тогда
			ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации не указана масса груза по транспортной накладной перевозчика!";
			СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателя, НомерЛиста);
		КонецЕсли;
		
		ИмяГрафы = ПрефиксИмени + "18";
		ИмяПоказателя = ПрефиксИмени + "18_" + НомерСтрокиСтроковый;
		ЗначениеГрафы = СтрокаДекларации[ИмяГрафы];
		Если НЕ ЗначениеЗаполнено(ЗначениеГрафы) Тогда
			ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации не указана дата ТТН грузоотправителя!";
			СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателя, НомерЛиста);
		КонецЕсли;
		
		ИмяГрафы = ПрефиксИмени + "19";
		ИмяПоказателя = ПрефиксИмени + "19_" + НомерСтрокиСтроковый;
		ЗначениеГрафы = СокрЛП(СтрокаДекларации[ИмяГрафы]);
		Если ПустаяСтрока(ЗначениеГрафы) Тогда
			ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации не указан номер ТТН грузоотправителя!";
			СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателя, НомерЛиста);
		КонецЕсли;
		
		ИмяГрафы = ПрефиксИмени + "20";
		ИмяПоказателя = ПрефиксИмени + "20_" + НомерСтрокиСтроковый;
		ЗначениеГрафы = СокрЛП(СтрокаДекларации[ИмяГрафы]);
		Если ПустаяСтрока(ЗначениеГрафы) Тогда
			ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации не указано наименование организации-грузополучателя!";
			СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателя, НомерЛиста);
		КонецЕсли;
		
		ИмяГрафыИНН = ПрефиксИмени + "21";
		ИмяПоказателяИНН = ПрефиксИмени + "21_" + НомерСтрокиСтроковый;
		ИНН = СокрЛП(СтрокаДекларации[ИмяГрафыИНН]);
		
		ИмяГрафыКПП = ПрефиксИмени + "22";
		ИмяПоказателяКПП = ПрефиксИмени + "22_" + НомерСтрокиСтроковый;
		КПП = СокрЛП(СтрокаДекларации[ИмяГрафыКПП]);
		
		ЭтоРоссийскаяОрганизация = НЕ ПустаяСтрока(ИНН) ИЛИ НЕ ПустаяСтрока(КПП);
		Если ЭтоРоссийскаяОрганизация Тогда
			Если ПустаяСтрока(ИНН) Тогда
				ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации не указан ИНН организации-грузополучателя!";
				СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателяИНН, НомерЛиста);
			ИначеЕсли (НЕ ПустаяСтрока(КПП) И СтрДлина(ИНН) <> 10) ИЛИ (ПустаяСтрока(КПП) И СтрДлина(ИНН) <> 12) Тогда
				ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации неверно указан ИНН организации-грузополучателя!";
				СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателяИНН, НомерЛиста);
			КонецЕсли;
		
			Если СтрДлина(ИНН) <> 12 Тогда
				Если ПустаяСтрока(КПП) Тогда
					ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации не указан КПП организации-грузополучателя!";
					СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателяКПП, НомерЛиста);
				ИначеЕсли СтрДлина(КПП) <> 9 Тогда
					ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации неверно указан КПП организации-грузополучателя!";
					СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателяКПП, НомерЛиста);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		ИмяГрафы = ПрефиксИмени + "23";
		ИмяПоказателя = ПрефиксИмени + "23_" + НомерСтрокиСтроковый;
		ЗначениеГрафы = СтрокаДекларации[ИмяГрафы];
		Если НЕ ЗначениеЗаполнено(ЗначениеГрафы) Тогда
			ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации не указана масса груза по ТТН грузоотправителя!";
			СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателя, НомерЛиста);
		КонецЕсли;
		
		ИмяГрафы = ПрефиксИмени + "24";
		ИмяПоказателя = ПрефиксИмени + "24_" + НомерСтрокиСтроковый;
		ЗначениеГрафы = СтрокаДекларации[ИмяГрафы];
		Если НЕ ЗначениеЗаполнено(ЗначениеГрафы) Тогда
			ТекстОписанияОшибки = "В строке " + НомерСтрокиСтроковый + " таблицы "+ ИмяТаблицы + " на листе " + НомерЛистаСтроковый + " декларации не указан объем перевозимой продукции!";
			СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, "Декларация", ИмяПоказателя, НомерЛиста);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СообщитьОбОшибкеЗаполнения(СтатусПроверки, ТекстОписанияОшибки, Раздел, Показатель, ЛистРаздела = Неопределено)
	
	СтатусПроверки = Ложь;
	Если ЛистРаздела = Неопределено Тогда
		РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, ТекстОписанияОшибки, Раздел, Показатель);
	Иначе
		РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, ТекстОписанияОшибки, Раздел, Показатель, ЛистРаздела);
	КонецЕсли;
	
КонецПроцедуры

Функция УказанаЛицензия(ДанныеЛиста)
	
	ЕстьСведения = ЗначениеЗаполнено(ДанныеЛиста.П000000000101)
	           ИЛИ ЗначениеЗаполнено(ДанныеЛиста.П000000000102)
	           ИЛИ ЗначениеЗаполнено(ДанныеЛиста.П000000000103)
	           ИЛИ ЗначениеЗаполнено(ДанныеЛиста.П000000000104)
	           ИЛИ ЗначениеЗаполнено(ДанныеЛиста.П000000000105);
	
	Возврат ЕстьСведения;
	
КонецФункции

Функция ЗаполненЛистДекларация(ДанныеЛиста)
	
	Заполнен = НЕ ПустаяСтрока(ДанныеЛиста.ОбособленноеПодразделение);
	
	Для Каждого Показатель Из ДанныеЛиста Цикл
		ИмяПоказателя = Показатель.Ключ;
		Если Найти(ИмяПоказателя, "П00001") = 1 И Сред(ИмяПоказателя, 12, 2) <> "00" Тогда
			Заполнен = (Заполнен ИЛИ ЗначениеЗаполнено(Показатель.Значение));
			Если Заполнен Тогда
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Заполнен;
	
КонецФункции

Функция ЗначениеСодержитсяВСписке(ПроверяемоеЗначение, КонтрольныйСписок)
	
	Результат = Ложь;
	
	Значения = ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок(КонтрольныйСписок, ",");
	Для Инд = 0 По Значения.ВГраница() Цикл
		Значения[Инд] = СокрЛП(Значения[Инд]);
	КонецЦикла;
	
	Результат = (Значения.Найти(ПроверяемоеЗначение) <> Неопределено);
	
	Возврат Результат
	
КонецФункции

Функция ДополнительныеДанныеПоАдресу(Данные, Адрес)
	
	Перем ЗначениеДанных;
	
	Если ТипЗнч(Данные) = Тип("Структура") Тогда
		ПозицияРазделителя = Найти(Адрес, ".");
		Если ПозицияРазделителя = 0 Тогда
			АдресВерхнегоУровня = СокрЛП(Адрес);
			Данные.Свойство(АдресВерхнегоУровня, ЗначениеДанных);
		Иначе
			АдресВерхнегоУровня = СокрЛП(Лев(Адрес, ПозицияРазделителя - 1));
			ДанныеВерхнегоУровня = Неопределено;
			Если Данные.Свойство(АдресВерхнегоУровня, ДанныеВерхнегоУровня) Тогда
				АдресСледующегоУровня = СокрЛП(Сред(Адрес, ПозицияРазделителя + 1));
				ЗначениеДанных = ДополнительныеДанныеПоАдресу(ДанныеВерхнегоУровня, АдресСледующегоУровня);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЗначениеДанных;
	
КонецФункции

// Функция формирует текст выгрузки. Вызывается из формы ФормаДокумента документа ВыгрузкаРегламентированныхОтчетов
//
// Параметры:
//	Текст - ТекстовыйДокумент, в которой следует добавлять строки. В случае, если параметр не задан - создается 
//			новый объект ТекстовыйДокумент
//
// Возвращаемое значение:
//	Булево - Истина, если текст успешно сформирован; Ложь - в противном случае
//
Функция УниверсальнаяВыгрузка(Текст = Неопределено, ПроверятьБлокировки = Истина) Экспорт
	
	Отказ = Ложь;
	РегламентированнаяОтчетность.ПередВыгрузкойРегламентированногоОтчета(ЭтаФорма, Отказ, ПроверятьБлокировки);
	Если Отказ Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Модифицированность Тогда
		Если Вопрос("Перед выгрузкой отчета требуется сохранить введенную информацию." + Символы.ПС + "Продолжить?", РежимДиалогаВопрос.ДаНет) <> КодВозвратаДиалога.Да ИЛИ НЕ Сохранить() Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	СформироватьСтруктуруПоказателей();
	Параметры = СформироватьСтруктуруПараметров();
	
	Если НЕ ПроверитьПоказатели() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ДеревоВыгрузки = ИзвлечьСтруктуруXML();
	ЗаполнитьДанными(ДеревоВыгрузки, Параметры);
	Если НЕ ПроверитьДанные(ДеревоВыгрузки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Текст = ВыгрузитьДеревоВXML(ДеревоВыгрузки, Параметры);
	
	СтрокаИмениФайла = "<!-- ИдФайл=""" + Параметры.ИдФайл + """ -->";
	Текст = Текст + Символы.ПС + СтрокаИмениФайла;
	
	Если Открыта() Тогда
		СохранитьФайлВыгрузкиНаДиск(Текст, Параметры);
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Процедура активизирует область поля табличного документа формы,
// соответствующей заданному показателю. При необходимости,
// процедура устанавливает в качестве активной ту страницу раздела, на которой
// находится показатель.
//
// Параметры:
//	Ячейка - СтрокаТаблицыЗначений с "координатами" показателя
//
Процедура АктивизироватьЯчейку(Ячейка) Экспорт
	
	РегламентированнаяОтчетность.роАктивизироватьЯчейку(ЭтаФорма, Ячейка);
	УстановитьДоступностьПолейДекларация();
	
КонецПроцедуры

// ПолеТабличногоДокументаПриИзмененииСодержимогоОбласти
//
Процедура ПолеТабличногоДокументаПриИзмененииСодержимогоОбласти(Элемент, Область)
	
	Модифицированность = Истина;
	Страница = ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница.Имя;
	
	Если НЕ ((Страница = "Титульный") Или (Страница = "ФизЛица")) Тогда
		ОбластьИмя = Элемент.ТекущаяОбласть.Имя;
		Расчет("");
	КонецЕсли;
	
КонецПроцедуры // ПолеТабличногоДокументаПриИзмененииСодержимогоОбласти

// Процедура является частью механизма автосохранения 
// и назначается динамически методом ПодключитьОбработчикОжидания()
// Обработчик может подключаться и отключаться из процедуры ПриОткрытии текущей формы и
// процедуры ОсновныеДействияФормыКнопкаВыбратьНажатие формы ФормаНастройки
Процедура ОбработчикСобытияПоТаймеру()
	
	Если РегламентированнаяОтчетность.АвтосохранениеВозможно(ЭтаФорма) Тогда
		Сохранить(Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	РегламентированнаяОтчетность.ОбработкаОповещенияОбИзмененииСтатусаРегламентированногоОтчета(ЭтаФорма, ИмяСобытия, Параметр, Источник, "ФСРАР");
	Если Имясобытия = "Изменение статуса отправки регламентированного отчета" И ЗначениеЗаполнено(Параметр) И мСохраненныйДок <> Неопределено И Источник = мСохраненныйДок.Ссылка Тогда
		
		Если мСохраненныйДок <> Неопределено И РегламентированнаяОтчетность.КонтекстЭДОЛокальный() <> Неопределено Тогда
			РеквизитыОтправки = РегламентированнаяОтчетность.КонтекстЭДОЛокальный().ПолучитьРеквизитыОтправкиФСРАР(мСохраненныйДок.Ссылка);
			Если РеквизитыОтправки.СтатусОтправки = Перечисления.СтатусыОтправки.Сдан Тогда
				РегламентированнаяОтчетность.ЗаписатьСтатусОтчета(Источник, Перечисления.СтатусыРегламентированныхОтчетов.Сдан);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СохранитьФайлВыгрузкиНаДиск(ТекстXML, Параметры)
	
	ПутьДляВыгрузки = РегламентированнаяОтчетность.ПолучитьПутьВыгрузки(); // получаем каталог для сохранения
	Если ПутьДляВыгрузки <> Ложь Тогда
		ФайлДляВыгрузки = ?(Прав(ПутьДляВыгрузки, 1) = "\", ПутьДляВыгрузки, ПутьДляВыгрузки + "\") + Параметры.ИдФайл + ".xml";
		ЗаписатьТесктНаДиск(ТекстXML, ФайлДляВыгрузки); // записываем XML в файл на диск
	КонецЕсли;
	
КонецПроцедуры

Функция ИзвлечьСтруктуруXML()
	
	ДеревоСтруктуры = Новый ДеревоЗначений;
	ДеревоСтруктуры.Колонки.Добавить("Код");
	ДеревоСтруктуры.Колонки.Добавить("Значение");
	ДеревоСтруктуры.Колонки.Добавить("ЗначениеПоУмолчанию");
	ДеревоСтруктуры.Колонки.Добавить("Тип");
	ДеревоСтруктуры.Колонки.Добавить("Формат");
	ДеревоСтруктуры.Колонки.Добавить("МинРазмерность");
	ДеревоСтруктуры.Колонки.Добавить("МаксРазмерность");
	ДеревоСтруктуры.Колонки.Добавить("Обязательность");
	ДеревоСтруктуры.Колонки.Добавить("Многостраничность");
	ДеревоСтруктуры.Колонки.Добавить("Многострочность");
	ДеревоСтруктуры.Колонки.Добавить("Раздел");
	ДеревоСтруктуры.Колонки.Добавить("Ключ");
	ДеревоСтруктуры.Колонки.Добавить("Условие");
	ДеревоСтруктуры.Колонки.Добавить("Представление");
	ДеревоСтруктуры.Колонки.Добавить("Показатели");
	
	Если мДатаКонцаПериодаОтчета < '20131101' Тогда
		ИмяМакета = "СхемаВыгрузки420";
	Иначе
		ИмяМакета = "СхемаВыгрузки430";
	КонецЕсли;
	Макет = ПолучитьМакет(ИмяМакета);
	
	ВысотаТаблицы = Макет.ВысотаТаблицы;
	
	УчтенныеГруппы = Новый Соответствие;
	
	Для Уровень = 0 По Макет.КоличествоУровнейГруппировокСтрок() - 1 Цикл
		Макет.ПоказатьУровеньГруппировокСтрок(Уровень);
		Для НомерСтроки = 2 По ВысотаТаблицы Цикл
			НомСтр = ВысотаТаблицы - НомерСтроки + 2;
			Если Макет.Область(НомСтр, 0, НомСтр, 0).Видимость И УчтенныеГруппы.Получить(НомСтр) = Неопределено Тогда
				
				РодительскийУзел = ДеревоСтруктуры;
				Если Уровень <> 0 Тогда
					Для Инд = 1 По НомСтр - 2 Цикл
						Узел = УчтенныеГруппы.Получить(НомСтр - Инд);
						Если Узел <> Неопределено Тогда
							РодительскийУзел = Узел;
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				
				НовСтр = РодительскийУзел.Строки.Вставить(0);
				НовСтр.Код = СокрЛП(Макет.Область(НомСтр, 1, НомСтр, 1).Текст);
				НовСтр.Раздел = СокрЛП(Макет.Область(НомСтр, 2, НомСтр, 2).Текст);
				НовСтр.Ключ = СокрЛП(Макет.Область(НомСтр, 3, НомСтр, 3).Текст);
				НовСтр.Тип = СокрЛП(Макет.Область(НомСтр, 4, НомСтр, 4).Текст);
				НовСтр.Формат = СокрЛП(Макет.Область(НомСтр, 5, НомСтр, 5).Текст);
				МинРазмерность = СокрЛП(Макет.Область(НомСтр, 6, НомСтр, 6).Текст);
				НовСтр.МинРазмерность = ?(ПустаяСтрока(МинРазмерность), ?(НовСтр.Формат = "N", 99999, 0), Число(МинРазмерность));
				МаксРазмерность = СокрЛП(Макет.Область(НомСтр, 7, НомСтр, 7).Текст);
				НовСтр.МаксРазмерность = ?(ПустаяСтрока(МаксРазмерность), 99999, Число(МаксРазмерность));
				НовСтр.Обязательность = СокрЛП(Макет.Область(НомСтр, 8, НомСтр, 8).Текст);
				НовСтр.Многостраничность = НЕ ПустаяСтрока(Макет.Область(НомСтр, 9, НомСтр, 9).Текст);
				НовСтр.Многострочность = НЕ ПустаяСтрока(Макет.Область(НомСтр, 10, НомСтр, 10).Текст);
				НовСтр.Условие = СокрЛП(Макет.Область(НомСтр, 11, НомСтр, 11).Текст);
				НовСтр.ЗначениеПоУмолчанию = СокрЛП(Макет.Область(НомСтр, 12, НомСтр, 12).Текст);
				НовСтр.Представление = СокрЛП(Макет.Область(НомСтр, 13, НомСтр, 13).Текст);
				
				УчтенныеГруппы.Вставить(НомСтр, НовСтр);
				
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ДеревоСтруктуры;
	
КонецФункции

Функция СформироватьИдФайл(Параметры)
	
	ПрефиксИмениФайлаВыгрузки = "N1";
	
	ИдентификаторОтправителя = Параметры.ИННЮЛ;
	ПериодДекларации = "0" + Параметры.ПризПериодОтч + Прав(Параметры.ГодПериодОтч, 1);
	ДеньФормированияДекларации = Формат(РабочаяДата, "ДФ=ддММгггг");
	УникальныйИдентификатор = Врег(Строка(Новый УникальныйИдентификатор));
	
	Возврат ПрефиксИмениФайлаВыгрузки
	        + "_" + ИдентификаторОтправителя
	        + "_" + ПериодДекларации
	        + "_" + ДеньФормированияДекларации
	        + "_" + УникальныйИдентификатор;
	
КонецФункции

Функция СформироватьСтруктуруПоказателей()
	
	мДанныеОтчета = Новый Структура;
	
	СохрСтруктура = мСохраненныйДок.ДанныеОтчета.Получить();
	ПоказателиОтчета = СохрСтруктура.ПоказателиОтчета;
	Для Каждого Раздел Из ПоказателиОтчета Цикл
		мДанныеОтчета.Вставить(Раздел.Ключ, РегламентированнаяОтчетность.СвернутьДанныеТабличногоПоля(Раздел.Значение));
	КонецЦикла;
	мСтруктураМногостраничныхРазделов = СохрСтруктура.ДанныеМногостраничныхРазделов;
	Для Каждого Раздел Из мСтруктураМногостраничныхРазделов Цикл
		ТаблицаЛистовРаздела = Раздел.Значение;
		Для Каждого Лист Из ТаблицаЛистовРаздела Цикл
			Лист.Данные = РегламентированнаяОтчетность.СвернутьДанныеТабличногоПоля(Лист.Данные);
		КонецЦикла;
		мДанныеОтчета.Вставить(Раздел.Ключ, Раздел.Значение);
	КонецЦикла;
	
КонецФункции

Функция СформироватьСтруктуруПараметров()
	
	Данные = мСохраненныйДок.ДанныеОтчета.Получить();
	Орг = мСохраненныйДок.Организация;
	
	ЭтоПБОЮЛ = РегламентированнаяОтчетность.ЭтоПБОЮЛ(Орг);
	
	ПоказателиОтчета = Данные.ПоказателиОтчета;
	ПоказателиТитульный = РегламентированнаяОтчетность.СвернутьДанныеТабличногоПоля(РегламентированнаяОтчетность.СкопированнаяСтруктура(ПоказателиОтчета.ПолеТабличногоДокументаТитульный));
	
	НомерГода = Формат(мСохраненныйДок.ДатаОкончания, "ДФ=гггг");
	
	Параметры = Новый Структура;
	Параметры.Вставить("НаимПрог", РегламентированнаяОтчетность.СформироватьВерсПрог());
	Параметры.Вставить("ДатаДок",  Формат(РабочаяДата, "ДФ=дд.ММ.гггг"));
	
	КодКвартала = ПолучитьКодКвартала(ПоказателиТитульный);
	Параметры.Вставить("ПризПериодОтч", КодКвартала);
	
	Вариант = мСохраненныйДок.Вид;
	ЭтоПервичнаяОтчетность = (Вариант = 0);
	
	Параметры.Вставить("ПризФОтч", Строка(Данные.ПризнакФормыОтчетности));
	Параметры.Вставить("ГодПериодОтч", НомерГода);
	
	Параметры.Вставить("Первичная",      ЭтоПервичнаяОтчетность);
	Параметры.Вставить("Корректирующая", НЕ ЭтоПервичнаяОтчетность);
	Параметры.Вставить("НомерКорр", Вариант);
	
	НазваниеОрганизации = СокрЛП(ПоказателиТитульный.ОргНазв);
	ИНН = СокрЛП(ПоказателиТитульный.ИНН1);
	Если Лев(ИНН, 2) = "00" Тогда
		ИНН = Сред (ИНН, 3); // Удаление нулей, добавленных в соответствии с порядком заполнения.
	КонецЕсли;
	КПП = СокрЛП(ПоказателиТитульный.КПП1);
	ТелефонОрганизации  = СокрЛП(ПоказателиТитульный.ТелОрганизации);
	ЭлектроннаяПочтаОрганизации = СокрЛП(ПоказателиТитульный.ЭлектроннаяПочта);
	
	Параметры.Вставить("НаимЮЛ", НазваниеОрганизации);
	Параметры.Вставить("ЭтоЮЛ", НЕ ЭтоПБОЮЛ);
	Параметры.Вставить("ЭтоФЛ", ЭтоПБОЮЛ);
	Если ЭтоПБОЮЛ Тогда
		Параметры.Вставить("ИННФЛ",     ИНН);
	Иначе
		Параметры.Вставить("ИННЮЛ",     ИНН);
		Параметры.Вставить("КППЮЛ",     КПП);
	КонецЕсли;
	Параметры.Вставить("ТелОрг",    ТелефонОрганизации);
	Параметры.Вставить("EmailОтпр", ЭлектроннаяПочтаОрганизации);
	
	СтруктураАдреса = Данные.ДополнительныеДанные.Титульный.АдресОрганизации;
	Параметры.Вставить("КодСтраны",  СтруктураАдреса.КодСтраны);
	Параметры.Вставить("Индекс",     СтруктураАдреса.Индекс);
	Параметры.Вставить("КодРегион",  СтруктураАдреса.КодРегиона);
	Параметры.Вставить("Район",      СтруктураАдреса.Район);
	Параметры.Вставить("Город",      СтруктураАдреса.Город);
	Параметры.Вставить("НаселПункт", СтруктураАдреса.НаселенныйПункт);
	Параметры.Вставить("Улица",      СтруктураАдреса.Улица);
	Параметры.Вставить("Дом",        СтруктураАдреса.Дом);
	Параметры.Вставить("Корпус",     СтруктураАдреса.Корпус);
	Параметры.Вставить("Литера",     "");
	Параметры.Вставить("Кварт",      СтруктураАдреса.Квартира);
	
	ФИОРуководителя = РазложитьФИО(ПоказателиТитульный.ОргДиректор);
	Параметры.Вставить("РуководительФамилия",  ФИОРуководителя.Фамилия);
	Параметры.Вставить("РуководительИмя",      ФИОРуководителя.Имя);
	Параметры.Вставить("РуководительОтчество", ФИОРуководителя.Отчество);
	
	ФИОГлавбуха = РазложитьФИО(ПоказателиТитульный.ОргБухгалтер);
	Параметры.Вставить("ГлавбухФамилия",  ФИОГлавбуха.Фамилия);
	Параметры.Вставить("ГлавбухИмя",      ФИОГлавбуха.Имя);
	Параметры.Вставить("ГлавбухОтчество", ФИОГлавбуха.Отчество);
	
	Параметры.Вставить("ИмяКлючевогоУзлаСодержательнойЧасти", "Документ");
	
	Параметры.Вставить("ИдФайл", СформироватьИдФайл(Параметры));
	
	Возврат Параметры;
	
КонецФункции

Функция ПолучитьКодКвартала(ПоказателиТитульногоЛиста)
	
	Если НЕ ПустаяСтрока(ПоказателиТитульногоЛиста.ОтчетныйПериод1Кв) Тогда
		Возврат "3";
		
	ИначеЕсли НЕ ПустаяСтрока(ПоказателиТитульногоЛиста.ОтчетныйПериод2Кв) Тогда
		Возврат "6";
		
	ИначеЕсли НЕ ПустаяСтрока(ПоказателиТитульногоЛиста.ОтчетныйПериод3Кв) Тогда
		Возврат "9";
		
	ИначеЕсли НЕ ПустаяСтрока(ПоказателиТитульногоЛиста.ОтчетныйПериод4Кв) Тогда
		Возврат "0";
		
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

Функция РазложитьФИО(Знач ФИОСтр)
	
	ФИОСтр = СокрЛП(ФИОСтр);
	ФИО = Новый Структура("Фамилия, Имя, Отчество", "", "", "");
	
	ПервыйПробел = Найти(ФИОСтр, " ");
	Если ПервыйПробел = 0 Тогда
		ФИО.Фамилия = ФИОСтр;
		Возврат ФИО;
	КонецЕсли;
	ФИО.Фамилия = СокрЛП(Лев(ФИОСтр, ПервыйПробел - 1));
	ФИОСтр = СокрЛП(Сред(ФИОСтр, ПервыйПробел + 1));
	
	ВторойПробел = Найти(ФИОСтр, " ");
	Если ВторойПробел = 0 Тогда
		ФИО.Имя = ФИОСтр;
		Возврат ФИО;
	КонецЕсли;
	ФИО.Имя = СокрЛП(Лев(ФИОСтр, ВторойПробел - 1));
	
	ФИО.Отчество = СокрЛП(Сред(ФиоСтр, ВторойПробел + 1));
	
	Возврат ФИО;
	
КонецФункции

Функция НовыйУзелИзПрототипа(ПрототипУзла);
	
	РодительУзла = ПрототипУзла.Родитель;
	
	ПозицияИсходногоУзла = РодительУзла.Строки.Индекс(ПрототипУзла);
	НовыйУзел = РодительУзла.Строки.Вставить(ПозицияИсходногоУзла);
	ЗаполнитьЗначенияСвойств(НовыйУзел, ПрототипУзла, , "Родитель, Строки");
	Для Каждого Стр из ПрототипУзла.Строки Цикл
		СкопироватьУзел(НовыйУзел, Стр);
	КонецЦикла;
	
	Возврат НовыйУзел;
	
КонецФункции

Функция СкопироватьУзел(Родитель, Узел)
	
	НовыйУзел = Родитель.Строки.Добавить();
	ЗаполнитьЗначенияСвойств(НовыйУзел, Узел, , "Родитель, Строки");
	Для Каждого Стр из Узел.Строки Цикл
		СкопироватьУзел(НовыйУзел, Стр);
	КонецЦикла;
	Возврат НовыйУзел;
	
КонецФункции

Функция ПолучитьПодчиненныйЭлемент(Узел, КодЭлемента)
	
	Для Каждого Стр Из Узел.Строки Цикл
		Если Стр.Код = КодЭлемента Тогда
			Возврат Стр;
		КонецЕсли;
	КонецЦикла;
	
КонецФункции

Процедура ЗаполнитьСодержательнуюЧасть(УзелРодитель, КорневойУзел, Параметры)
	
	Узел_Справочники = ПолучитьПодчиненныйЭлемент(КорневойУзел, "Справочники");
	Узел_Документ = УзелРодитель;
	
	Узел_Организация = ПолучитьПодчиненныйЭлемент(Узел_Документ, "Организация");
	Если мДатаКонцаПериодаОтчета < '20131101' Тогда
		// Формат Выгрузки 4.20
		Узел_Лицензии = ПолучитьПодчиненныйЭлемент(Узел_Организация, "Лицензии");
		
		ПрототипУзла_Лицензия = ПолучитьПодчиненныйЭлемент(Узел_Лицензии, "Лицензия");
		Для Каждого Страница Из мДанныеОтчета.Лицензии Цикл
			ДанныеСтраницы = Страница.Данные;
			ДополнительныеДанныеСтраницы = Страница.ДополнительныеДанные;
			Узел_Лицензия = НовыйУзелИзПрототипа(ПрототипУзла_Лицензия);
			УстановитьЗначениеЭлемента(Узел_Лицензия, "ВидДеят", ДополнительныеДанныеПоАдресу(ДополнительныеДанныеСтраницы, "П000000000105"));
			УстановитьЗначениеЭлемента(Узел_Лицензия, "СерЛиц",      ДанныеСтраницы.П000000000101);
			УстановитьЗначениеЭлемента(Узел_Лицензия, "НомерЛиц",    ДанныеСтраницы.П000000000102);
			УстановитьЗначениеЭлемента(Узел_Лицензия, "ДатаНачЛиц",  ?(ЗначениеЗаполнено(ДанныеСтраницы.П000000000103), ДанныеСтраницы.П000000000103, ""));
			УстановитьЗначениеЭлемента(Узел_Лицензия, "ДатаОконЛиц", ?(ЗначениеЗаполнено(ДанныеСтраницы.П000000000104), ДанныеСтраницы.П000000000104, ""));
		КонецЦикла;
		УдалитьУзел(ПрототипУзла_Лицензия);
	Иначе
		// Формат Выгрузки 4.30
		Узел_Деятельность = ПолучитьПодчиненныйЭлемент(Узел_Организация, "Деятельность");
		Узел_Лицензируемая = ПолучитьПодчиненныйЭлемент(Узел_Деятельность, "Лицензируемая");
		Узел_Нелицензируемая = ПолучитьПодчиненныйЭлемент(Узел_Деятельность, "Нелицензируемая");
		ДопДанные = мДанныеОтчета.Лицензии[0].ДополнительныеДанные;
		Если ЛицензируемаяДеятельность() Тогда
			УдалитьУзел(Узел_Нелицензируемая);
			ПрототипУзла_Лицензия = ПолучитьПодчиненныйЭлемент(Узел_Лицензируемая, "Лицензия");
			Для Каждого Страница Из мДанныеОтчета.Лицензии Цикл
				ДанныеСтраницы = Страница.Данные;
				Если Не УказанаЛицензия(ДанныеСтраницы) Тогда
					Продолжить;
				КонецЕсли;
				ДополнительныеДанныеСтраницы = Страница.ДополнительныеДанные;
				Узел_Лицензия = НовыйУзелИзПрототипа(ПрототипУзла_Лицензия);
				УстановитьЗначениеЭлемента(Узел_Лицензия, "ВидДеят", ДополнительныеДанныеПоАдресу(ДополнительныеДанныеСтраницы, "П000000000105"));
				УстановитьЗначениеЭлемента(Узел_Лицензия, "СерНомЛиц", ДанныеСтраницы.П000000000101 + " " + ДанныеСтраницы.П000000000102);
				УстановитьЗначениеЭлемента(Узел_Лицензия, "ДатаНачЛиц",  ?(ЗначениеЗаполнено(ДанныеСтраницы.П000000000103), ДанныеСтраницы.П000000000103, ""));
				УстановитьЗначениеЭлемента(Узел_Лицензия, "ДатаОконЛиц", ?(ЗначениеЗаполнено(ДанныеСтраницы.П000000000104), ДанныеСтраницы.П000000000104, ""));
			КонецЦикла;
			УдалитьУзел(ПрототипУзла_Лицензия);
		Иначе
			УдалитьУзел(Узел_Лицензируемая);
			УстановитьЗначениеЭлемента(Узел_Нелицензируемая, "ВидДеят", ДополнительныеДанныеПоАдресу(ДопДанные, "П000000000105"));
		КонецЕсли;
	КонецЕсли;
	
	ДанныеВыгрузки = Новый Структура; // Структура для обработки данных выгрузки и хранения справочников
	
	Перевозчики = Новый ТаблицаЗначений;
	Перевозчики.Колонки.Добавить("ИдПеревозчика"); // Уникальный идентификатор
	Перевозчики.Колонки.Добавить("Фамилия");
	Перевозчики.Колонки.Добавить("Имя");
	Перевозчики.Колонки.Добавить("Отчество");
	Перевозчики.Колонки.Добавить("КодСтраны");
	Перевозчики.Колонки.Добавить("КодРегион");
	Перевозчики.Колонки.Добавить("Индекс");
	Перевозчики.Колонки.Добавить("Район");
	Перевозчики.Колонки.Добавить("Город");
	Перевозчики.Колонки.Добавить("НаселПункт");
	Перевозчики.Колонки.Добавить("Улица");
	Перевозчики.Колонки.Добавить("Дом");
	Перевозчики.Колонки.Добавить("Корпус");
	Перевозчики.Колонки.Добавить("Литера");
	Перевозчики.Колонки.Добавить("Кварт");
	Перевозчики.Колонки.Добавить("П000000000003"); // Наименование перевозчика
	Перевозчики.Колонки.Добавить("П000000000004"); // ИНН перевозчика
	Перевозчики.Колонки.Добавить("П000000000005"); // КПП перевозчика
	
	ДанныеВыгрузки.Вставить("Перевозчики", Перевозчики);
	
	Лицензии = Новый ТаблицаЗначений;
	Лицензии.Колонки.Добавить("ИдЛицензии"); // Уникальный идентификатор
	Лицензии.Колонки.Добавить("П000000000008Серия"); // Серия лицензии
	Лицензии.Колонки.Добавить("П000000000008Номер"); // Номер лицензии
	Лицензии.Колонки.Добавить("П000000000009"); // Дата выдачи
	Лицензии.Колонки.Добавить("П000000000010"); // Дата окончания
	Лицензии.Колонки.Добавить("П000000000011"); // Кем выдана
	Лицензии.Колонки.Добавить("ИдПеревозчика"); // Уникальный идентификатор перевозчика
	
	ДанныеВыгрузки.Вставить("Лицензии", Лицензии);
	
	ТранспортныеСредства = Новый ТаблицаЗначений;
	ТранспортныеСредства.Колонки.Добавить("ИдТС"); // Уникальный идентификатор
	ТранспортныеСредства.Колонки.Добавить("П000000000012"); // Тип транспортного средства
	ТранспортныеСредства.Колонки.Добавить("П000000000013"); // Грузоподъемность транспортного средства в тоннах
	ТранспортныеСредства.Колонки.Добавить("П000000000014"); // Регистрационный номер транспортного средства
	
	ДанныеВыгрузки.Вставить("ТранспортныеСредства", ТранспортныеСредства);
	
	Получатели = Новый ТаблицаЗначений;
	Получатели.Колонки.Добавить("ИдПолучателя"); // Уникальный идентификатор
	Получатели.Колонки.Добавить("П000000000019"); // Наименование получателя
	Получатели.Колонки.Добавить("П000000000020"); // ИНН получателя
	Получатели.Колонки.Добавить("П000000000021"); // КПП получателя
	
	ДанныеВыгрузки.Вставить("Получатели", Получатели);
	
	ПрототипУзла_ОбъемПеревозки = ПолучитьПодчиненныйЭлемент(Узел_Документ, "ОбъемПеревозки");
	
	// Сведения обособленных подразделений;
	Для Каждого Страница Из мДанныеОтчета.Декларация Цикл
		ДанныеСтраницы = Страница.Данные;
		
		СведенияОбОП = ДополнительныеДанныеПоАдресу(Страница.ДополнительныеДанные, "СведенияОбОП");
		Если СведенияОбОП = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НазваниеОрганизации = СведенияОбОП.Наименование;
		КПП = СведенияОбОП.КПП;
		
		Если ПустаяСтрока(НазваниеОрганизации) И ПустаяСтрока(КПП) Тогда
			Продолжить;
		КонецЕсли;
		
		Узел_ОбъемПеревозки = НовыйУзелИзПрототипа(ПрототипУзла_ОбъемПеревозки);
		УстановитьЗначениеЭлемента(Узел_ОбъемПеревозки, "НаимЮЛ", НазваниеОрганизации);
		УстановитьЗначениеЭлемента(Узел_ОбъемПеревозки, "КППЮЛ", КПП);
		
		Узел_АдрОрг = ПолучитьПодчиненныйЭлемент(Узел_ОбъемПеревозки, "АдрОрг");
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "КодСтраны",  "643");
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "Индекс",     СведенияОбОП.Индекс);
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "КодРегион",  СведенияОбОП.КодРегиона);
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "Район",      СведенияОбОП.Район);
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "Город",      СведенияОбОП.Город);
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "НаселПункт", СведенияОбОП.НаселенныйПункт);
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "Улица",      СведенияОбОП.Улица);
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "Дом",        СведенияОбОП.Дом);
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "Корпус",     СведенияОбОП.Корпус);
		УстановитьЗначениеЭлемента(Узел_АдрОрг, "Кварт",      СведенияОбОП.Квартира);
		
		ДанныеВыгрузки.Вставить("ДанныеСтраницы", Страница.Данные);
		ДанныеВыгрузки.Вставить("ДанныеДопСтрок", Страница.ДанныеДопСтрок);
		ДанныеВыгрузки.Вставить("ДополнительныеДанные", Страница.ДополнительныеДанные);
		ЗаполнитьСведенияПоСтранице(ДанныеВыгрузки, Узел_ОбъемПеревозки);
		
		Узел_Перевозка = ПолучитьПодчиненныйЭлемент(Узел_ОбъемПеревозки, "Перевозка");
		ЕстьПеревозки = (Узел_Перевозка <> Неопределено);
		УстановитьЗначениеЭлемента(Узел_ОбъемПеревозки, "НаличиеПеревозок", ?(ЕстьПеревозки, "true", "false"));
		
	КонецЦикла;
	УдалитьУзел(ПрототипУзла_ОбъемПеревозки);
	
	ЗаполнитьСправочникиДекларации(Узел_Справочники, ДанныеВыгрузки);
	
КонецПроцедуры

Процедура УстановитьЗначениеЭлемента(УзелРодитель, ИмяЭлемента, ЗначениеЭлемента)
	
	ПодчиненныйЭлемент = ПолучитьПодчиненныйЭлемент(УзелРодитель, ИмяЭлемента);
	ВывестиПоказательВXML(ПодчиненныйЭлемент, ЗначениеЭлемента);
	
КонецПроцедуры

Процедура ЗаполнитьСведенияПоСтранице(ДанныеВыгрузки, Узел_ОбъемПеревозки)
	
	ДанныеСтраницы = ДанныеВыгрузки.ДанныеСтраницы;
	ДанныеДопСтрок = ДанныеВыгрузки.ДанныеДопСтрок;
	ДополнительныеДанныеСтраницы = ДанныеВыгрузки.ДополнительныеДанные;
	
	ПрототипУзла_Перевозка = ПолучитьПодчиненныйЭлемент(Узел_ОбъемПеревозки, "Перевозка");
	
	ТаблицаПеревозок = ДанныеДопСтрок[мИдГруппы2].Скопировать();
	
	// Заполнение идентификаторов справочников выгрузки для последующей обработки.
	ТаблицаПеревозок.Колонки.Добавить("ИдПеревозчика");
	ТаблицаПеревозок.Колонки.Добавить("ИдЛицензии");
	ТаблицаПеревозок.Колонки.Добавить("ИдТС");
	Для Инд = 0 По ТаблицаПеревозок.Количество() - 1 Цикл
		Постфикс = "_" + Формат(Инд + 1, "ЧГ=0");
		
		АдресПеревозчика = ДополнительныеДанныеПоАдресу(ДополнительныеДанныеСтраницы, "П000010000307" + Постфикс);
		ТаблицаПеревозок[Инд].ИдПеревозчика = ПолучитьИдентификаторПеревозчика(ДанныеВыгрузки, ТаблицаПеревозок[Инд], АдресПеревозчика);
		ТаблицаПеревозок[Инд].ИдЛицензии = ПолучитьИдентификаторЛицензии(ДанныеВыгрузки, ТаблицаПеревозок[Инд]);
		ТаблицаПеревозок[Инд].ИдТС = ПолучитьИдентификаторТранспортногоСредства(ДанныеВыгрузки, ТаблицаПеревозок[Инд]);
	КонецЦикла;
	
	КодыВидовПеревозки = ТаблицаПеревозок.Скопировать(, "П000010000301");
	КодыВидовПеревозки.Свернуть("П000010000301");
	КодыВидовПеревозки = КодыВидовПеревозки.ВыгрузитьКолонку("П000010000301");
	
	НомерППКодаВидаПеревозки = 0;
	Для Каждого КодВидаПеревозки Из КодыВидовПеревозки Цикл
		
		Если ПустаяСтрока(КодВидаПеревозки) Тогда
			Продолжить;
		КонецЕсли;
		
		Узел_Перевозка = НовыйУзелИзПрототипа(ПрототипУзла_Перевозка);
		
		НомерППКодаВидаПеревозки = НомерППКодаВидаПеревозки + 1;
		
		УстановитьЗначениеЭлемента(Узел_Перевозка, "ПN", НомерППКодаВидаПеревозки);
		УстановитьЗначениеЭлемента(Узел_Перевозка, "П000000000002", КодВидаПеревозки);
		
		УсловияОтбора = Новый Структура("П000010000301", КодВидаПеревозки);
		СтрокиПродукции = ТаблицаПеревозок.НайтиСтроки(УсловияОтбора);
		ВидыПродукции = ТаблицаПеревозок.Скопировать(СтрокиПродукции, "П000010000302");
		ВидыПродукции.Свернуть("П000010000302");
		ВидыПродукции = ВидыПродукции.ВыгрузитьКолонку("П000010000302");
		
		ПрототипУзла_Продукция = ПолучитьПодчиненныйЭлемент(Узел_Перевозка, "Продукция");
		Для Каждого КодВидаПродукции Из ВидыПродукции Цикл
			Узел_Продукция = НовыйУзелИзПрототипа(ПрототипУзла_Продукция);
			УстановитьЗначениеЭлемента(Узел_Продукция, "П000000000004", КодВидаПродукции);
			
			УсловияОтбора = Новый Структура;
			УсловияОтбора.Вставить("П000010000301", КодВидаПеревозки);
			УсловияОтбора.Вставить("П000010000302", КодВидаПродукции);
			СтрокиПеревозчиков = ТаблицаПеревозок.НайтиСтроки(УсловияОтбора);
			ТаблицаПеревозчиков = ТаблицаПеревозок.Скопировать(СтрокиПеревозчиков);
			ТаблицаПеревозчиков.Свернуть("ИдПеревозчика, ИдЛицензии");
			
			НомерПППеревозчика = 0;
			ПрототипУзла_СведПеревозчик = ПолучитьПодчиненныйЭлемент(Узел_Продукция, "СведПеревозчик");
			Для Каждого СтрокаПеревозчика Из ТаблицаПеревозчиков Цикл
				Узел_СведПеревозчик = НовыйУзелИзПрототипа(ПрототипУзла_СведПеревозчик);
				
				НомерПППеревозчика = НомерПППеревозчика + 1;
				УстановитьЗначениеЭлемента(Узел_СведПеревозчик, "ПN", НомерПППеревозчика);
				УстановитьЗначениеЭлемента(Узел_СведПеревозчик, "ИдПеревозчика", СтрокаПеревозчика.ИдПеревозчика);
				УстановитьЗначениеЭлемента(Узел_СведПеревозчик, "ИдЛицензии", СтрокаПеревозчика.ИдЛицензии);
				
				УсловияОтбора = Новый Структура;
				УсловияОтбора.Вставить("П000010000301", КодВидаПеревозки);
				УсловияОтбора.Вставить("П000010000302", КодВидаПродукции);
				УсловияОтбора.Вставить("ИдПеревозчика", СтрокаПеревозчика.ИдПеревозчика);
				УсловияОтбора.Вставить("ИдЛицензии",    СтрокаПеревозчика.ИдЛицензии);
				
				СтрокиТранспортныхСредств = ТаблицаПеревозок.НайтиСтроки(УсловияОтбора);
				ТаблицаТранспортныхСредств = ТаблицаПеревозок.Скопировать(СтрокиТранспортныхСредств);
				ТаблицаТранспортныхСредств.Свернуть("ИдТС");
				
				НомерППТранспортногоСредства = 0;
				ПрототипУзла_ТранспортноеСредство = ПолучитьПодчиненныйЭлемент(Узел_СведПеревозчик, "ТранспортноеСредство");
				Для Каждого СтрокаТранспортногоСредства Из ТаблицаТранспортныхСредств Цикл
					Узел_ТранспортноеСредство = НовыйУзелИзПрототипа(ПрототипУзла_ТранспортноеСредство);
					
					НомерППТранспортногоСредства = НомерППТранспортногоСредства + 1;
					УстановитьЗначениеЭлемента(Узел_ТранспортноеСредство, "ПN", НомерППТранспортногоСредства);
					УстановитьЗначениеЭлемента(Узел_ТранспортноеСредство, "ИдТС", СтрокаТранспортногоСредства.ИдТС);
					
					УсловияОтбора = Новый Структура;
					УсловияОтбора.Вставить("П000010000301", КодВидаПеревозки);
					УсловияОтбора.Вставить("П000010000302", КодВидаПродукции);
					УсловияОтбора.Вставить("ИдПеревозчика", СтрокаПеревозчика.ИдПеревозчика);
					УсловияОтбора.Вставить("ИдЛицензии",    СтрокаПеревозчика.ИдЛицензии);
					УсловияОтбора.Вставить("ИдТС",          СтрокаТранспортногоСредства.ИдТС);
					
					СтрокиТранспортныхНакладных = ТаблицаПеревозок.НайтиСтроки(УсловияОтбора);
					ТаблицаТранспортныхНакладных = ТаблицаПеревозок.Скопировать(СтрокиТранспортныхНакладных);
					ТаблицаТранспортныхНакладных.Свернуть("П000010000315, П000010000316, П000010000317");
					
					НомерППТранспортнойНакладной = 0;
					ПрототипУзла_СведТН = ПолучитьПодчиненныйЭлемент(Узел_ТранспортноеСредство, "СведТН");
					Для Каждого СтрокаТранспортнойНакладной Из ТаблицаТранспортныхНакладных Цикл
						Узел_СведТН = НовыйУзелИзПрототипа(ПрототипУзла_СведТН);
						
						НомерППТранспортнойНакладной = НомерППТранспортнойНакладной + 1;
						УстановитьЗначениеЭлемента(Узел_СведТН, "ПN", НомерППТранспортнойНакладной);
						
						УстановитьЗначениеЭлемента(Узел_СведТН, "П000000000014", СтрокаТранспортнойНакладной.П000010000315);
						УстановитьЗначениеЭлемента(Узел_СведТН, "П000000000015", СтрокаТранспортнойНакладной.П000010000316);
						УстановитьЗначениеЭлемента(Узел_СведТН, "П000000000016", СтрокаТранспортнойНакладной.П000010000317);
						
						УсловияОтбора = Новый Структура;
						УсловияОтбора.Вставить("П000010000301", КодВидаПеревозки);
						УсловияОтбора.Вставить("П000010000302", КодВидаПродукции);
						УсловияОтбора.Вставить("ИдПеревозчика", СтрокаПеревозчика.ИдПеревозчика);
						УсловияОтбора.Вставить("ИдЛицензии",    СтрокаПеревозчика.ИдЛицензии);
						УсловияОтбора.Вставить("ИдТС",          СтрокаТранспортногоСредства.ИдТС);
						УсловияОтбора.Вставить("П000010000315", СтрокаТранспортнойНакладной.П000010000315);
						УсловияОтбора.Вставить("П000010000316", СтрокаТранспортнойНакладной.П000010000316);
						УсловияОтбора.Вставить("П000010000317", СтрокаТранспортнойНакладной.П000010000317);
						
						СтрокиТранспортныхНакладныхГрузоотправителя = ТаблицаПеревозок.НайтиСтроки(УсловияОтбора);
						
						НомерППТранспортнойНакладнойГрузоотправителя = 0;
						ПрототипУзла_СведТТН = ПолучитьПодчиненныйЭлемент(Узел_СведТН, "СведТТН");
						Для Каждого СтрокаТранспортнойНакладнойГрузоотправителя Из СтрокиТранспортныхНакладныхГрузоотправителя Цикл
							Узел_СведТТН = НовыйУзелИзПрототипа(ПрототипУзла_СведТТН);
							
							НомерППТранспортнойНакладнойГрузоотправителя = НомерППТранспортнойНакладнойГрузоотправителя + 1;
							УстановитьЗначениеЭлемента(Узел_СведТТН, "ПN", НомерППТранспортнойНакладнойГрузоотправителя);
							
							УстановитьЗначениеЭлемента(Узел_СведТТН, "П000000000017", СтрокаТранспортнойНакладнойГрузоотправителя.П000010000318);
							УстановитьЗначениеЭлемента(Узел_СведТТН, "П000000000018", СтрокаТранспортнойНакладнойГрузоотправителя.П000010000319);
							УстановитьЗначениеЭлемента(Узел_СведТТН, "П000000000019", СтрокаТранспортнойНакладнойГрузоотправителя.П000010000323);
							ИдПолучателя = ПолучитьИдентификаторГрузополучателя(ДанныеВыгрузки, СтрокаТранспортнойНакладнойГрузоотправителя);
							УстановитьЗначениеЭлемента(Узел_СведТТН, "ИдПолучателя", ИдПолучателя);
							УстановитьЗначениеЭлемента(Узел_СведТТН, "П000000000020", СтрокаТранспортнойНакладнойГрузоотправителя.П000010000324);
							
						КонецЦикла;
						УдалитьУзел(ПрототипУзла_СведТТН);
						
					КонецЦикла;
					УдалитьУзел(ПрототипУзла_СведТН);
					
				КонецЦикла;
				УдалитьУзел(ПрототипУзла_ТранспортноеСредство);
				
			КонецЦикла;
			УдалитьУзел(ПрототипУзла_СведПеревозчик);
			
		КонецЦикла;
		УдалитьУзел(ПрототипУзла_Продукция);
		
	КонецЦикла;
	
	УдалитьУзел(ПрототипУзла_Перевозка);
	
КонецПроцедуры

Функция ПолучитьИдентификаторТранспортногоСредства(ДанныеВыгрузки, СтрокаСведенийТранспортногоСредства)
	
	Результат = 0;
	
	СведенияУказаны = ЗначениеЗаполнено(СтрокаСведенийТранспортногоСредства.П000010000312)
	              ИЛИ ЗначениеЗаполнено(СтрокаСведенийТранспортногоСредства.П000010000313)
	              ИЛИ ЗначениеЗаполнено(СтрокаСведенийТранспортногоСредства.П000010000314);
	
	Если СведенияУказаны Тогда
		ТранспортныеСредства = ДанныеВыгрузки.ТранспортныеСредства;
		
		УсловияОтбора = Новый Структура;
		УсловияОтбора.Вставить("П000000000012", СокрЛП(СтрокаСведенийТранспортногоСредства.П000010000312));
		УсловияОтбора.Вставить("П000000000013", СтрокаСведенийТранспортногоСредства.П000010000313);
		УсловияОтбора.Вставить("П000000000014", СокрЛП(СтрокаСведенийТранспортногоСредства.П000010000314));
		
		НайденныеСтроки = ТранспортныеСредства.НайтиСтроки(УсловияОтбора);
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			
			Результат = НайденныеСтроки[0].ИдТС;
			
		Иначе
			НоваяСтрока = ТранспортныеСредства.Добавить();
			
			Идентификатор = ТранспортныеСредства.Индекс(НоваяСтрока) + 1;
			
			НоваяСтрока.ИдТС          = Идентификатор;
			НоваяСтрока.П000000000012 = СтрокаСведенийТранспортногоСредства.П000010000312;
			НоваяСтрока.П000000000013 = СтрокаСведенийТранспортногоСредства.П000010000313;
			НоваяСтрока.П000000000014 = СтрокаСведенийТранспортногоСредства.П000010000314;
			
			Результат = Идентификатор;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьИдентификаторПеревозчика(ДанныеВыгрузки, СтрокаСведенийПеревозчика, АдресПеревозчика)
	
	Результат = 0;
	
	Перевозчики = ДанныеВыгрузки.Перевозчики;
	
	УсловияОтбора = Новый Структура;
	
	ФИО = РазложитьФИО(СтрокаСведенийПеревозчика.П000010000306);
	УсловияОтбора.Вставить("Фамилия",  СокрЛП(ФИО.Фамилия));
	УсловияОтбора.Вставить("Имя",      СокрЛП(ФИО.Имя));
	УсловияОтбора.Вставить("Отчество", СокрЛП(ФИО.Отчество));
	
	УсловияОтбора.Вставить("КодСтраны",  "");
	УсловияОтбора.Вставить("КодРегион",  "");
	УсловияОтбора.Вставить("Индекс",     "");
	УсловияОтбора.Вставить("Район",      "");
	УсловияОтбора.Вставить("Город",      "");
	УсловияОтбора.Вставить("НаселПункт", "");
	УсловияОтбора.Вставить("Улица",      "");
	УсловияОтбора.Вставить("Дом",        "");
	УсловияОтбора.Вставить("Корпус",     "");
	УсловияОтбора.Вставить("Литера",     "");
	УсловияОтбора.Вставить("Кварт",      "");
	
	Если АдресПеревозчика <> Неопределено Тогда
		УсловияОтбора.Вставить("КодСтраны",  СокрЛП(АдресПеревозчика.КодСтраны));
		УсловияОтбора.Вставить("КодРегион",  СокрЛП(АдресПеревозчика.Регион));
		УсловияОтбора.Вставить("Индекс",     СокрЛП(АдресПеревозчика.Индекс));
		УсловияОтбора.Вставить("Район",      СокрЛП(АдресПеревозчика.Район));
		УсловияОтбора.Вставить("Город",      СокрЛП(АдресПеревозчика.Город));
		УсловияОтбора.Вставить("НаселПункт", СокрЛП(АдресПеревозчика.НаселенныйПункт));
		УсловияОтбора.Вставить("Улица",      СокрЛП(АдресПеревозчика.Улица));
		УсловияОтбора.Вставить("Дом",        СокрЛП(АдресПеревозчика.Дом));
		УсловияОтбора.Вставить("Корпус",     СокрЛП(АдресПеревозчика.Корпус));
		УсловияОтбора.Вставить("Кварт",      СокрЛП(АдресПеревозчика.Квартира));
	КонецЕсли;
	
	УсловияОтбора.Вставить("П000000000003", СокрЛП(СтрокаСведенийПеревозчика.П000010000303));
	УсловияОтбора.Вставить("П000000000004", СокрЛП(СтрокаСведенийПеревозчика.П000010000304));
	УсловияОтбора.Вставить("П000000000005", СокрЛП(СтрокаСведенийПеревозчика.П000010000305));
	
	ДанныеЗаполнены = Ложь;
	Для Каждого ЭлементОтбора Из УсловияОтбора Цикл
		Если ЗначениеЗаполнено(ЭлементОтбора.Значение) Тогда
			ДанныеЗаполнены = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ДанныеЗаполнены Тогда
		НайденныеСтроки = Перевозчики.НайтиСтроки(УсловияОтбора);
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			
			Результат = НайденныеСтроки[0].ИдПеревозчика;
			
		Иначе
			НоваяСтрока = Перевозчики.Добавить();
			
			Идентификатор = Перевозчики.Индекс(НоваяСтрока) + 1;
			
			НоваяСтрока.ИдПеревозчика = Идентификатор;
			ЗаполнитьЗначенияСвойств(НоваяСтрока, УсловияОтбора);
			
			Результат = Идентификатор;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьИдентификаторЛицензии(ДанныеВыгрузки, СтрокаСведенийПеревозчика)
	
	Результат = 0;
	
	Лицензии = ДанныеВыгрузки.Лицензии;
	
	ИдПеревозчика = СтрокаСведенийПеревозчика.ИдПеревозчика;
	
	УсловияОтбора = Новый Структура;
	УсловияОтбора.Вставить("П000000000008Серия", СокрЛП(СтрокаСведенийПеревозчика.П000010000381));
	УсловияОтбора.Вставить("П000000000008Номер", СокрЛП(СтрокаСведенийПеревозчика.П000010000382));
	УсловияОтбора.Вставить("П000000000009",      СтрокаСведенийПеревозчика.П000010000309);
	УсловияОтбора.Вставить("ИдПеревозчика",      ИдПеревозчика);
	
	ЛицензияУказана = (ЗначениеЗаполнено(СтрокаСведенийПеревозчика.П000010000381)
	               ИЛИ ЗначениеЗаполнено(СтрокаСведенийПеревозчика.П000010000382)
	               ИЛИ ЗначениеЗаполнено(СтрокаСведенийПеревозчика.П000010000309));
	
	Если ЛицензияУказана Тогда
	
		НайденныеСтроки = Лицензии.НайтиСтроки(УсловияОтбора);
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			
			Результат = НайденныеСтроки[0].ИдЛицензии;
			
		Иначе
			НоваяСтрока = Лицензии.Добавить();
			
			Идентификатор = Лицензии.Индекс(НоваяСтрока) + 1;
			
			НоваяСтрока.ИдЛицензии = Идентификатор;
			НоваяСтрока.П000000000008Серия = СтрокаСведенийПеревозчика.П000010000381;
			НоваяСтрока.П000000000008Номер = СтрокаСведенийПеревозчика.П000010000382;
			НоваяСтрока.П000000000009      = СтрокаСведенийПеревозчика.П000010000309;
			НоваяСтрока.П000000000010      = СтрокаСведенийПеревозчика.П000010000310;
			НоваяСтрока.П000000000011      = СтрокаСведенийПеревозчика.П000010000311;
			НоваяСтрока.ИдПеревозчика      = ИдПеревозчика;
			
			Результат = Идентификатор;
			
		КонецЕсли;
	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьИдентификаторГрузополучателя(ДанныеВыгрузки, СтрокаСведенийГрузополучателя)
	
	Результат = 0;
	
	Получатели = ДанныеВыгрузки.Получатели;
	
	УсловияОтбора = Новый Структура;
	УсловияОтбора.Вставить("П000000000019", СокрЛП(СтрокаСведенийГрузополучателя.П000010000320));
	УсловияОтбора.Вставить("П000000000020", СокрЛП(СтрокаСведенийГрузополучателя.П000010000321));
	УсловияОтбора.Вставить("П000000000021", СокрЛП(СтрокаСведенийГрузополучателя.П000010000322));
	
	НайденныеСтроки = Получатели.НайтиСтроки(УсловияОтбора);
	
	Если НайденныеСтроки.Количество() > 0 Тогда
		
		Результат = НайденныеСтроки[0].ИдПолучателя;
		
	Иначе
		НоваяСтрока = Получатели.Добавить();
		
		Идентификатор = Получатели.Индекс(НоваяСтрока) + 1;
		
		НоваяСтрока.ИдПолучателя  = Идентификатор;
		НоваяСтрока.П000000000019 = СтрокаСведенийГрузополучателя.П000010000320;
		НоваяСтрока.П000000000020 = СтрокаСведенийГрузополучателя.П000010000321;
		НоваяСтрока.П000000000021 = СтрокаСведенийГрузополучателя.П000010000322;
		
		Результат = Идентификатор;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьСправочникиДекларации(Узел_Справочники, ДанныеВыгрузки)
	
	ПрототипУзла_Перевозчики = ПолучитьПодчиненныйЭлемент(Узел_Справочники, "Перевозчики");
	Для Каждого СтрокаПеревозчика Из ДанныеВыгрузки.Перевозчики Цикл
		Узел_Перевозчики = НовыйУзелИзПрототипа(ПрототипУзла_Перевозчики);
		УстановитьЗначениеЭлемента(Узел_Перевозчики, "ИдПеревозчика", СтрокаПеревозчика.ИдПеревозчика);
		
		Узел_ПеревозчикЮрЛицо = ПолучитьПодчиненныйЭлемент(Узел_Перевозчики, "ПеревозчикЮрЛицо");
		Узел_ПеревозчикФизЛицо = ПолучитьПодчиненныйЭлемент(Узел_Перевозчики, "ПеревозчикФизЛицо");
		
		УказанКПП = НЕ ПустаяСтрока(СтрокаПеревозчика.П000000000005);
		Если УказанКПП Тогда
			УстановитьЗначениеЭлемента(Узел_ПеревозчикЮрЛицо, "П000000000003", СтрокаПеревозчика.П000000000003);
			УстановитьЗначениеЭлемента(Узел_ПеревозчикЮрЛицо, "П000000000004", СтрокаПеревозчика.П000000000004);
			УстановитьЗначениеЭлемента(Узел_ПеревозчикЮрЛицо, "П000000000005", СтрокаПеревозчика.П000000000005);
			
			УдалитьУзел(Узел_ПеревозчикФизЛицо);
		Иначе
			
			Узел_ФИО = ПолучитьПодчиненныйЭлемент(Узел_ПеревозчикФизЛицо, "ФИО");
			УстановитьЗначениеЭлемента(Узел_ФИО, "Фамилия",  СтрокаПеревозчика.Фамилия);
			УстановитьЗначениеЭлемента(Узел_ФИО, "Имя",      СтрокаПеревозчика.Имя);
			УстановитьЗначениеЭлемента(Узел_ФИО, "Отчество", СтрокаПеревозчика.Отчество);
			
			Узел_Адрес = ПолучитьПодчиненныйЭлемент(Узел_ПеревозчикФизЛицо, "Адрес");
			УстановитьЗначениеЭлемента(Узел_Адрес, "КодСтраны",  СтрокаПеревозчика.КодСтраны);
			УстановитьЗначениеЭлемента(Узел_Адрес, "КодРегион",  СтрокаПеревозчика.КодРегион);
			УстановитьЗначениеЭлемента(Узел_Адрес, "Индекс",     СтрокаПеревозчика.Индекс);
			УстановитьЗначениеЭлемента(Узел_Адрес, "Район",      СтрокаПеревозчика.Район);
			УстановитьЗначениеЭлемента(Узел_Адрес, "Город",      СтрокаПеревозчика.Город);
			УстановитьЗначениеЭлемента(Узел_Адрес, "НаселПункт", СтрокаПеревозчика.НаселПункт);
			УстановитьЗначениеЭлемента(Узел_Адрес, "Улица",      СтрокаПеревозчика.Улица);
			УстановитьЗначениеЭлемента(Узел_Адрес, "Дом",        СтрокаПеревозчика.Дом);
			УстановитьЗначениеЭлемента(Узел_Адрес, "Корпус",     СтрокаПеревозчика.Корпус);
			УстановитьЗначениеЭлемента(Узел_Адрес, "Литера",     СтрокаПеревозчика.Литера);
			УстановитьЗначениеЭлемента(Узел_Адрес, "Кварт",      СтрокаПеревозчика.Кварт);
			
			УдалитьУзел(Узел_ПеревозчикЮрЛицо);
		КонецЕсли;
		
		СтрокиЛицензий = ДанныеВыгрузки.Лицензии.НайтиСтроки(Новый Структура("ИдПеревозчика", СтрокаПеревозчика.ИдПеревозчика));
		Узел_Лицензии = ПолучитьПодчиненныйЭлемент(Узел_Перевозчики, "Лицензии");
		ПрототипУзла_Лицензия = ПолучитьПодчиненныйЭлемент(Узел_Лицензии, "Лицензия");
		Для Каждого СтрокаЛицензии Из СтрокиЛицензий Цикл
			Узел_Лицензия = НовыйУзелИзПрототипа(ПрототипУзла_Лицензия);
			УстановитьЗначениеЭлемента(Узел_Лицензия, "ИдЛицензии",    СтрокаЛицензии.ИдЛицензии);
			УстановитьЗначениеЭлемента(Узел_Лицензия, "П000000000008", СтрокаЛицензии.П000000000008Серия + ", " + СтрокаЛицензии.П000000000008Номер);
			УстановитьЗначениеЭлемента(Узел_Лицензия, "П000000000009", СтрокаЛицензии.П000000000009);
			УстановитьЗначениеЭлемента(Узел_Лицензия, "П000000000010", СтрокаЛицензии.П000000000010);
			УстановитьЗначениеЭлемента(Узел_Лицензия, "П000000000011", СтрокаЛицензии.П000000000011);
		КонецЦикла;
		УдалитьУзел(ПрототипУзла_Лицензия);
		
	КонецЦикла;
	УдалитьУзел(ПрототипУзла_Перевозчики);
	
	ПрототипУзла_ТранспортныеСредства = ПолучитьПодчиненныйЭлемент(Узел_Справочники, "ТранспортныеСредства");
	Для Каждого СтрокаТранспортногоСредства Из ДанныеВыгрузки.ТранспортныеСредства Цикл
		Узел_ТранспортныеСредства = НовыйУзелИзПрототипа(ПрототипУзла_ТранспортныеСредства);
		УстановитьЗначениеЭлемента(Узел_ТранспортныеСредства, "ИдТС", СтрокаТранспортногоСредства.ИдТС);
		УстановитьЗначениеЭлемента(Узел_ТранспортныеСредства, "П000000000012", СтрокаТранспортногоСредства.П000000000012);
		УстановитьЗначениеЭлемента(Узел_ТранспортныеСредства, "П000000000013", СтрокаТранспортногоСредства.П000000000013);
		УстановитьЗначениеЭлемента(Узел_ТранспортныеСредства, "П000000000014", СтрокаТранспортногоСредства.П000000000014);
	КонецЦикла;
	УдалитьУзел(ПрототипУзла_ТранспортныеСредства);
	
	ПрототипУзла_Получатели = ПолучитьПодчиненныйЭлемент(Узел_Справочники, "Получатели");
	Для Каждого СтрокаПолучателя Из ДанныеВыгрузки.Получатели Цикл
		Узел_Получатели = НовыйУзелИзПрототипа(ПрототипУзла_Получатели);
		УстановитьЗначениеЭлемента(Узел_Получатели, "ИдПолучателя", СтрокаПолучателя.ИдПолучателя);
		УстановитьЗначениеЭлемента(Узел_Получатели, "П000000000019", СтрокаПолучателя.П000000000019);
		
		Узел_ОрганизацияРФ = ПолучитьПодчиненныйЭлемент(Узел_Получатели, "ОрганизацияРФ");
		УстановитьЗначениеЭлемента(Узел_ОрганизацияРФ, "П000000000020", СтрокаПолучателя.П000000000020);
		УстановитьЗначениеЭлемента(Узел_ОрганизацияРФ, "П000000000021", СтрокаПолучателя.П000000000021);
	КонецЦикла;
	УдалитьУзел(ПрототипУзла_Получатели);
	
КонецПроцедуры

Процедура ЗаполнитьДанными(ДеревоВыгрузки, Параметры)
	
	ОбработатьУсловныеЭлементы(Параметры, ДеревоВыгрузки);
	
	ЗаполнитьДаннымиУзел(Параметры, ДеревоВыгрузки);
	
	ОтсечьНезаполненныеНеобязательныеУзлы(ДеревоВыгрузки);
	
КонецПроцедуры

Функция ИмеютсяАналогичныеСоседниеУзлы(Стр)
	
	Возврат (Стр.Родитель.Строки.НайтиСтроки(Новый Структура("Ключ", Стр.Ключ), Ложь).Количество() > 1);
	
КонецФункции

Процедура УдалитьУзел(Узел)
	
	РодительУзла = ?(Узел.Родитель = Неопределено, Узел.Владелец(), Узел.Родитель);
	РодительУзла.Строки.Удалить(Узел);
	
КонецПроцедуры

Процедура ОтсечьНезаполненныеНеобязательныеУзлы(Узел)
	
	КоличествоСтрок = Узел.Строки.Количество();
	Для Инд = 1 По КоличествоСтрок Цикл
		Стр = Узел.Строки.Получить(КоличествоСтрок - Инд);
		ОтсечьНезаполненныеНеобязательныеУзлы(Стр);
	КонецЦикла;
	
	Если ТипЗнч(Узел) <> Тип("ДеревоЗначений") Тогда
		Если (Найти(Узел.Обязательность, "Н") <> 0 ИЛИ Найти(Узел.Обязательность, "H") <> 0) И УзелПуст(Узел) Тогда // учтем оба варианта: кириллицу и латиницу
			УдалитьУзел(Узел);
		ИначеЕсли (Найти(Узел.Обязательность, "М") <> 0 ИЛИ Найти(Узел.Обязательность, "M") <> 0) // учтем оба варианта: кириллицу и латиницу
		И УзелПуст(Узел)
		И ?(Найти(Узел.Обязательность, "О") <> 0 ИЛИ Найти(Узел.Обязательность, "O") <> 0, ИмеютсяАналогичныеСоседниеУзлы(Узел), Истина) Тогда
			УдалитьУзел(Узел);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция УзелПуст(Узел)
	
	ПустойУзел = ?(Узел.Формат = "N", Узел.Значение = "0" ИЛИ (НЕ ЗначениеЗаполнено(Узел.Значение)), НЕ ЗначениеЗаполнено(Узел.Значение));
	Для Каждого Стр из Узел.Строки Цикл
		Если НЕ УзелПуст(Стр) Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	Возврат ПустойУзел;
	
КонецФункции

Функция УсловиеВыполнено(Параметры, Условие)
	
	Попытка
		РезультатВычисленияВыражения = Вычислить(СтрЗаменить(Условие, "&", "Параметры."));
		Если ТипЗнч(РезультатВычисленияВыражения) <> Тип("Булево") Тогда
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "Не удалось вычислить выражение-условие: """ + Условие + """!", "Титульный", "");
		КонецЕсли;
		Возврат НЕ (РезультатВычисленияВыражения = Ложь);
	Исключение
		РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "Не удалось вычислить выражение-условие: """ + Условие + """!", "Титульный", "");
		Возврат Истина;
	КонецПопытки;
	
КонецФункции

Функция СвойствоОпределено(Объект, ИмяСвойства)
	
	ГУИД = Новый УникальныйИдентификатор;
	ВремСтрукт = Новый Структура(ИмяСвойства, ГУИД);
	ЗаполнитьЗначенияСвойств(ВремСтрукт, Объект);
	Возврат (ВремСтрукт[ИмяСвойства] <> ГУИД);
	
КонецФункции

Процедура ОбработатьУсловныеЭлементы(Знач Параметры, Узел)
	
	КоличествоСтрок = Узел.Строки.Количество();
	Для Инд = 1 По КоличествоСтрок Цикл
		ТекСтр = Узел.Строки.Получить(КоличествоСтрок - Инд);
		Если НЕ ПустаяСтрока(ТекСтр.Условие) Тогда
			Если НЕ УсловиеВыполнено(Параметры, ТекСтр.Условие) Тогда
				Узел.Строки.Удалить(ТекСтр);
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		ОбработатьУсловныеЭлементы(Параметры, ТекСтр);
	КонецЦикла;
	
КонецПроцедуры

Функция ВывестиПоказательВXML(Узел, ЗначениеПоказателя)
	
	МинШирина = Узел.МинРазмерность;
	МаксШирина = Узел.МаксРазмерность;
	
	Если Узел.Формат = "T" ИЛИ Узел.Формат = "Т" Тогда // учтем оба варианта: кириллицу и латиницу
		Узел.Значение = ?(МаксШирина < СтрДлина(СокрЛП(ЗначениеПоказателя)), СокрЛП(Лев(СокрЛП(ЗначениеПоказателя), МаксШирина)), СокрЛП(ЗначениеПоказателя));
	ИначеЕсли Узел.Формат = "N" Тогда
		СтрокаФормата = "ЧРД=.;ЧН=0;ЧГ=;";
		Если Узел.МаксРазмерность <> 0 И Узел.МаксРазмерность <> 99999 Тогда
			СтрокаФормата = СтрокаФормата + "ЧЦ=" + Формат(Узел.МаксРазмерность, "ЧГ=") + ";";
		КонецЕсли;
		Если Узел.МинРазмерность <> 99999 Тогда
			СтрокаФормата = СтрокаФормата + "ЧДЦ=" + Формат(Узел.МинРазмерность, "ЧГ=") + ";";
		КонецЕсли;
		Узел.Значение = СокрЛП(Формат(ЗначениеПоказателя, СтрокаФормата));
	ИначеЕсли Узел.Формат = "gYear" Тогда
		Если ТипЗнч(ЗначениеПоказателя) = Тип("Дата") Тогда
			Узел.Значение = СокрЛП(Формат(ЗначениеПоказателя, "ДФ=гггг"));
		Иначе
			Узел.Значение = Прав(СокрЛП(ЗначениеПоказателя), 4);
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДаннымиУзел(Параметры, Узел)
	
	Для Каждого Стр из Узел.Строки Цикл
		Если Стр.Тип = "С" ИЛИ Стр.Тип = "C" Тогда // учтем оба варианта: кириллицу и латиницу
			Если СвойствоОпределено(Параметры, "ИмяКлючевогоУзлаСодержательнойЧасти") И Стр.Код = Параметры.ИмяКлючевогоУзлаСодержательнойЧасти Тогда
				ЗаполнитьДаннымиУзел(Параметры, Стр);
				ЗаполнитьСодержательнуюЧасть(Стр, Узел, Параметры);
			Иначе
				ЗаполнитьДаннымиУзел(Параметры, Стр);
			КонецЕсли;
		Иначе
			Если ПустаяСтрока(Стр.ЗначениеПоУмолчанию) Тогда
				Если НЕ ПустаяСтрока(Стр.Ключ) И СвойствоОпределено(Параметры, Стр.Ключ) Тогда
					ВывестиПоказательВXML(Стр, Параметры[Стр.Ключ]);
				Иначе
					Стр.Значение = "";
				КонецЕсли;
			ИначеЕсли Лев(Стр.ЗначениеПоУмолчанию, 1) = "&" Тогда
				Ключ = Сред(Стр.ЗначениеПоУмолчанию, 2);
				Если СвойствоОпределено(Параметры, Ключ) Тогда
					ВывестиПоказательВXML(Стр, Параметры[Ключ]);
				КонецЕсли;
			Иначе
				Стр.Значение = Стр.ЗначениеПоУмолчанию;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ЗначениеСоответствуетФормату(Стр)
	
	СтатусПроверки = Истина;
	Если Стр.Формат = "Т" ИЛИ Стр.Формат = "T" Тогда // учтем оба варианта: кириллицу и латиницу
		ДлинаЗначения = СтрДлина(Стр.Значение);
		Если ДлинаЗначения < Стр.МинРазмерность Тогда
			СтатусПроверки = Ложь;
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "Формирование XML: Длина значения показателя """ + ПредставлениеПоказателя(Стр) + """ (" + АдресПоказателя(Стр)+ ") меньше минимально возможной (" + ДлинаЗначения + " < " + Стр.МинРазмерность + ")", "Титульный", "");
		КонецЕсли;
		Если ДлинаЗначения > Стр.МаксРазмерность Тогда
			СтатусПроверки = Ложь;
			РегламентированнаяОтчетность.роСообщитьВТаблицуСообщений(ЭтаФорма, "Формирование XML: Длина значения показателя """ + ПредставлениеПоказателя(Стр) + """  (" + АдресПоказателя(Стр)+ ") больше максимально возможной (" + ДлинаЗначения + " > " + Стр.МаксРазмерность + ")", "Титульный", "");
		КонецЕсли;
	КонецЕсли;
	Возврат СтатусПроверки;
	
КонецФункции

Функция ПредставлениеПоказателя(Стр)
	
	Возврат ?(ПустаяСтрока(Стр.Представление), Стр.Код, Стр.Представление);
	
КонецФункции

Функция АдресПоказателя(СтрокаПоказателя)
	
	Результат = СтрокаПоказателя.Код;
	
	СтрокаВерхнегоУровня = СтрокаПоказателя.Родитель;
	Пока ЗначениеЗаполнено(СтрокаВерхнегоУровня) Цикл
		Результат = СтрокаВерхнегоУровня.Код + "\" + Результат;
		СтрокаВерхнегоУровня = СтрокаВерхнегоУровня.Родитель;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ПроверитьДанные(Узел)
	
	СтатусПроверки = Истина;
	Если ТипЗнч(Узел) <> Тип("ДеревоЗначений") Тогда
		Если НЕ ЗначениеСоответствуетФормату(Узел) Тогда
			СтатусПроверки = Ложь;
		КонецЕсли;
	КонецЕсли;
	Для Каждого Стр из Узел.Строки Цикл
		СтатусПроверки = (ПроверитьДанные(Стр) И СтатусПроверки); // именно в указанном порядке для выдачи всех ошибок
	КонецЦикла;
	Возврат СтатусПроверки;
	
КонецФункции

Функция ВыгрузитьДеревоВXML(ДеревоВыгрузки, Параметры)
	
	ПотокXML = СоздатьНовыйПотокXML();
	
	ЗаписатьУзелДереваВXML(ДеревоВыгрузки, ПотокXML, Параметры);
	
	ТекстДляЗаписи = ПотокXML.Закрыть();
	
	Возврат ТекстДляЗаписи;
	
КонецФункции

Функция СоздатьНовыйПотокXML() Экспорт
	
	ПотокXML = Новый ЗаписьXML();
	ПотокXML.УстановитьСтроку("windows-1251");
	ПотокXML.ЗаписатьОбъявлениеXML();
	ПотокXML.Отступ = Истина;
	Возврат ПотокXML;
	
КонецФункции

Функция ЗаписатьУзелДереваВXML(СтрокаДерева, ПотокXML, Параметры) Экспорт
	
	Если ТипЗнч(СтрокаДерева) = Тип("ДеревоЗначений") Тогда
		ПотокXML.ЗаписатьНачалоЭлемента("Файл");
		Для каждого Стр Из СтрокаДерева.Строки Цикл
			ЗаписатьУзелДереваВXML(Стр, ПотокXML, Параметры);
		КонецЦикла;
		ПотокXML.ЗаписатьКонецЭлемента();
	Иначе
		Если СтрокаДерева.Тип = "А" ИЛИ СтрокаДерева.Тип = "A" Тогда 
			ПотокXML.ЗаписатьАтрибут(СтрокаДерева.Код, Строка(СтрокаДерева.Значение));
		Иначе
			ПотокXML.ЗаписатьНачалоЭлемента(СтрокаДерева.Код);
			Для каждого Лист из СтрокаДерева.Строки Цикл
				ЗаписатьУзелДереваВXML(Лист, ПотокXML, Параметры);
			КонецЦикла;
			ПотокXML.ЗаписатьТекст(Строка(СтрокаДерева.Значение));
			ПотокXML.ЗаписатьКонецЭлемента();
		КонецЕсли;
	КонецЕсли;

КонецФункции

Функция ЗаписатьТесктНаДиск(ТекстДляЗаписи, ФайлДляЗаписи) Экспорт
	
	ПотокЗаписи = Новый ЗаписьТекста(ФайлДляЗаписи, "windows-1251");
	ПотокЗаписи.Записать(ТекстДляЗаписи);
	ПотокЗаписи.Закрыть();
	Сообщить("Отчет успешно выгружен в файл """ + ФайлДляЗаписи + """.", СтатусСообщения.Информация);
	Возврат Истина;
	
КонецФункции

Процедура ПроставитьНаВсехЛистах(Показатель, Знач ЗначениеПоказателя, Посимвольно = Истина)
	
	// Преобразовываем, при необходимости, значение показателя к типу "Строка".
	ТипЗначенияПоказателя = ТипЗнч(ЗначениеПоказателя);
	Если ТипЗначенияПоказателя = Тип("Число") Тогда
		ЗначениеПоказателя = Формат(ЗначениеПоказателя, "ЧГ=");
	ИначеЕсли ТипЗначенияПоказателя = Тип("Дата") Тогда
		ЗначениеПоказателя = Формат(ЗначениеПоказателя, "ДФ=dd.MM.yyyy");
	КонецЕсли;
	
	// Проставляем значение в табличные документы.
	Для Каждого Раздел Из мСвойстваРазделовДекларации.Строки Цикл
		ТаблДок = ЭлементыФормы["ПолеТабличногоДокумента" + Раздел.ИмяСтраницы];
		Если НЕ Посимвольно Тогда
			Если ТаблДок.Области.Найти(Показатель) <> Неопределено Тогда
				ТаблДок.Области[Показатель].Значение = ЗначениеПоказателя;
			КонецЕсли;
		Иначе
			Для Инд = 1 По СтрДлина(ЗначениеПоказателя) Цикл
				ТекОбласть = ТаблДок.Области.Найти(Показатель + Формат(Инд, "ЧГ="));
				Если ТекОбласть <> Неопределено Тогда
					ТекОбласть.Значение = Сред(ЗначениеПоказателя, Инд, 1);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	// Проставляем значение в многостраничные разделы.
	Для Каждого Раздел Из мСтруктураМногостраничныхРазделов Цикл
		СтраницыРаздела = Раздел.Значение;
		ТаблДокРаздела = ЭлементыФормы["ПолеТабличногоДокумента" + Раздел.Ключ];
		Если НЕ Посимвольно Тогда
			Если ТаблДокРаздела.Области.Найти(Показатель) <> Неопределено Тогда
				Для Каждого Стр Из СтраницыРаздела Цикл
					ДанныеСтраницы = Стр.Данные;
					Если ДанныеСтраницы = Неопределено Тогда
						Продолжить;
					КонецЕсли;
					ДанныеСтраницы.Вставить(Показатель, ЗначениеПоказателя);
				КонецЦикла;
			КонецЕсли;
		Иначе
			Для Каждого Стр Из СтраницыРаздела Цикл
				ДанныеСтраницы = Стр.Данные;
				Если ДанныеСтраницы = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				Для Инд = 1 По СтрДлина(ЗначениеПоказателя) Цикл
					Если ТаблДокРаздела.Области.Найти(Показатель + Формат(Инд, "ЧГ=")) <> Неопределено Тогда
						ДанныеСтраницы.Вставить(Показатель + Формат(Инд, "ЧГ="), Сред(ЗначениеПоказателя, Инд, 1));
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////

Процедура КоманднаяПанельФормыВыгрузитьПакет(Кнопка)
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Сообщить("Организация не выбрана! Выгрузка невозможна.", СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	// сохраняем при необходимости
	Если Модифицированность Тогда
		Если Вопрос("Перед выгрузкой отчета требуется сохранить введеную информацию." + Символы.ПС + "Продолжить?", РежимДиалогаВопрос.ДаНет) <> КодВозвратаДиалога.Да ИЛИ НЕ Сохранить() Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	РезультатВыгрузки = РегламентированнаяОтчетность.КонтекстЭДОЛокальный().ВыгрузитьПакетФСРАР(мСохраненныйДок.Ссылка, Истина);
	Если ЗначениеЗаполнено(РезультатВыгрузки) Тогда
		Сообщить("Пакет успешно выгружен в файл """ + РезультатВыгрузки + """.", СтатусСообщения.Информация);
	КонецЕсли;
	
КонецПроцедуры

Процедура КоманднаяПанельФормыОтправить(Кнопка)
	
	// сохраняем при необходимости
	Если Модифицированность Тогда
		Если Вопрос("Перед выгрузкой отчета требуется сохранить введеную информацию." + Символы.ПС + "Продолжить?",РежимДиалогаВопрос.ДаНет) <> КодВозвратаДиалога.Да ИЛИ НЕ Сохранить() Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	РегламентированнаяОтчетность.КонтекстЭДОЛокальный().ОтправитьОбъектВФСРАР(мСохраненныйДок.Ссылка, Кнопка, Справочники.ВидыОтправляемыхДокументов.ОбъемПеревозкиЭтиловогоСпиртаАлкогольнойИСпиртосодержащейПродукции);
	
КонецПроцедуры

Процедура КоманднаяПанельФормыПолучитьРезультат(Кнопка)
	
	Если мСохраненныйДок = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если РегламентированнаяОтчетность.КонтекстЭДОЛокальный().ОбновитьРезультатОтправкиФСРАР(мСохраненныйДок.Ссылка, Справочники.ВидыОтправляемыхДокументов.ОбъемПеревозкиЭтиловогоСпиртаАлкогольнойИСпиртосодержащейПродукции) Тогда
		// покажем протокол текущей отправки
		ПоказатьПротоколТекущейОтправки()
	КонецЕсли;
	
КонецПроцедуры

Процедура Подключаемый_КоманднаяПанельФормыПоказатьРезультат(Кнопка)
	
	// покажем протокол текущей отправки
	ПоказатьПротоколТекущейОтправки();
	
КонецПроцедуры

Процедура КоманднаяПанельФормыПоказатьОтправки(Кнопка)
	
	СсылкаФСРАР = ?(мСохраненныйДок <> Неопределено, мСохраненныйДок.Ссылка, Документы.РегламентированныйОтчет.ПустаяСсылка());
	РегламентированнаяОтчетность.КонтекстЭДОЛокальный().ПоказатьОтправкиФСРАР(СсылкаФСРАР);
	
КонецПроцедуры

Процедура КнопкаОбновитьСостояниеОтправкиНажатие(Элемент)
	
	Если мСохраненныйДок = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыОтправки = РегламентированнаяОтчетность.КонтекстЭДОЛокальный().ПолучитьРеквизитыОтправкиФСРАР(мСохраненныйДок.Ссылка);
	Если РеквизитыОтправки <> Неопределено Тогда
		Если РеквизитыОтправки.СтатусОтправки = Перечисления.СтатусыОтправки.Отправлен Тогда
			РегламентированнаяОтчетность.КонтекстЭДОЛокальный().ОбновитьРезультатОтправкиФСРАР(мСохраненныйДок.Ссылка, Справочники.ВидыОтправляемыхДокументов.ОбъемПеревозкиЭтиловогоСпиртаАлкогольнойИСпиртосодержащейПродукции);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура КнопкаОткрытьПротоколОтправкиНажатие(Элемент)
	
	Если мСохраненныйДок = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// покажем протокол текущей отправки
	ПоказатьПротоколТекущейОтправки();
	
КонецПроцедуры

Процедура ПоказатьПротоколТекущейОтправки()
	
	Если мСохраненныйДок = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыОтправки = РегламентированнаяОтчетность.КонтекстЭДОЛокальный().ПолучитьРеквизитыОтправкиФСРАР(мСохраненныйДок.Ссылка, Истина);
	
	Если РеквизитыОтправки = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ЗаголовокПротокола = "Протокол обработки";
	РегламентированнаяОтчетность.ПоказатьПротоколОбработки(РеквизитыОтправки.Протокол.Получить(), ЗаголовокПротокола);
	
КонецПроцедуры

//Андрей
Процедура ЗаполнитьАвто() Экспорт 
	
	Отказ = Ложь;
	РегламентированнаяОтчетность.ПередЗаполнениемРегламентированногоОтчета(ЭтаФорма, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Запрашиваем подтверждение у пользователя и очищаем отчет.
	ПрерватьАвтоЗаполнение = РегламентированнаяОтчетность.ПрерватьАвтоЗаполнениеПоДаннымИБ(ЭтаФорма);
	Если ПрерватьАвтоЗаполнение Тогда
		Возврат;
	КонецЕсли;
	
	РегламентированнаяОтчетность.роОчистить(ЭтаФорма, Ложь);
	
	// Здесь необходимо разместить вызовы процедур заполнения отчета.
	ИДКонфигурации = РегламентированнаяОтчетность.ИДКонфигурации();
	РедакцияКонфигурации = Лев(РегламентированнаяОтчетность.РедакцияКонфигурации(),1);
	СписокОрганизаций = Новый СписокЗначений;
	
	СписокОрганизаций.Добавить(Организация.Ссылка, Организация.Наименование);
	ЗаполнитьАвтоБухгалтерия(СписокОрганизаций);
	
	Расчет();
	//УстановитьДоступностьПолейТаблицы1();
	//УстановитьДоступностьПолейТаблицы2();
	
	Модифицированность = Истина;
	
	РегламентированнаяОтчетность.ОтключитьОбработчикАвтосохранения(ЭтаФорма);
	РегламентированнаяОтчетность.ВключитьОбработчикАвтосохранения(ЭтаФорма);
	
КонецПроцедуры

Процедура ЗаполнитьАвтоБухгалтерия(СписокОрганизаций) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДанныеНакладных.Ссылка,
		|	""АТ"" КАК П000010000101,
		|	""АП"" КАК П000010000102,
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000103, """") КАК П000010000103,
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000104, """") КАК П000010000104,
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000105, """") КАК П000010000105,
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000109, """") КАК П000010000109,
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000110, """") КАК П000010000110,
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000111, """") КАК П000010000111,
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000112, """") КАК П000010000112,
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000113, """") КАК П000010000113,
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000114, """") КАК П000010000114,
		|	ДанныеНакладных.Дата КАК П000010000115,
		|	ДанныеНакладных.Номер КАК П000010000116,
		|	СУММА(ДанныеНакладных.Количество * СведенияОбАлкогольнойПродукции.КоэффПересчетаДал) КАК П000010000117,
		|	ДанныеНакладных.Дата КАК П000010000118,
		|	ДанныеНакладных.Номер КАК П000010000119,
		|	ДанныеНакладных.Контрагент КАК П000010000120,
		|	ДанныеНакладных.Контрагент.ИНН КАК П000010000121,
		|	ДанныеНакладных.Контрагент.КПП КАК П000010000122,
		|	СУММА(ДанныеНакладных.Количество * СведенияОбАлкогольнойПродукции.КоэффПересчетаДал) КАК П000010000123,
		|	СУММА(ДанныеНакладных.Количество * СведенияОбАлкогольнойПродукции.КоэффПересчетаДал) КАК П000010000124,
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000181, """") КАК П000010000181,
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000182, """") КАК П000010000182
		|ИЗ
		|	(ВЫБРАТЬ
		|		ПоступлениеТоваровУслуг.Ссылка КАК Ссылка,
		|		ВЫБОР
		|			КОГДА ПоступлениеТоваровУслуг.ДатаВходящегоДокумента = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|				ТОГДА ПоступлениеТоваровУслуг.Дата
		|			ИНАЧЕ ПоступлениеТоваровУслуг.ДатаВходящегоДокумента
		|		КОНЕЦ КАК Дата,
		|		ВЫБОР
		|			КОГДА ПоступлениеТоваровУслуг.НомерВходящегоДокумента = """"
		|				ТОГДА ПоступлениеТоваровУслуг.Номер
		|			ИНАЧЕ ПоступлениеТоваровУслуг.НомерВходящегоДокумента
		|		КОНЕЦ КАК Номер,
		|		ВЫБОР
		|			КОГДА ПоступлениеТоваровУслуг.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|				ТОГДА ПоступлениеТоваровУслуг.Организация
		|			ИНАЧЕ ПоступлениеТоваровУслуг.Грузополучатель
		|		КОНЕЦ КАК Контрагент,
		|		ПоступлениеТоваровУслугТовары.Номенклатура КАК Номенклатура,
		|		ПоступлениеТоваровУслугТовары.Количество КАК Количество
		|	ИЗ
		|		Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
		|			ПО ПоступлениеТоваровУслугТовары.Ссылка = ПоступлениеТоваровУслуг.Ссылка
		|	ГДЕ
		|		ПоступлениеТоваровУслуг.Проведен
		|		И ПоступлениеТоваровУслуг.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|		И ПоступлениеТоваровУслуг.Организация В(&СписокОрганизаций)) КАК ДанныеНакладных
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбАлкогольнойПродукции КАК СведенияОбАлкогольнойПродукции
		|		ПО ДанныеНакладных.Номенклатура = СведенияОбАлкогольнойПродукции.Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АвтомобилиСобственные КАК АвтомобилиСобственные
		|		ПО ДанныеНакладных.Ссылка = АвтомобилиСобственные.ДокПоступления
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеНакладных.Контрагент,
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000114, """"),
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000112, """"),
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000111, """"),
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000110, """"),
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000105, """"),
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000109, """"),
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000103, """"),
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000104, """"),
		|	ДанныеНакладных.Ссылка,
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000181, """"),
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000182, """"),
		|	ДанныеНакладных.Дата,
		|	ДанныеНакладных.Номер,
		|	ДанныеНакладных.Контрагент.ИНН,
		|	ДанныеНакладных.Контрагент.КПП,
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000113, """"),
		|	ДанныеНакладных.Дата,
		|	ДанныеНакладных.Номер
		|
		|УПОРЯДОЧИТЬ ПО
		|	П000010000115";

	Запрос.УстановитьПараметр("ДатаНачала",			НачалоДня(ЭтотОбъект.мДатаНачалаПериодаОтчета));
	Запрос.УстановитьПараметр("ДатаОкончания",		КонецДня(ЭтотОбъект.мДатаКонцаПериодаОтчета));
	Запрос.УстановитьПараметр("СписокОрганизаций",	СписокОрганизаций);
	ВыборкаДетальныеЗаписи=Запрос.Выполнить().Выбрать();
	ИмяРаздела="Декларация";
	СтраницыРаздела = мСтруктураМногостраничныхРазделов[ИмяРаздела];
	
	АктивнаяСтраница = СтраницыРаздела.Найти(Истина, "АктивнаяСтраница");
	
	Если ТипЗнч(АктивнаяСтраница.ДополнительныеДанные) <> Тип("Структура") Тогда
		АктивнаяСтраница.ДополнительныеДанные = Новый Структура;
	КонецЕсли;
	
	ДополнительныеДанныеСтраницы = АктивнаяСтраница.ДополнительныеДанные;
	
	СведенияОбПоставщике = Неопределено;
	ИмяЭлементаСтруктуры = "П000010000107";
	
	//ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();
	
	ТаблицаЗаполнения1 = мСтруктураМногострочныхРазделов.П0000100001.СкопироватьКолонки();
	НС = 1;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АвтомобилиСобственные.П000010000103 КАК П000010000103,
		|	АвтомобилиСобственные.П000010000104 КАК П000010000104,
		|	АвтомобилиСобственные.П000010000105 КАК П000010000105,
		|	АвтомобилиСобственные.П000010000109 КАК П000010000109,
		|	АвтомобилиСобственные.П000010000110 КАК П000010000110,
		|	АвтомобилиСобственные.П000010000111 КАК П000010000111,
		|	АвтомобилиСобственные.П000010000112 КАК П000010000112,
		|	АвтомобилиСобственные.П000010000113 КАК П000010000113,
		|	АвтомобилиСобственные.П000010000114 КАК П000010000114,
		|	АвтомобилиСобственные.П000010000181 КАК П000010000181,
		|	АвтомобилиСобственные.П000010000182 КАК П000010000182
		|ИЗ
		|	РегистрСведений.АвтомобилиСобственные КАК АвтомобилиСобственные
		|ГДЕ
		|	АвтомобилиСобственные.ДокПоступления = ЗНАЧЕНИЕ(Документ.ПоступлениеТоваровУслуг.ПустаяСсылка)
		|	И АвтомобилиСобственные.ДокРеализация = ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)";
	ТабМашин=Запрос.Выполнить().Выгрузить();
	Машин=ТабМашин.Количество();
	ГСЧ=Новый ГенераторСлучайныхЧисел(31415);
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СтрокаЗаполнения = ТаблицаЗаполнения1.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаЗаполнения,ВыборкаДетальныеЗаписи);
		СтрокаЗаполнения.П000010000116=ПривестиНомерНаПечать(ВыборкаДетальныеЗаписи.П000010000116);
		СтрокаЗаполнения.П000010000119=ПривестиНомерНаПечать(ВыборкаДетальныеЗаписи.П000010000119);
		Если СтрокаЗаполнения.П000010000103="" Тогда
			Если Машин Тогда
				//Заполняем случайноой машиной
				Машина=ТабМашин.Получить(ГСЧ.СлучайноеЧисло(0,Машин-1));
				ЗаполнитьЗначенияСвойств(СтрокаЗаполнения,Машина);
				СтрокаЗаполнения.П000010000101="АТ";
				СтрокаЗаполнения.П000010000102="АП";
			Иначе
				Сообщить("В накладной не заполнены сведения о перевозчике.  "+ВыборкаДетальныеЗаписи.Ссылка,СтатусСообщения.ОченьВажное);
			КонецЕсли;
		КонецЕсли;
		НС = НС + 1;
		Состояние("Вывод отчета: "+Формат(НС/ВыборкаДетальныеЗаписи.Количество()*100,"ЧДЦ=2")+"%");
	КонецЦикла;
	
	//Возвраты
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДанныеНакладных.Ссылка,
		|	""АТ"" КАК П000010000101,
		|	""АП"" КАК П000010000102,
		|	ВЫРАЗИТЬ(ДанныеНакладных.Контрагент.НаименованиеПолное КАК СТРОКА(200)) КАК П000010000103,
		|	ДанныеНакладных.Контрагент.ИНН КАК П000010000104,
		|	ДанныеНакладных.Контрагент.КПП КАК П000010000105,
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000109, """") КАК П000010000109,
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000110, """") КАК П000010000110,
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000111, """") КАК П000010000111,
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000112, """") КАК П000010000112,
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000113, """") КАК П000010000113,
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000114, """") КАК П000010000114,
		|	ДанныеНакладных.Дата КАК П000010000115,
		|	ДанныеНакладных.Номер КАК П000010000116,
		|	СУММА(ДанныеНакладных.Количество * СведенияОбАлкогольнойПродукции.КоэффПересчетаДал) КАК П000010000117,
		|	ДанныеНакладных.Дата КАК П000010000118,
		|	ДанныеНакладных.Номер КАК П000010000119,
		|	ВЫРАЗИТЬ(ДанныеНакладных.Организация.НаименованиеСокращенное КАК СТРОКА(200)) КАК П000010000120,
		|	ДанныеНакладных.Организация.ИНН КАК П000010000121,
		|	ДанныеНакладных.Организация.КПП КАК П000010000122,
		|	СУММА(ДанныеНакладных.Количество * СведенияОбАлкогольнойПродукции.КоэффПересчетаДал) КАК П000010000123,
		|	СУММА(ДанныеНакладных.Количество * СведенияОбАлкогольнойПродукции.КоэффПересчетаДал) КАК П000010000124,
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000181, """") КАК П000010000181,
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000182, """") КАК П000010000182
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВозвратТоваровОтПокупателя.Ссылка КАК Ссылка,
		|		ВЫБОР
		|			КОГДА ВозвратТоваровОтПокупателя.ДатаВходящегоДокумента = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|				ТОГДА ВозвратТоваровОтПокупателя.Дата
		|			ИНАЧЕ ВозвратТоваровОтПокупателя.ДатаВходящегоДокумента
		|		КОНЕЦ КАК Дата,
		|		ВЫБОР
		|			КОГДА ВозвратТоваровОтПокупателя.НомерВходящегоДокумента = """"
		|				ТОГДА ВозвратТоваровОтПокупателя.Номер
		|			ИНАЧЕ ВозвратТоваровОтПокупателя.НомерВходящегоДокумента
		|		КОНЕЦ КАК Номер,
		|		ВозвратТоваровОтПокупателя.Организация КАК Организация,
		|		ВозвратТоваровОтПокупателя.Контрагент КАК Контрагент,
		|		ВозвратТоваровОтПокупателяТовары.Номенклатура КАК Номенклатура,
		|		ВозвратТоваровОтПокупателяТовары.Количество КАК Количество
		|	ИЗ
		|		Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
		|			ПО ВозвратТоваровОтПокупателяТовары.Ссылка = ВозвратТоваровОтПокупателя.Ссылка
		|	ГДЕ
		|		ВозвратТоваровОтПокупателя.Проведен
		|		И ВозвратТоваровОтПокупателя.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|		И ВозвратТоваровОтПокупателя.Организация В(&СписокОрганизаций)) КАК ДанныеНакладных
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбАлкогольнойПродукции КАК СведенияОбАлкогольнойПродукции
		|		ПО ДанныеНакладных.Номенклатура = СведенияОбАлкогольнойПродукции.Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АвтомобилиСобственные КАК АвтомобилиСобственные
		|		ПО ДанныеНакладных.Ссылка = АвтомобилиСобственные.ДокПоступления
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеНакладных.Ссылка,
		|	ВЫРАЗИТЬ(ДанныеНакладных.Контрагент.НаименованиеПолное КАК СТРОКА(200)),
		|	ДанныеНакладных.Контрагент.ИНН,
		|	ДанныеНакладных.Контрагент.КПП,
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000109, """"),
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000110, """"),
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000111, """"),
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000112, """"),
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000114, """"),
		|	ВЫРАЗИТЬ(ДанныеНакладных.Организация.НаименованиеСокращенное КАК СТРОКА(200)),
		|	ДанныеНакладных.Организация.ИНН,
		|	ДанныеНакладных.Организация.КПП,
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000181, """"),
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000182, """"),
		|	ДанныеНакладных.Дата,
		|	ДанныеНакладных.Номер,
		|	ДанныеНакладных.Дата,
		|	ДанныеНакладных.Номер,
		|	ЕСТЬNULL(АвтомобилиСобственные.П000010000113, """")
		|
		|УПОРЯДОЧИТЬ ПО
		|	П000010000115";

	Запрос.УстановитьПараметр("ДатаНачала",			НачалоДня(ЭтотОбъект.мДатаНачалаПериодаОтчета));
	Запрос.УстановитьПараметр("ДатаОкончания",		КонецДня(ЭтотОбъект.мДатаКонцаПериодаОтчета));
	Запрос.УстановитьПараметр("СписокОрганизаций",	СписокОрганизаций);
	ВыборкаДетальныеЗаписи=Запрос.Выполнить().Выбрать();
	ИмяРаздела="Декларация";
	СтраницыРаздела = мСтруктураМногостраничныхРазделов[ИмяРаздела];
	
	АктивнаяСтраница = СтраницыРаздела.Найти(Истина, "АктивнаяСтраница");
	
	Если ТипЗнч(АктивнаяСтраница.ДополнительныеДанные) <> Тип("Структура") Тогда
		АктивнаяСтраница.ДополнительныеДанные = Новый Структура;
	КонецЕсли;
	
	ДополнительныеДанныеСтраницы = АктивнаяСтраница.ДополнительныеДанные;
	
	СведенияОбПоставщике = Неопределено;
	ИмяЭлементаСтруктуры = "П000010000107";
	
	//ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();
	
	//ТаблицаЗаполнения1 = мСтруктураМногострочныхРазделов.П0000100001.СкопироватьКолонки();
	//НС = 1;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СтрокаЗаполнения = ТаблицаЗаполнения1.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаЗаполнения,ВыборкаДетальныеЗаписи);
		СтрокаЗаполнения.П000010000116=ПривестиНомерНаПечать(ВыборкаДетальныеЗаписи.П000010000116);
		СтрокаЗаполнения.П000010000119=ПривестиНомерНаПечать(ВыборкаДетальныеЗаписи.П000010000119);
		СтрокаЗаполнения.П000010000101="АТ";
		СтрокаЗаполнения.П000010000102="АП";
		НС = НС + 1;
		Состояние("Вывод отчета: "+Формат(НС/ВыборкаДетальныеЗаписи.Количество()*100,"ЧДЦ=2")+"%");
	КонецЦикла;
	Состояние("");
	ВывестиСведенияПодразделения(СведенияОПодразделении(Организация), "Декларация");
	ВывестиРазделВТабличныйДокумент("П0000100001", ТаблицаЗаполнения1);
	
	ТаблицаЗаполнения13 = ТаблицаЗаполнения1.Скопировать();
	
	Для Каждого Колонка Из ТаблицаЗаполнения13.Колонки Цикл
		Колонка.Имя = СтрЗаменить(Колонка.Имя, "П0000100001", "П0000100003");
	КонецЦикла;
	
	ВывестиРазделВТабличныйДокумент("П0000100003", ТаблицаЗаполнения13);
	Расчет();
КонецПроцедуры

Процедура ВывестиСведенияПодразделения(СведенияОПодразделении, ИмяРаздела)
	
	ИДКонфигурации = РегламентированнаяОтчетность.ИДКонфигурации();
	Если ИДКонфигурации = "УПП" ИЛИ ИДКонфигурации = "КА" Тогда
		СведенияОбОрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Организация, ДатаПодписи, Ложь);
	Иначе
		СведенияОбОрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Организация);
	КонецЕсли;
	ИНН = СведенияОбОрганизации.ИНН;
	
	СтраницыРаздела = мСтруктураМногостраничныхРазделов[ИмяРаздела];
	ПолеТабличногоДокумента = ЭлементыФормы["ПолеТабличногоДокумента" + ИмяРаздела];
	
	Если СтраницыРаздела.Колонки.Найти("ДополнительныеДанные") = Неопределено Тогда
		СтраницыРаздела.Колонки.Добавить("ДополнительныеДанные");
	КонецЕсли;
	
	АктивнаяСтраница = СтраницыРаздела.Найти(Истина, "АктивнаяСтраница");
	
	Если ТипЗнч(АктивнаяСтраница.ДополнительныеДанные) <> Тип("Структура") Тогда
		АктивнаяСтраница.ДополнительныеДанные = Новый Структура;
	КонецЕсли;
	
	СведенияОбОП = Неопределено;
	ИмяЭлементаСтруктуры = "СведенияОбОП";
	
	Если НЕ АктивнаяСтраница.ДополнительныеДанные.Свойство(ИмяЭлементаСтруктуры, СведенияОбОП)
		ИЛИ СведенияОбОП = Неопределено Тогда
		
		СведенияОбОП = Новый Структура;
		
		СведенияОбОП.Вставить("Наименование", "");
		СведенияОбОП.Вставить("ИНН",          "");
		СведенияОбОП.Вставить("КПП",          "");
		
		СведенияОбОП.Вставить("Индекс",          "");
		СведенияОбОП.Вставить("КодРегиона",          "");
		СведенияОбОП.Вставить("Район",           "");
		СведенияОбОП.Вставить("Город",           "");
		СведенияОбОП.Вставить("НаселенныйПункт", "");
		СведенияОбОП.Вставить("Улица",           "");
		СведенияОбОП.Вставить("Дом",             "");
		СведенияОбОП.Вставить("Корпус",          "");
		СведенияОбОП.Вставить("Квартира",        "");
		
		СведенияОбОП.Вставить("ПредставлениеАдреса", "");
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(СведенияОбОП, СведенияОПодразделении);
	ЗаполнитьЗначенияСвойств(СведенияОбОП, СведенияОПодразделении.ЭлементыАдреса);
	СведенияОбОП.ПредставлениеАдреса = СведенияОПодразделении.Адрес;
	СведенияОбОП.ИНН = ИНН;
	
	АктивнаяСтраница.ДополнительныеДанные.Вставить(ИмяЭлементаСтруктуры, СведенияОбОП);
	
	ВыводитьРеквизиты = (НЕ ПустаяСтрока(СведенияОбОП.Наименование) 
	                ИЛИ НЕ ПустаяСтрока(СведенияОбОП.ИНН) 
	                ИЛИ НЕ ПустаяСтрока(СведенияОбОП.КПП)
	                ИЛИ НЕ ПустаяСтрока(СведенияОбОП.ПредставлениеАдреса));
	
	Если ВыводитьРеквизиты Тогда
		НаименованиеОП = "";
		Если НЕ ПустаяСтрока(СведенияОбОП.Наименование) Тогда
			НаименованиеОП = СведенияОбОП.Наименование;
		КонецЕсли;
		Если НЕ ПустаяСтрока(СведенияОбОП.ИНН) Тогда
			НаименованиеОП = НаименованиеОП + ?(ПустаяСтрока(НаименованиеОП), "", ", ") + "ИНН " + СведенияОбОП.ИНН;
		КонецЕсли;
		Если НЕ ПустаяСтрока(СведенияОбОП.КПП) Тогда
			НаименованиеОП = НаименованиеОП + ?(ПустаяСтрока(НаименованиеОП), "", ", ") + "КПП " + СведенияОбОП.КПП;
		КонецЕсли;
		Если НЕ ПустаяСтрока(СведенияОбОП.ПредставлениеАдреса) Тогда
			НаименованиеОП = НаименованиеОП + ?(ПустаяСтрока(НаименованиеОП), "", Символы.ПС) + СведенияОбОП.ПредставлениеАдреса;
		КонецЕсли;
	Иначе
		НаименованиеОП = "";
	КонецЕсли;
	
	ПолеТабличногоДокумента.Области.ОбособленноеПодразделение.Значение = НаименованиеОП;
	
КонецПроцедуры

Функция СведенияОПодразделении(Подразделение)
	
	ИДКонфигурации = РегламентированнаяОтчетность.ИДКонфигурации();
	
	ЭлементыАдреса = Новый Структура();
	ЭлементыАдреса.Вставить("Индекс",          "");
	ЭлементыАдреса.Вставить("КодРегиона",          "");
	ЭлементыАдреса.Вставить("Район",           "");
	ЭлементыАдреса.Вставить("Город",           "");
	ЭлементыАдреса.Вставить("НаселенныйПункт", "");
	ЭлементыАдреса.Вставить("Улица",           "");
	ЭлементыАдреса.Вставить("Дом",             "");
	ЭлементыАдреса.Вставить("Корпус",          "");
	ЭлементыАдреса.Вставить("Квартира",        "");
	ЭлементыАдреса.Вставить("ТипДома",         "");
	ЭлементыАдреса.Вставить("ТипКорпуса",      "");
	ЭлементыАдреса.Вставить("ТипКвартиры",     "");
	
	Если Не ЗначениеЗаполнено(Подразделение) Тогда
		
		Возврат Новый Структура("Наименование, КПП, Адрес, ЭлементыАдреса", "", "", "", ЭлементыАдреса);
		
	ИначеЕсли ТипЗнч(Подразделение) = Тип("СправочникСсылка.Организации") Тогда
		
		Если ИДКонфигурации = "УПП" ИЛИ ИДКонфигурации = "КА" Тогда
			СведенияОбОрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Подразделение, ДатаПодписи, Ложь);
		Иначе
			СведенияОбОрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Подразделение);
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Объект", Подразделение);
		Запрос.УстановитьПараметр("Тип"   , Перечисления.ТипыКонтактнойИнформации.Адрес);
		Запрос.УстановитьПараметр("Вид"   , Справочники.ВидыКонтактнойИнформации.ФактАдресОрганизации);
		
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		               |	КонтактнаяИнформация.Представление,
		               |	КонтактнаяИнформация.Поле1,
		               |	КонтактнаяИнформация.Поле2,
		               |	КонтактнаяИнформация.Поле3,
		               |	КонтактнаяИнформация.Поле4,
		               |	КонтактнаяИнформация.Поле5,
		               |	КонтактнаяИнформация.Поле6,
		               |	КонтактнаяИнформация.Поле7,
		               |	КонтактнаяИнформация.Поле8,
		               |	КонтактнаяИнформация.Поле9,
		               |	КонтактнаяИнформация.ТипДома,
		               |	КонтактнаяИнформация.ТипКорпуса,
		               |	КонтактнаяИнформация.ТипКвартиры
		               |ИЗ
		               |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
		               |ГДЕ
		               |	КонтактнаяИнформация.Объект = &Объект
		               |	И КонтактнаяИнформация.Тип = &Тип
		               |	И КонтактнаяИнформация.Вид = &Вид";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если НЕ РезультатЗапроса.Пустой() Тогда
			КонтактнаяИнформация = РезультатЗапроса.Выгрузить()[0];
			Адрес = КонтактнаяИнформация.Представление;
			
			ЭлементыАдреса.Индекс          = КонтактнаяИнформация.Поле1;
			ЭлементыАдреса.КодРегиона          = РегламентированнаяОтчетность.КодРегионаПоНазванию(КонтактнаяИнформация.Поле2);
			ЭлементыАдреса.Район           = КонтактнаяИнформация.Поле3;
			ЭлементыАдреса.Город           = КонтактнаяИнформация.Поле4;
			ЭлементыАдреса.НаселенныйПункт = КонтактнаяИнформация.Поле5;
			ЭлементыАдреса.Улица           = КонтактнаяИнформация.Поле6;
			ЭлементыАдреса.Дом             = КонтактнаяИнформация.Поле7;
			ЭлементыАдреса.Корпус          = КонтактнаяИнформация.Поле8;
			ЭлементыАдреса.Квартира        = КонтактнаяИнформация.Поле9;
			ЭлементыАдреса.ТипДома         = КонтактнаяИнформация.ТипДома;
			ЭлементыАдреса.ТипКорпуса      = КонтактнаяИнформация.ТипКорпуса;
			ЭлементыАдреса.ТипКвартиры     = КонтактнаяИнформация.ТипКвартиры;
		Иначе
			Адрес = "";
		КонецЕсли;
		
		Возврат Новый Структура("Наименование, КПП, Адрес, ЭлементыАдреса", СведенияОбОрганизации.ПолноеНаименование, СведенияОбОрганизации.КПП, Адрес, ЭлементыАдреса);
		
	ИначеЕсли ТипЗнч(Подразделение) = Тип("СправочникСсылка.ПодразделенияОрганизаций") Тогда
		
		Наименование = Подразделение.НаименованиеПолное;
		КПП = ?(ЗначениеЗаполнено(Подразделение.РегистрацияВИФНС), Подразделение.РегистрацияВИФНС.КПП, Подразделение.КПП);
		Адрес = "";
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Объект", Подразделение);
		Запрос.УстановитьПараметр("Тип"   , Перечисления.ТипыКонтактнойИнформации.Адрес);
		Запрос.УстановитьПараметр("Вид"   , Справочники.ВидыКонтактнойИнформации.ФактАдресПодразделенияОрганизации);
		
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		               |	КонтактнаяИнформация.Представление,
		               |	КонтактнаяИнформация.Поле1,
		               |	КонтактнаяИнформация.Поле2,
		               |	КонтактнаяИнформация.Поле3,
		               |	КонтактнаяИнформация.Поле4,
		               |	КонтактнаяИнформация.Поле5,
		               |	КонтактнаяИнформация.Поле6,
		               |	КонтактнаяИнформация.Поле7,
		               |	КонтактнаяИнформация.Поле8,
		               |	КонтактнаяИнформация.Поле9,
		               |	КонтактнаяИнформация.ТипДома,
		               |	КонтактнаяИнформация.ТипКорпуса,
		               |	КонтактнаяИнформация.ТипКвартиры
		               |ИЗ
		               |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
		               |ГДЕ
		               |	КонтактнаяИнформация.Объект = &Объект
		               |	И КонтактнаяИнформация.Тип = &Тип
		               |	И КонтактнаяИнформация.Вид = &Вид";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если НЕ РезультатЗапроса.Пустой() Тогда
			КонтактнаяИнформация = РезультатЗапроса.Выгрузить()[0];
			Адрес = КонтактнаяИнформация.Представление;
			
			ЭлементыАдреса.Индекс          = КонтактнаяИнформация.Поле1;
			ЭлементыАдреса.КодРегиона      = РегламентированнаяОтчетность.КодРегионаПоНазванию(КонтактнаяИнформация.Поле2);
			ЭлементыАдреса.Район           = КонтактнаяИнформация.Поле3;
			ЭлементыАдреса.Город           = КонтактнаяИнформация.Поле4;
			ЭлементыАдреса.НаселенныйПункт = КонтактнаяИнформация.Поле5;
			ЭлементыАдреса.Улица           = КонтактнаяИнформация.Поле6;
			ЭлементыАдреса.Дом             = КонтактнаяИнформация.Поле7;
			ЭлементыАдреса.Корпус          = КонтактнаяИнформация.Поле8;
			ЭлементыАдреса.Квартира        = КонтактнаяИнформация.Поле9;
			ЭлементыАдреса.ТипДома         = КонтактнаяИнформация.ТипДома;
			ЭлементыАдреса.ТипКорпуса      = КонтактнаяИнформация.ТипКорпуса;
			ЭлементыАдреса.ТипКвартиры     = КонтактнаяИнформация.ТипКвартиры;
		Иначе
			Адрес = "";
		КонецЕсли;
		
		Возврат Новый Структура("Наименование, КПП, Адрес, ЭлементыАдреса", Наименование, КПП, Адрес, ЭлементыАдреса);
		
	Иначе
		
		Возврат Новый Структура("Наименование, КПП, Адрес, ЭлементыАдреса", "", "", "", ЭлементыАдреса);
		
	КонецЕсли;
	
КонецФункции

Процедура КоманднаяПанельФормыЗаполнить(Кнопка)
	ЗаполнитьАвто();
КонецПроцедуры

Процедура ПолучитьСведенияОбАдресе(Объект,ТипАдрес,Адрес,ЭлементыАдреса)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Объект", Объект);
	Запрос.УстановитьПараметр("Тип"   , Перечисления.ТипыКонтактнойИнформации.Адрес);
	Запрос.УстановитьПараметр("Вид"   , ТипАдрес);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	               |	КонтактнаяИнформация.Представление,
	               |	КонтактнаяИнформация.Поле1,
	               |	КонтактнаяИнформация.Поле2,
	               |	КонтактнаяИнформация.Поле3,
	               |	КонтактнаяИнформация.Поле4,
	               |	КонтактнаяИнформация.Поле5,
	               |	КонтактнаяИнформация.Поле6,
	               |	КонтактнаяИнформация.Поле7,
	               |	КонтактнаяИнформация.Поле8,
	               |	КонтактнаяИнформация.Поле9,
	               |	КонтактнаяИнформация.ТипДома,
	               |	КонтактнаяИнформация.ТипКорпуса,
	               |	КонтактнаяИнформация.ТипКвартиры
	               |ИЗ
	               |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	               |ГДЕ
	               |	КонтактнаяИнформация.Объект = &Объект
	               |	И КонтактнаяИнформация.Тип = &Тип
	               |	И КонтактнаяИнформация.Вид = &Вид";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		КонтактнаяИнформация = РезультатЗапроса.Выгрузить()[0];
		Адрес = КонтактнаяИнформация.Представление;
		
		ЭлементыАдреса.Индекс          = КонтактнаяИнформация.Поле1;
		ЭлементыАдреса.КодРегиона      = РегламентированнаяОтчетность.КодРегионаПоНазванию(КонтактнаяИнформация.Поле2);
		ЭлементыАдреса.Район           = КонтактнаяИнформация.Поле3;
		ЭлементыАдреса.Город           = КонтактнаяИнформация.Поле4;
		ЭлементыАдреса.НаселенныйПункт = КонтактнаяИнформация.Поле5;
		ЭлементыАдреса.Улица           = КонтактнаяИнформация.Поле6;
		ЭлементыАдреса.Дом             = КонтактнаяИнформация.Поле7;
		ЭлементыАдреса.Корпус          = КонтактнаяИнформация.Поле8;
		ЭлементыАдреса.Квартира        = КонтактнаяИнформация.Поле9;
		ЭлементыАдреса.ТипДома         = КонтактнаяИнформация.ТипДома;
		ЭлементыАдреса.ТипКорпуса      = КонтактнаяИнформация.ТипКорпуса;
		ЭлементыАдреса.ТипКвартиры     = КонтактнаяИнформация.ТипКвартиры;
	Иначе
		Адрес = "";
	КонецЕсли;
КонецПроцедуры

Процедура ПолучитьСведенияОбАдресеКонтрагнета(Контрагент,СтруктураАдреса)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Объект", Контрагент);
	Запрос.УстановитьПараметр("Тип"   , Перечисления.ТипыКонтактнойИнформации.Адрес);
	Запрос.УстановитьПараметр("Вид"   , Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	               |	КонтактнаяИнформация.Представление,
	               |	КонтактнаяИнформация.Поле1,
	               |	КонтактнаяИнформация.Поле2,
	               |	КонтактнаяИнформация.Поле3,
	               |	КонтактнаяИнформация.Поле4,
	               |	КонтактнаяИнформация.Поле5,
	               |	КонтактнаяИнформация.Поле6,
	               |	КонтактнаяИнформация.Поле7,
	               |	КонтактнаяИнформация.Поле8,
	               |	КонтактнаяИнформация.Поле9,
	               |	КонтактнаяИнформация.ТипДома,
	               |	КонтактнаяИнформация.ТипКорпуса,
	               |	КонтактнаяИнформация.ТипКвартиры
	               |ИЗ
	               |	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	               |ГДЕ
	               |	КонтактнаяИнформация.Объект = &Объект
	               |	И КонтактнаяИнформация.Тип = &Тип
	               |	И КонтактнаяИнформация.Вид = &Вид";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		КонтактнаяИнформация = РезультатЗапроса.Выгрузить()[0];
		Адрес = КонтактнаяИнформация.Представление;
		Если ЗначениеЗаполнено(КонтактнаяИнформация.Поле6) Тогда //Российский адрес. Потому что хотя улицу должны заполнить были
			Если Не ЗначениеЗаполнено(КонтактнаяИнформация.Поле2) Тогда
				Сообщить("Для контрагента "+Контрагент.НаименованиеПолное+" не заполнено поле Регион",СтатусСообщения.ОченьВажное);
			КонецЕсли;
			СтруктураАдреса.Страна			= "643";
			СтруктураАдреса.Индекс          = КонтактнаяИнформация.Поле1;
			СтруктураАдреса.Регион  	    = КонтактнаяИнформация.Поле2;
			СтруктураАдреса.Район           = КонтактнаяИнформация.Поле3;
			СтруктураАдреса.Город           = КонтактнаяИнформация.Поле4;
			СтруктураАдреса.НаселенныйПункт = КонтактнаяИнформация.Поле5;
			СтруктураАдреса.Улица           = КонтактнаяИнформация.Поле6;
			СтруктураАдреса.Дом             = КонтактнаяИнформация.Поле7;
			СтруктураАдреса.Корпус          = КонтактнаяИнформация.Поле8;
			СтруктураАдреса.Квартира        = КонтактнаяИнформация.Поле9;
			СтруктураАдреса.ТипДома         = КонтактнаяИнформация.ТипДома;
			СтруктураАдреса.ТипКорпуса      = КонтактнаяИнформация.ТипКорпуса;
			СтруктураАдреса.ТипКвартиры     = КонтактнаяИнформация.ТипКвартиры;
			СтруктураАдреса.Адрес			= Адрес;
		Иначе //Иностранный Адрес
			Сообщить("Для контрагента "+Контрагент.НаименованиеПолное+" необходимо заполнить страну",СтатусСообщения.Внимание);
			СтруктураАдреса.Страна			= "";
			СтруктураАдреса.Индекс          = "";
			СтруктураАдреса.Регион  	    = "";
			СтруктураАдреса.Район           = "";
			СтруктураАдреса.Город           = "";
			СтруктураАдреса.НаселенныйПункт = "";
			СтруктураАдреса.Улица           = "";
			СтруктураАдреса.Дом             = "";
			СтруктураАдреса.Корпус          = "";
			СтруктураАдреса.Квартира        = "";
			СтруктураАдреса.ТипДома         = "";
			СтруктураАдреса.ТипКорпуса      = "";
			СтруктураАдреса.ТипКвартиры     = "";
			СтруктураАдреса.Адрес			= Адрес;
		КонецЕсли;
	Иначе
		Сообщить("Для контрагнета "+Контрагент.НаименованиеПолное+" не задан Фактический адрес");
	КонецЕсли;
КонецПроцедуры

Процедура КоманднаяПанельФормыРасчет(Кнопка)
	Расчет("");
КонецПроцедуры

Функция ПривестиНомерНаПечать(Номер)
	ТемпНомер=номер;
	Пока НЕ (Лев(ТемпНомер,1)>="0" И Лев(ТемпНомер,1)<="9") Цикл
		ТемпНомер=Сред(ТемпНомер,2);
	КонецЦикла;
	Пока Лев(ТемпНомер,1)="0" Цикл
		ТемпНомер=Сред(ТемпНомер,2);
	КонецЦикла;
	Возврат ТемпНомер;
КонецФункции
//Андрей
////////////////////////////////////////////////////////////////////////////////

мСоставПоказателей = "СоставПоказателей2012Кв3";

мОписаниеТиповЧислоЕ = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(14, 2);

МассивБулево = Новый Массив;
МассивБулево.Добавить(Тип("Булево"));
мОписаниеТиповБулево    = Новый ОписаниеТипов(МассивБулево);

ОписаниеТиповСтрока  = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(0);
ОписаниеТиповСтрока254 = ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(254);
ОписаниеТиповДата      = ОбщегоНазначения.ПолучитьОписаниеТиповДаты(ЧастиДаты.Дата);
ОписаниеТиповЧисло15_5 = ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15, 5);

мИдГруппы1  = "П0000100001"; // Идентификатор многострочной группы
КолИсхСтрокГруппы1 = 1; // Исходное количество строк группы

// Структура строк многострочного раздела на странице.
СтруктураГруппы1 = Новый ТаблицаЗначений;
СтруктураГруппы1.Колонки.Добавить("П000010000100", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(6));
СтруктураГруппы1.Колонки.Добавить("П000010000101", ОписаниеТиповСтрока254);
СтруктураГруппы1.Колонки.Добавить("П000010000102", ОписаниеТиповСтрока254);
СтруктураГруппы1.Колонки.Добавить("П000010000103", ОписаниеТиповСтрока254);
СтруктураГруппы1.Колонки.Добавить("П000010000104", ОписаниеТиповСтрока254);
СтруктураГруппы1.Колонки.Добавить("П000010000105", ОписаниеТиповСтрока254);
СтруктураГруппы1.Колонки.Добавить("П000010000106", ОписаниеТиповСтрока254);
СтруктураГруппы1.Колонки.Добавить("П000010000107", ОписаниеТиповСтрока254);
СтруктураГруппы1.Колонки.Добавить("П000010000181", ОписаниеТиповСтрока254);
СтруктураГруппы1.Колонки.Добавить("П000010000182", ОписаниеТиповСтрока254);
СтруктураГруппы1.Колонки.Добавить("П000010000109", ОписаниеТиповДата);
СтруктураГруппы1.Колонки.Добавить("П000010000110", ОписаниеТиповДата);
СтруктураГруппы1.Колонки.Добавить("П000010000111", ОписаниеТиповСтрока254);
СтруктураГруппы1.Колонки.Добавить("П000010000112", ОписаниеТиповСтрока254);
СтруктураГруппы1.Колонки.Добавить("П000010000113", ОписаниеТиповЧисло15_5);
СтруктураГруппы1.Колонки.Добавить("П000010000114", ОписаниеТиповСтрока254);
СтруктураГруппы1.Колонки.Добавить("П000010000115", ОписаниеТиповДата);
СтруктураГруппы1.Колонки.Добавить("П000010000116", ОписаниеТиповСтрока254);
СтруктураГруппы1.Колонки.Добавить("П000010000118", ОписаниеТиповДата);
СтруктураГруппы1.Колонки.Добавить("П000010000117", ОписаниеТиповЧисло15_5);
СтруктураГруппы1.Колонки.Добавить("П000010000119", ОписаниеТиповСтрока254);
СтруктураГруппы1.Колонки.Добавить("П000010000120", ОписаниеТиповСтрока254);
СтруктураГруппы1.Колонки.Добавить("П000010000121", ОписаниеТиповСтрока254);
СтруктураГруппы1.Колонки.Добавить("П000010000122", ОписаниеТиповСтрока254);
СтруктураГруппы1.Колонки.Добавить("П000010000123", ОписаниеТиповЧисло15_5);
СтруктураГруппы1.Колонки.Добавить("П000010000124", ОписаниеТиповЧисло15_5);

// Структура многострочных разделов формы.
мСтруктураМногострочныхРазделов = Новый Структура;
мСтруктураМногострочныхРазделов.Вставить(мИдГруппы1, СтруктураГруппы1);

// Структура исходных строк (минимального кол-ва) многострочных разделов формы.
мСтруктураИсхКолвоСтрокРазделов = Новый Структура;
мСтруктураИсхКолвоСтрокРазделов.Вставить(мИдГруппы1, КолИсхСтрокГруппы1);

мИдГруппы2  = "П0000100003";

КолИсхСтрокГруппы2 = 1;

СтруктураГруппы2 = Новый ТаблицаЗначений;
СтруктураГруппы2.Колонки.Добавить("П000010000300", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(6));
СтруктураГруппы2.Колонки.Добавить("П000010000301", ОписаниеТиповСтрока254);
СтруктураГруппы2.Колонки.Добавить("П000010000302", ОписаниеТиповСтрока254);
СтруктураГруппы2.Колонки.Добавить("П000010000303", ОписаниеТиповСтрока254);
СтруктураГруппы2.Колонки.Добавить("П000010000304", ОписаниеТиповСтрока254);
СтруктураГруппы2.Колонки.Добавить("П000010000305", ОписаниеТиповСтрока254);
СтруктураГруппы2.Колонки.Добавить("П000010000306", ОписаниеТиповСтрока254);
СтруктураГруппы2.Колонки.Добавить("П000010000307", ОписаниеТиповСтрока254);
СтруктураГруппы2.Колонки.Добавить("П000010000381", ОписаниеТиповСтрока254);
СтруктураГруппы2.Колонки.Добавить("П000010000382", ОписаниеТиповСтрока254);
СтруктураГруппы2.Колонки.Добавить("П000010000309", ОписаниеТиповДата);
СтруктураГруппы2.Колонки.Добавить("П000010000310", ОписаниеТиповДата);
СтруктураГруппы2.Колонки.Добавить("П000010000311", ОписаниеТиповСтрока254);
СтруктураГруппы2.Колонки.Добавить("П000010000312", ОписаниеТиповСтрока254);
СтруктураГруппы2.Колонки.Добавить("П000010000313", ОписаниеТиповЧисло15_5);
СтруктураГруппы2.Колонки.Добавить("П000010000314", ОписаниеТиповСтрока254);
СтруктураГруппы2.Колонки.Добавить("П000010000315", ОписаниеТиповДата);
СтруктураГруппы2.Колонки.Добавить("П000010000316", ОписаниеТиповСтрока254);
СтруктураГруппы2.Колонки.Добавить("П000010000318", ОписаниеТиповДата);
СтруктураГруппы2.Колонки.Добавить("П000010000317", ОписаниеТиповЧисло15_5);
СтруктураГруппы2.Колонки.Добавить("П000010000319", ОписаниеТиповСтрока254);
СтруктураГруппы2.Колонки.Добавить("П000010000320", ОписаниеТиповСтрока254);
СтруктураГруппы2.Колонки.Добавить("П000010000321", ОписаниеТиповСтрока254);
СтруктураГруппы2.Колонки.Добавить("П000010000322", ОписаниеТиповСтрока254);
СтруктураГруппы2.Колонки.Добавить("П000010000323", ОписаниеТиповЧисло15_5);
СтруктураГруппы2.Колонки.Добавить("П000010000324", ОписаниеТиповЧисло15_5);

мСтруктураМногострочныхРазделов.Вставить(мИдГруппы2, СтруктураГруппы2);
мСтруктураИсхКолвоСтрокРазделов.Вставить(мИдГруппы2, КолИсхСтрокГруппы2);

ЦветЗаполняетсяТолькоВручную     = Новый Цвет(255, 255, 225);
ЦветЗаполняетсяАвтоКорректир     = Новый Цвет(255, 240, 200);
ЦветЗаполняетсяАвтоБезКорректир  = Новый Цвет(230, 240, 220);
ЦветЗаполняетсяАвтоАрифметика    = Новый Цвет(192, 220, 192); // Зеленый
ЦветЗаполняетсяОднозначноВручную = Новый Цвет(255, 255, 192); // Желтый
ЦветЗаполняетсяАвтоматически     = Новый Цвет(255, 255, 255); // Белый

// Структура цветов раскраски ячеек.
мСтруктураЦветовРаскраски = Новый Структура;
мСтруктураЦветовРаскраски.Вставить("_1", ЦветЗаполняетсяТолькоВручную);
мСтруктураЦветовРаскраски.Вставить("_2", ЦветЗаполняетсяАвтоКорректир);
мСтруктураЦветовРаскраски.Вставить("_3", ЦветЗаполняетсяАвтоБезКорректир);
мСтруктураЦветовРаскраски.Вставить("_4", ЦветЗаполняетсяАвтоАрифметика);
мСтруктураЦветовРаскраски.Вставить("_0", ЦветЗаполняетсяОднозначноВручную);
мСтруктураЦветовРаскраски.Вставить("_5", ЦветЗаполняетсяАвтоматически);

// Таблица значений хранит данные дополнительной страницы многостраничных разделов отчета.
// В колонках таблицы хранятся следующие данные:
//    - имя дополнительной страницы (отображается в списке дополнительных страниц);
//    - булево, признак текущей страницы (отображенной в поле табличного документа);
//    - структура, содержащая имена и значения редактируемых ячеек дополнительной страницы.
//    - структура, элементы которой содержат данные дополнительных строк страницы, где
//      в качестве ключа используется имя многострочного блока, а в качестве значения-
//      таблица значений показателей многострочного блока.
//

ТаблицаСтраницыРаздела            = Новый ТаблицаЗначений;
ТаблицаСтраницыРаздела.Колонки.Добавить("Представление", ОписаниеТиповСтрока254, "Наименование");
ТаблицаСтраницыРаздела.Колонки.Добавить("АктивнаяСтраница", мОписаниеТиповБулево);
ТаблицаСтраницыРаздела.Колонки.Добавить("Данные");
ТаблицаСтраницыРаздела.Колонки.Добавить("ДополнительныеДанные");
ТаблицаСтраницыРаздела.Колонки.Добавить("АвтоматическоеПредставление", мОписаниеТиповБулево);

СтрПерваяСтраница = ТаблицаСтраницыРаздела.Добавить();
СтрПерваяСтраница.Представление      = "Новая страница";
СтрПерваяСтраница.АктивнаяСтраница   = Истина;
СтрПерваяСтраница.АвтоматическоеПредставление = Истина;

ТаблицаСтраницыРазделаДляМногострочныхРазделов = Новый ТаблицаЗначений;
ТаблицаСтраницыРазделаДляМногострочныхРазделов.Колонки.Добавить("Представление",    ОписаниеТиповСтрока254, "Наименование");
ТаблицаСтраницыРазделаДляМногострочныхРазделов.Колонки.Добавить("АктивнаяСтраница", мОписаниеТиповБулево);
ТаблицаСтраницыРазделаДляМногострочныхРазделов.Колонки.Добавить("Данные");
ТаблицаСтраницыРазделаДляМногострочныхРазделов.Колонки.Добавить("ДанныеДопСтрок");
ТаблицаСтраницыРазделаДляМногострочныхРазделов.Колонки.Добавить("ДополнительныеДанные");
ТаблицаСтраницыРазделаДляМногострочныхРазделов.Колонки.Добавить("АвтоматическоеПредставление", мОписаниеТиповБулево);

ТаблицаСтраницыЛицензии = ТаблицаСтраницыРаздела.Скопировать();

ТаблицаСтраницыДекларация = ТаблицаСтраницыРазделаДляМногострочныхРазделов.Скопировать();
СтрПерваяСтраница = ТаблицаСтраницыДекларация.Добавить();
СтрПерваяСтраница.Представление = "Новая страница";
СтрПерваяСтраница.ДанныеДопСтрок = Неопределено; // В разделе есть многострочность
СтрПерваяСтраница.АктивнаяСтраница = Истина;
СтрПерваяСтраница.АвтоматическоеПредставление = Истина;

мСтруктураМногостраничныхРазделов = Новый Структура;
мСтруктураМногостраничныхРазделов.Вставить("Лицензии", ТаблицаСтраницыЛицензии);
мСтруктураМногостраничныхРазделов.Вставить("Декларация", ТаблицаСтраницыДекларация);

мВерсияФормы = "13/09/2012";

ТаблицаСообщений = Новый ТаблицаЗначений;
ТаблицаСообщений.Колонки.Добавить("Раздел");
ТаблицаСообщений.Колонки.Добавить("Графа");
ТаблицаСообщений.Колонки.Добавить("Строка");
ТаблицаСообщений.Колонки.Добавить("Страница");
ТаблицаСообщений.Колонки.Добавить("СтрокаПП");
ТаблицаСообщений.Колонки.Добавить("ИмяЯчейки");
ТаблицаСообщений.Колонки.Добавить("Описание");

мПечатныеформы = Новый СписокЗначений;
мПечатаемыеРазделы = Новый СписокЗначений;
